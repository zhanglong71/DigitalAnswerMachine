.LIST
REMOTE_PRO:
	LAC	MSG			;接线后按ON/OFF(相当于CPC)
	XORL	CMSG_KEY6S
	BS	ACZ,REMOTE_CPC_RUN
	
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_CPC_RUN
	
	LAC	PRO_VAR
	ANDL	0X0F
	BS	ACZ,REMOTE_PRO0		;0 = IDEL
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW	;1 = play new
	SBHK	1
	BS	ACZ,REMOTE_PROPALL	;2 = play all
	SBHK	1
	BS	ACZ,REMOTE_PROMREC	;3 = record MEMO
	SBHK	4
	BS	ACZ,REMOTE_PROOGM	;7 = record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PROROMMONITOR	;8 = room monitor
	SBHK	1
	BS	ACZ,REMOTE_PRO9		;9 = reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO10	;10 = exit
	
	RET
REMOTE_CPC_RUN:
	LAC	PRO_VAR
	ANDL	0X0F
	SBHK	1
	BS	ACZ,REMOTE_CPC_RUN_PLYVP
	SBHK	1
	BS	ACZ,REMOTE_CPC_RUN_PLYVP
	
	BS	B1,REMOTE_PRO10_END
REMOTE_CPC_RUN_PLYVP:
	CALL	INIT_DAM_FUNC

	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	BS	B1,REMOTE_PRO10_END
;===============================================================================
REMOTE_PRO0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL(发两声BEEP)
	BS	ACZ,REMOTE_PRO0_INIT
;REMOTE_PRO0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO0_REV_DTMF
;REMOTE_PRO0_2:	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PRO0_STOPVP	;VP完
;REMOTE_PRO0_3:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
;REMOTE_PRO0_4:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
;REMOTE_PRO0_5:	
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,REMOTE_PRO0_TMR
		
	RET
REMOTE_PRO0_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP		;BB
	
	RET
REMOTE_PRO0_STOPVP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	CALL	LINE_START
	
	LACK	0			;开始计时
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	CALL	BCVOX_INIT
	
	RET
	
REMOTE_PRO0_REV_DTMF:
	CALL	CLR_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	CALL	BCVOX_INIT
	
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	BS	ACZ,REMOTE_PRO0_INDOORMONITOR	;*---in door monitor
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PLYNEW		;1---play new
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PLYALL		;2---play all
	SBHK	1
	BS	ACZ,REMOTE_PRO0_ERAOLD		;3---erase all
	SBHK	4
	BS	ACZ,REMOTE_PRO0_RECOGM		;7---record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO0_RECMEMO		;8---record memo
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PROONOFF	;9---on/off
	SBHK	6
	BS	ACZ,REMOTE_PRO0_REV_DTMF_B	;#---hang off	
	
	RET

REMOTE_PRO0_PLYNEW:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	CLR_FLAG
	
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,REMOTE_PRO0_PLYNEW_1

	CALL	BBBEEP		;BBB
	
	LACK	0
	SAH	PRO_VAR
	RET
REMOTE_PRO0_PLYNEW_1:
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	1
	SAH	PRO_VAR

	LACK	0
	SAH	MSG_ID

	LACL	0X005
	CALL	STOR_VP
	
	RET	
	
REMOTE_PRO0_PLYALL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	VPMSG_CHK

	LACK	0
	SAH	MSG_ID
	
	BIT	ANN_FG,14
	BS	TB,REMOTE_PRO0_REV_DTMF_2_1

	CALL	BBBEEP		;BBB
	LACK	0
	SAH	PRO_VAR
	
	BS	B1,REMOTE_PRO0_REV_DTMF_2_END
REMOTE_PRO0_REV_DTMF_2_1:
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	2
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP
REMOTE_PRO0_REV_DTMF_2_END:
	
	RET

REMOTE_PRO0_ERAOLD:
	CALL	INIT_DAM_FUNC
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	LACL	0X6080
	SAH	CONF
	CALL	DAM_BIOS

	CALL	VPMSG_CHK
	BIT	ANN_FG,14
	BS	TB,REMOTE_PRO0_REV_DTMF_3_1

	CALL	BBBEEP		;BBB
	
	BS	B1,REMOTE_PRO0_REV_DTMF_3_END
REMOTE_PRO0_REV_DTMF_3_1:	

	CALL	BBEEP		;BB
REMOTE_PRO0_REV_DTMF_3_END:	
	CALL	DAA_LIN_SPK
	
	RET

REMOTE_PRO0_RECOGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	BIT	ANN_FG,13
	BS	TB,REMOTE_PROX_DTMF_MFUL
	
	LACK	0X0007
	SAH	PRO_VAR
	CALL	LBEEP		;Lbee...

	
	RET

REMOTE_PRO0_RECMEMO:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	BIT	ANN_FG,13
	BS	TB,REMOTE_PROX_DTMF_MFUL
	
	LACK	0X0003
	SAH	PRO_VAR
	CALL	LBEEP		;Lbee...

	RET
REMOTE_PROX_DTMF_MFUL:		;mfull 直接退出
	CALL	BBBEEP
	LACK	0
	SAH	PRO_VAR

	RET

REMOTE_PRO0_INDOORMONITOR:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	LBEEP		;Lbee...
		
	LACK	0X0008
	SAH	PRO_VAR
	
	RET

REMOTE_PRO0_PROONOFF:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LAC	EVENT
	XORL	1<<9
	SAH	EVENT

	BIT	EVENT,9
	BS	TB,REMOTE_PRO0_PROOFF
;REMOTE_PRO0_PROON:
	CALL	BEEP
	CALL	BEEP
	
	RET
REMOTE_PRO0_PROOFF:
	CALL	BEEP
	CALL	BEEP
	CALL	BEEP
	
	RET
	
REMOTE_PRO0_REV_DTMF_A:
	CALL	INIT_DAM_FUNC	
	CALL	DAA_LIN_SPK
	
	LACL	0X005
	CALL	STOR_VP

	RET
REMOTE_PRO0_REV_DTMF_B:		;退出准备
	
	RET
;---
REMOTE_PRO0_EXIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
		
	LACK	10
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP

	RET
REMOTE_PRO0_TONE:
	BS	B1,REMOTE_PRO0_EXIT
REMOTE_PRO0_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	;SBHK	10
	SBHK	20		;20s无操作退出
	BZ	SGN,REMOTE_PRO0_EXIT
	
	RET	
;=======================================================================
;11111111111111111111111111111111111111111111111111111111111111111111111
REMOTE_PROPNEW:				;播放新message
	LAC	PRO_VAR
	SFR	4
	ANDK	0XF
	BS	ACZ,REMOTE_PROPNEW0
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE		;功能暂停
	SBHK	1
	BS	ACZ,REMOTE_PROXTMR_PAUSE	;时间暂停
	
	RET
REMOTE_PROPNEW0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PROPNEW_OVER
;REMOTE_PROPNEW0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROPNEW_DTMF
;REMOTE_PROPNEW0_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PRO10_END
;REMOTE_PROPNEW0_3:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROPNEW0_TMR
	
	RET
;---------------处理消息
REMOTE_PROPNEW0_TMR:
REMOTE_PROPX_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHL	160		;2分40秒
	;SBHL	16
	BZ	SGN,REMOTE_PROPX_TMRPASUE
	
	RET
REMOTE_PROPX_TMRPASUE:
	LAC	CONF
	ORL	1<<8
	SAH	MSG
	CALL	DAM_BIOSFUNC
	
	CALL	PAUBEEP		;BEEP Prompt

	LAC	PRO_VAR
	ORK	0X20
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	RET

REMOTE_PROPNEW_OVER:
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,REMOTE_PROX_PLYSTOP
;REMOTE_PRO1_PLAY_OVER1:

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

	CALL	FFW_MANAGE	
	BZ	ACZ,REMOTE_PROX_PLYSTOP	;没新的了

	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP
	
	RET	
;-------
REMOTE_PROPNEW_DTMF:	
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	3
	BS	ACZ,REMOTE_PROX_ERAPLYING	;3#---erase playing message
	SBHK	3
	BS	ACZ,REMOTE_PROPNEW_PLYPRE	;6#---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_NEXT		;7#---skip to next message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_REPEAT	;8#---repeat message
	SBHK	6
	BS	ACZ,REMOTE_PROX_DO_PAUSE	;*#---remote prompt
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP	;##---hang off	
	
	RET
;---------------
REMOTE_PROX_ERAPLYING:
	LAC	MSG_ID
	CALL	SET_DELMARK
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	
	RET
;-------
REMOTE_PROPNEW_PLYPRE:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_PLYPRE_1

	LAC	MSG_ID
	SBHK	1
	SAH	MSG_ID	

	CALL	REW_MANAGE
REMOTE_PROPNEW_PLYPRE_1:
	BS	B1,REMOTE_PROX_LOADVP
;-------
REMOTE_PROPNEW_REPEAT:
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROX_LOADVP
;-------
REMOTE_PROPNEW_NEXT:
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPNEW_OVER
;-------
REMOTE_PROX_DO_PAUSE:
	LAC	CONF
	ORL	0X0100
	SAH	CONF
	CALL	DAM_BIOS
	
	LAC	PRO_VAR
	ORK	0X10
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	RET

REMOTE_PROX_PAUSE:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROX_PAUSE_DTMF
REMOTE_PROX_PAUSE_1:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROX_PAUSE_TIMER
	
	RET
;---------------
REMOTE_PROX_PAUSE_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	3
	BS	ACZ,REMOTE_PROX_ERAPLYING	;3#---erase playing message
	SBHK	3
	BS	ACZ,REMOTE_PROPNEW_PLYPRE	;6#---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_NEXT		;7#---skip to next message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_REPEAT	;8#---repeat message
	SBHK	6
	BS	ACZ,REMOTE_PROX_DO_PAUSE	;*#---play again
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP		;##---hang off	
	
	RET
;---------------
REMOTE_PROX_PAUSE_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10
	BS	SGN,REMOTE_PROX_PAUSE_TIMER_1
	
	CALL	INIT_DAM_FUNC
	CALL	BBEEP
	LACK	0
	SAH	PRO_VAR

REMOTE_PROX_PAUSE_TIMER_1:	
	RET
REMOTE_PROX_PAUSE_PLY:
	LAC	CONF
	ANDL	~(1<<8)
	CALL	DAM_BIOSFUNC
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	RET
;-------------------------------------------------------------------------------
REMOTE_PROXTMR_PAUSE:		;时间暂停
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROXTMR_PAUSE_DTMF
REMOTE_PROXTMR_PAUSE_1:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROXTMR_PAUSE_TIMER

	RET
;---------------
REMOTE_PROXTMR_PAUSE_DTMF:
	LAC	CONF
	ANDL	~(1<<8)
	CALL	DAM_BIOSFUNC
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1

	RET
REMOTE_PROXTMR_PAUSE_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	8
	BZ	SGN,REMOTE_PROXTMR_PAUSE_TIMER_EXIT
	
	RET
REMOTE_PROXTMR_PAUSE_TIMER_EXIT:
	CALL	INIT_DAM_FUNC

	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	BS	B1,REMOTE_PRO0_EXIT

;=======================================================================
;22222222222222222222222222222222222222222222222222222222222222222222222
REMOTE_PROPALL:
	LAC	PRO_VAR
	SFR	4
	ANDK	0XF
	BS	ACZ,REMOTE_PROPALL0		;播放
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE		;功能暂停
	SBHK	1
	BS	ACZ,REMOTE_PROXTMR_PAUSE	;时间暂停

	RET
REMOTE_PROPALL0:	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PROPALL_OVER
REMOTE_PROPALL0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROPALL_DTMF
REMOTE_PROPALL0_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PRO10_END
REMOTE_PROPALL0_3:	
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROPALL0_TMR
	
	
	RET
;---------------处理消息
REMOTE_PROPALL0_TMR:
	BS	B1,REMOTE_PROPX_TMR

REMOTE_PROPALL_OVER:	
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,REMOTE_PROX_PLYSTOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID
	
REMOTE_PROX_LOADVP:
	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP
	
	RET

REMOTE_PROPALL_DTMF:	
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	3
	BS	ACZ,REMOTE_PROX_ERAPLYING	;3#---erase playing message
	SBHK	3
	BS	ACZ,REMOTE_PROPALL_PLYPRE	;6#---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_NEXT		;7#---skip to next message
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_REPEAT	;8#---repeat message
	SBHK	6
	BS	ACZ,REMOTE_PROX_DO_PAUSE	;*#---do pause
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP		;##---hang off	
	
	RET
;---------------
REMOTE_PROPALL_PLYPRE:		;前一个
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_PLYPRE_1 ;第一个吗?
	
	LAC	MSG_ID
	SBHK	1
	SAH	MSG_ID	

REMOTE_PROPALL_PLYPRE_1:
	BS	B1,REMOTE_PROX_LOADVP
;-------
REMOTE_PROPALL_REPEAT:		;重复当前的一个
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROX_LOADVP
;-------
REMOTE_PROPALL_NEXT:		;下一个
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPALL_OVER
;-------
REMOTE_PROX_PLYSTOP:
	CALL	INIT_DAM_FUNC
	CALL	BBEEP		;BB
	CALL	VPMSG_CHK
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1
	
	RET
;=======================================================================
;33333333333333333333333333333333333333333333333333333333333333333333333
REMOTE_PROMREC:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,REMOTE_PROMREC0	;play LBEEP
	SBHK	1
	BS	ACZ,REMOTE_PROMREC1	;record MEMO
	SBHK	1
	BS	ACZ,REMOTE_PROMREC2	;record end BEEP
	RET
;---------------------------------------
REMOTE_PROMREC0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PROMREC0_OVER
	
	RET
;---	
REMOTE_PROMREC0_OVER:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	LACK	0X0013
	SAH	PRO_VAR

	CALL	REC_START

	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1

	RET
REMOTE_PROMREC1:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PROMREC1_TIMER
REMOTE_PROMREC1_1:
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,REMOTE_PROMREC1_DTMF
REMOTE_PROMREC1_2:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PROMREC1_3:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PROMREC1_4:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,REMOTE_PROMREC1_STOP
	
	RET
;---------------
REMOTE_PROMREC1_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	60
	BZ	SGN,REMOTE_PROMREC1_TIMEROVER
	
	RET
REMOTE_PROMREC1_TIMEROVER:
	BS	B1,REMOTE_PROMREC1_STOP
;---
REMOTE_PROMREC1_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	8
	BS	ACZ,REMOTE_PROMREC1_STOP	;pressed "8","#"

	RET
;---
REMOTE_PROMREC1_STOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	CALL	BEEP
	LACK	0X003F		;延时
	CALL	STOR_VP

	CALL	VPMSG_CHK
	
	LACK	0X0023
	SAH	PRO_VAR
	
	RET
REMOTE_PROMREC2:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,REMOTE_PROMREC2_VPSTOP	;beep播放完毕
	
	
	RET
REMOTE_PROMREC2_VPSTOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP

	LACK	0X0
	SAH	PRO_VAR
	
	RET
;=======================================================================
;44444444444444444444444444444444444444444444444444444444444444444444444
;REMOTE_PRO4:
	RET
;=======================================================================
;55555555555555555555555555555555555555555555555555555555555555555555555
;REMOTE_PRO5:
	RET
;=======================================================================
;66666666666666666666666666666666666666666666666666666666666666666666666
;REMOTE_PRO6:
	RET
;=======================================================================
;77777777777777777777777777777777777777777777777777777777777777777777777
REMOTE_PROOGM:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,REMOTE_PRO7_0	;play LBEEP
	SBHK	1
	BS	ACZ,REMOTE_PRO7_RECORD	;record OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO7_2	;play OGM
	
	RET
;-------
REMOTE_PRO7_RECORD:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRO7_RECORD_TIMER
REMOTE_PRO7_RECORD_1:
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,REMOTE_PRO7_RECORD_DTMF
REMOTE_PRO7_RECORD_2:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO7_RECORD_3:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO7_RECORD_4:	
	;LAC	MSG
	;XORL	CMSG_TMR
	;BS	ACZ,REMOTE_PRO0_TMR
;REMOTE_PRO7_RECORD_5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,REMOTE_PRO7_RECOOGM_STOP

	RET
REMOTE_PRO7_RECORD_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	60
	BZ	SGN,REMOTE_PRO7_RECOOGM_STOP

	RET
;-------	
REMOTE_PRO7_RECORD_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	7
	BS	ACZ,REMOTE_PRO7_RECOOGM_STOP	;pressed "7","#"

	RET
REMOTE_PRO7_RECOOGM_STOP:		;处理"#","mfull"
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LACK	0X0027
	SAH	PRO_VAR
	
	CALL	OGM_SELECT
	
	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	CALL	OGM_SELECT
	BZ	ACZ,MAIN_PRO0_PLAY_OGM1
	
	CALL	INIT_DAM_FUNC
	CALL	BBBEEP
	
	LACK	0X0
	SAH	PRO_VAR
MAIN_PRO0_PLAY_OGM1:

	RET	

;-------	
REMOTE_PRO7_0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRO7_PLAY_OVER
	
	RET
REMOTE_PRO7_PLAY_OVER:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	LACK	0X0017
	SAH	PRO_VAR
;---
	CALL	OGM_SELECT
;-------delete the old OGM fisrt------------
	LAC	MSG_ID
	CALL	VPMSG_DEL
	CALL	GC_CHK
;---set user index data0"OGM_ID"------------
    	LAC	MSG_N
    	ORL	0X8D00
    	CALL	DAM_BIOSFUNC
;---	
	CALL	REC_START
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	RET
	
REMOTE_PRO7_2:
	LAC	MSG
	XORL	CVP_STOP		;CVP_STOP
	BS	ACZ,REMOTE_PRO7_VPSTOP
REMOTE_PRO7_2_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO7_2_REV_DTMF
	
	RET
REMOTE_PRO7_VPSTOP:
	CALL	INIT_DAM_FUNC	
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1

	RET
REMOTE_PRO7_2_REV_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SBHK	3
	BS	ACZ,REMOTE_PRO7_2_REV_DTMF_3	;"3","#"
	SBHK	12
	BS	ACZ,REMOTE_PRO7_VPSTOP	;#

	RET
REMOTE_PRO7_2_REV_DTMF_3:
	CALL	INIT_DAM_FUNC
	CALL	OGM_SELECT
;---delete the old OGM fisrt------------
	LAC	MSG_ID
	CALL	VPMSG_DEL
	CALL	GC_CHK

	CALL	BBEEP		;BB
	LACK	0
	SAH	PRO_VAR
	
	RET
;=======================================================================
;88888888888888888888888888888888888888888888888888888888888888888888888
REMOTE_PROROMMONITOR:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,REMOTE_PRO8_VOPLAY
	SBHK	1
	BS	ACZ,REMOTE_PROROMMONITING
	
	RET
;---------------------------------------
REMOTE_PROROMMONITING:

;REMOTE_PRORMONIT_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRORMONIT_DTMF
;REMOTE_PRORMONIT_2:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRORMONIT_TIMER
;REMOTE_PRORMONIT_3:	

	RET
;---------------处理消息
REMOTE_PRORMONIT_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	BS	ACZ,REMOTE_PRORMONIT_RESTART	;"0","#"
	SBHK	5
	BS	ACZ,REMOTE_PRORMONIT_STOP	;"5","#"

	RET

REMOTE_PRORMONIT_RESTART:
	LACK	0
	SAH	PRO_VAR1

	RET

REMOTE_PRORMONIT_STOP:
	CALL	INIT_DAM_FUNC
	CALL	BCVOX_INIT
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1
	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET

REMOTE_PRORMONIT_TIMER:
	LACL	CTMR_CTONE	;---不让产生VOX,CTONE消息,可产生BTONE
	SAH	TMR_VOX
	SAH	TMR_CTONE
	
	LAC	PRO_VAR1	;计时
	ADHK	1
	SAH	PRO_VAR1
	SBHK	30		;30s计时
	BZ	SGN,REMOTE_PRORMONIT_STOP
		
	RET
;-------
REMOTE_PRO8_VOPLAY:
	LAC	MSG
	XORL	CVP_STOP			;CVP_STOP
	BS	ACZ,REMOTE_PRO8_VOPLAY_END
	
	RET	
REMOTE_PRO8_VOPLAY_END:
	CALL	INIT_DAM_FUNC
	CALL	DAA_ROM_MOR
	CALL	LINE_START
	
	LACK	0X0018
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1


	RET	
;=======================================================================
;99999999999999999999999999999999999999999999999999999999999999999999999
REMOTE_PRO9:	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	RET
;=======================================================================
;#######################################################################
REMOTE_PRO10:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PRO10_END
	
	RET
REMOTE_PRO10_END:
	CALL	INIT_DAM_FUNC	
	CALL	CLR_FUNC	;先空
	LACL	LOCAL_PRO	;进入本地操作
     	CALL	PUSH_FUNC	
	LACK	0
	SAH	PRO_VAR

	;LACL	0X0018
	;CALL	STOR_VP		;延时192ms
	LACL	CMSG_INIT
	CALL	STOR_MSG

	CALL	DAA_OFF	
	CALL	HOOK_OFF

	RET
;-------------------------------------------------------------------------------
.END
	