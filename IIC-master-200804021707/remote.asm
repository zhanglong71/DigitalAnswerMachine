.LIST
REMOTE_PRO:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_KEYAS
	BS	ACZ,REMOTE_CPC_RUN
	
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_CPC_RUN
	
	LAC	PRO_VAR
	ANDL	0X0F
	BS	ACZ,REMOTE_PRO0		;IDEL
	SBHK	1
	BS	ACZ,REMOTE_PRO1		;play new
	SBHK	1
	BS	ACZ,REMOTE_PRO2		;play all
	SBHK	1
	BS	ACZ,REMOTE_PRO3		;reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO4		;reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO5		;reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO6		;reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO7		;record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO8		;room monitor
	SBHK	1
	BS	ACZ,REMOTE_PRO9		;reserved
	SBHK	1
	BS	ACZ,REMOTE_PRO10	;exit
	
	RET
REMOTE_CPC_RUN:
	LAC	PRO_VAR
	ANDL	0X0F
	SBHK	1
	BS	ACZ,REMOTE_CPC_RUN_PLYVP
	SBHK	1
	BS	ACZ,REMOTE_CPC_RUN_PLYVP
	
	BS	B1,REMOTE_PRO10_END
REMOTE_CPC_RUN_PLYVP:
	CALL	INIT_DAM_FUNC

	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	BS	B1,REMOTE_PRO10_END
;===============================================================================
REMOTE_PRO0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL(发两声BEEP)
	BS	ACZ,REMOTE_PRO0_INIT
REMOTE_PRO0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO0_REV_DTMF
REMOTE_PRO0_2:	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PRO0_STOPVP	;VP完
REMOTE_PRO0_3:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO0_4:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO0_5:	
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,REMOTE_PRO0_TMR
		
	RET
REMOTE_PRO0_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP		;BB
	
	RET
REMOTE_PRO0_STOPVP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	CALL	LINE_START
	
	LACK	0			;开始计时
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	CALL	BCVOX_INIT
	
	RET
	
REMOTE_PRO0_REV_DTMF:
	CALL	CLR_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	CALL	BCVOX_INIT
	
	LAC	DTMF_VAL
	SBHL	0X0F0
	BS	ACZ,REMOTE_PRO0_REV_DTMF_0	;0
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_1	;1---play new
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_2	;2---play all
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_3	;3---erase all
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_4	;4
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_5	;5
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_6	;6
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_7	;7---record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_8	;8---indoormonitor
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_9	;9---on/off
	SBHK	5
	BS	ACZ,REMOTE_PRO0_REV_DTMF_A	;*---remote prompt
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_B	;#---hang off	
	
	RET
REMOTE_PRO0_REV_DTMF_0:
	RET
REMOTE_PRO0_REV_DTMF_1:
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK
	CALL	CLR_FLAG
	
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,REMOTE_PRO0_REV_DTMF_1_1

	CALL	BBBEEP		;BBB
	
	LACK	0
	SAH	PRO_VAR
	BS	B1,REMOTE_PRO0_REV_DTMF_1_END
REMOTE_PRO0_REV_DTMF_1_1:
	LACK	1
	SAH	PRO_VAR

	LACK	0
	SAH	MSG_ID

	LACL	0X005
	CALL	STOR_VP
REMOTE_PRO0_REV_DTMF_1_END:
	CALL	DAA_LIN_SPK
		
	RET	
REMOTE_PRO0_REV_DTMF_2:	
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK

	LACK	0
	SAH	MSG_ID
	
	BIT	ANN_FG,14
	BS	TB,REMOTE_PRO0_REV_DTMF_2_1

	CALL	BBBEEP		;BBB
	LACK	0
	SAH	PRO_VAR
	
	BS	B1,REMOTE_PRO0_REV_DTMF_2_END
REMOTE_PRO0_REV_DTMF_2_1:
	LACK	2
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP
REMOTE_PRO0_REV_DTMF_2_END:
	CALL	DAA_LIN_SPK
	
	RET

REMOTE_PRO0_REV_DTMF_3:
	CALL	INIT_DAM_FUNC
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	LACL	0X6080
	SAH	CONF
	CALL	DAM_BIOS

	CALL	VPMSG_CHK
	BIT	ANN_FG,14
	BS	TB,REMOTE_PRO0_REV_DTMF_3_1

	CALL	BBBEEP		;BBB
	
	BS	B1,REMOTE_PRO0_REV_DTMF_3_END
REMOTE_PRO0_REV_DTMF_3_1:	

	CALL	BBEEP		;BB
REMOTE_PRO0_REV_DTMF_3_END:	
	CALL	DAA_LIN_SPK
	
	RET
REMOTE_PRO0_REV_DTMF_4:
	
	RET
REMOTE_PRO0_REV_DTMF_5:
	
	RET
REMOTE_PRO0_REV_DTMF_6:
	
	RET
REMOTE_PRO0_REV_DTMF_7:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	BIT	ANN_FG,13
	BS	TB,REMOTE_PRO0_REV_DTMF_7_FUL
	
	LACK	0X0007
	SAH	PRO_VAR
	CALL	LBEEP		;Lbee...

	
	RET
REMOTE_PRO0_REV_DTMF_7_FUL:	;mfull 直接退出
	CALL	BBBEEP
	LACK	0
	SAH	PRO_VAR

	RET
REMOTE_PRO0_REV_DTMF_8:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	LBEEP		;Lbee...
		
	LACK	0X0008
	SAH	PRO_VAR
	
	RET

REMOTE_PRO0_REV_DTMF_9:
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	
	RET
REMOTE_PRO0_REV_DTMF_A:
	CALL	INIT_DAM_FUNC	
	CALL	DAA_LIN_SPK
	
	LACL	0X005
	CALL	STOR_VP

	RET
REMOTE_PRO0_REV_DTMF_B:		;退出准备
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
		
	LACK	10
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP

	RET
REMOTE_PRO0_TONE:
	BS	B1,REMOTE_PRO0_REV_DTMF_B
REMOTE_PRO0_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10
	BZ	SGN,REMOTE_PRO0_REV_DTMF_B
	
	RET	
;=======================================================================
;11111111111111111111111111111111111111111111111111111111111111111111111
REMOTE_PRO1:				;播放新message
	LAC	PRO_VAR
	SFR	4	
	BZ	ACZ,REMOTE_PROX_PAUSE
	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRO1_PLAY_OVER
REMOTE_PRO1_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO1_REV_DTMF
REMOTE_PRO1_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PRO10_END
	RET
;---------------处理消息
REMOTE_PRO1_PLAY_OVER:

	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,REMOTE_PROX_REV_DTMF_POUND
REMOTE_PRO1_PLAY_OVER1:

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

	CALL	FFW_MANAGE	
	BZ	ACZ,REMOTE_PROX_REV_DTMF_POUND	;没新的了

	CALL	BEEP
REMOTE_PRO1_PLAY_OVER_END:	
	RET	
;-------	
REMOTE_PRO1_REV_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0F0
	SBHK	3
	BS	ACZ,REMOTE_PRO1_REV_DTMF_3	;3
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_4	;4
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_5	;5
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_6	;6
	SBHK	8
	BS	ACZ,REMOTE_PROX_REV_DTMF_STAR	;*
	SBHK	1
	BS	ACZ,REMOTE_PROX_REV_DTMF_POUND	;#
	RET
REMOTE_PRO1_REV_DTMF_3:
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	
	RET
REMOTE_PRO1_REV_DTMF_4:
	CALL	INIT_DAM_FUNC
	
	LAC	MSG_ID
	ADH	FILE_LEN
	ANDL	0X0FF
	SBH	MSG_T
	BZ	SGN,REMOTE_PRO1_REV_DTMF_4_1

	LAC	MSG_ID
	ADH	FILE_LEN
	ADHK	1
	SAH	MSG_ID	

	CALL	REW_MANAGE
REMOTE_PRO1_REV_DTMF_4_1:
	CALL	BEEP

	RET
REMOTE_PRO1_REV_DTMF_5:
	CALL	INIT_DAM_FUNC
	CALL	BEEP

	RET
REMOTE_PRO1_REV_DTMF_6:
	CALL	INIT_DAM_FUNC

	LACL	CVP_STOP
	CALL	STOR_MSG
	
	RET

REMOTE_PROX_REV_DTMF_STAR:
	LAC	CONF
	ORL	0X0100
	SAH	CONF
	CALL	DAM_BIOS
	
	LAC	PRO_VAR
	ORK	0X10
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	RET

REMOTE_PROX_PAUSE:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO1_PAUSE_REV_DTMF
REMOTE_PRO1_PAUSE_1:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PRO1_PAUSE_TIMER
	
	RET
REMOTE_PRO1_PAUSE_REV_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0F0
	SBHK	3
	BS	ACZ,REMOTE_PRO1_REV_DTMF_3	;3	
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_4	;4
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_5	;5
	SBHK	1
	BS	ACZ,REMOTE_PRO1_REV_DTMF_6	;6
	SBHK	8
	BS	ACZ,REMOTE_PRO1_PAUSE_REV_DTMF_STAR	;*
	SBHK	1
	BS	ACZ,REMOTE_PROX_REV_DTMF_POUND	;#
	
	RET
REMOTE_PRO1_PAUSE_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10
	BS	SGN,REMOTE_PRO1_PAUSE_TIMER_1
	
	CALL	INIT_DAM_FUNC
	CALL	BBEEP
	LACK	0
	SAH	PRO_VAR
	
REMOTE_PRO1_PAUSE_TIMER_1:		
	RET
REMOTE_PRO1_PAUSE_REV_DTMF_STAR:
	
	CRAM	CONF,8
	CALL	DAM_BIOS
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	RET
;=======================================================================
;22222222222222222222222222222222222222222222222222222222222222222222222
REMOTE_PRO2:
	LAC	PRO_VAR
	SFR	4	
	BZ	ACZ,REMOTE_PROX_PAUSE
	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRO2_PLAY_OVER
REMOTE_PRO2_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO2_REV_DTMF
REMOTE_PRO2_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PRO10_END
	RET
;-------
REMOTE_PRO2_PLAY_OVER:
	
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,REMOTE_PROX_REV_DTMF_POUND
REMOTE_PRO2_PLAY_OVER1:
	
	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP
	
	RET
	
REMOTE_PRO2_REV_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0F0
	SBHK	3
	BS	ACZ,REMOTE_PRO2_REV_DTMF_3	;3
	SBHK	1
	BS	ACZ,REMOTE_PRO2_REV_DTMF_4	;4
	SBHK	1
	BS	ACZ,REMOTE_PRO2_REV_DTMF_5	;5
	SBHK	1
	BS	ACZ,REMOTE_PRO2_REV_DTMF_6	;6
	SBHK	8
	BS	ACZ,REMOTE_PROX_REV_DTMF_STAR	;*
	SBHK	1
	BS	ACZ,REMOTE_PROX_REV_DTMF_POUND	;#
	RET
REMOTE_PRO2_REV_DTMF_3:
	LAC	MSG_ID
	CALL	SET_DELMARK
	CALL	INIT_DAM_FUNC
	
	CALL	BEEP
	
	RET
REMOTE_PRO2_REV_DTMF_4:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	ADH	FILE_LEN
	ANDL	0X0FF
	SBH	MSG_T
	BZ	SGN,REMOTE_PRO2_PLAY_NEXT_1 ;第一个吗?
	
	LAC	MSG_ID
	ADH	FILE_LEN
	ADHK	1
	SAH	MSG_ID	
	
REMOTE_PRO2_PLAY_NEXT_1:

	CALL	BEEP

	RET
REMOTE_PRO2_REV_DTMF_5:
	BS	B1,REMOTE_PRO1_REV_DTMF_5
	;RET
REMOTE_PRO2_REV_DTMF_6:
	CALL	INIT_DAM_FUNC

	LACL	CVP_STOP
	CALL	STOR_MSG
	
	RET

REMOTE_PROX_REV_DTMF_POUND:
	CALL	INIT_DAM_FUNC

	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	CALL	BBEEP		;BB

	LACK	0
	SAH	PRO_VAR
	
	RET
;=======================================================================
;33333333333333333333333333333333333333333333333333333333333333333333333
REMOTE_PRO3:	
	RET
;=======================================================================
;44444444444444444444444444444444444444444444444444444444444444444444444
REMOTE_PRO4:
	RET
;=======================================================================
;55555555555555555555555555555555555555555555555555555555555555555555555
REMOTE_PRO5:
	RET
;=======================================================================
;66666666666666666666666666666666666666666666666666666666666666666666666
REMOTE_PRO6:
	RET
;=======================================================================
;77777777777777777777777777777777777777777777777777777777777777777777777
REMOTE_PRO7:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,REMOTE_PRO7_0	;play LBEEP
	SBHK	1
	BS	ACZ,REMOTE_PRO7_RECORD	;record OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO7_2	;play OGM
	
	RET
;-------
REMOTE_PRO7_RECORD:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRO7_RECORD_TIMER
REMOTE_PRO7_RECORD_1:
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,REMOTE_PRO7_RECORD_DTMF
REMOTE_PRO7_RECORD_2:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO7_RECORD_3:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
REMOTE_PRO7_RECORD_4:	
	;LAC	MSG
	;XORL	CMSG_TMR
	;BS	ACZ,REMOTE_PRO0_TMR
;REMOTE_PRO7_RECORD_5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,REMOTE_PRO7_RECOOGM_STOP

	RET
REMOTE_PRO7_RECORD_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	120
	BZ	SGN,REMOTE_PRO7_RECOOGM_STOP

	RET
;-------	
REMOTE_PRO7_RECORD_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0FF
	BS	ACZ,REMOTE_PRO7_RECOOGM_STOP	;pressed "#"?
	
	RET
REMOTE_PRO7_RECOOGM_STOP:		;处理"#","mfull"
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK

	LACK	0X0027
	SAH	PRO_VAR

	BS	B1,PROOGM_LOADOGM

;-------	
REMOTE_PRO7_0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRO7_PLAY_OVER
	
	RET
REMOTE_PRO7_PLAY_OVER:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	LACK	0X0017
	SAH	PRO_VAR
	
	BS	B1,PROOGM_RECREADY

REMOTE_PRO7_2:
	LAC	MSG
	XORL	CVP_STOP		;CVP_STOP
	BS	ACZ,REMOTE_PRO7_VPSTOP
REMOTE_PRO7_2_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO7_2_REV_DTMF
	
	RET
REMOTE_PRO7_VPSTOP:
	CALL	INIT_DAM_FUNC	
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1

	RET
REMOTE_PRO7_2_REV_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0F0
	SBHK	3
	BS	ACZ,REMOTE_PRO7_2_REV_DTMF_3	;3
	SBHK	12
	BS	ACZ,REMOTE_PRO7_VPSTOP	;#
	
	RET
REMOTE_PRO7_2_REV_DTMF_3:
	CALL	INIT_DAM_FUNC
	
	CALL	OGM_STATUS
;-------delete the OGM----------------	
	LAC	MSG_ID
	ORL	0X2080
	SAH	CONF
	CALL	DAM_BIOS
	
	LAC	CONF
	ANDL	0X007F
	ORL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	CALL	BBEEP		;BB
	LACK	0
	SAH	PRO_VAR
	
	RET

;=======================================================================
;88888888888888888888888888888888888888888888888888888888888888888888888
REMOTE_PRO8:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,REMOTE_PRO8_VOPLAY
	;SBHK	1
	;BS	ACZ,REMOTE_PRO8_VOPLAY
	
	CALL	BCVOX_INIT
;---------------------------------------
REMOTE_PRO8_1:	
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO8_REV_DTMF
REMOTE_PRO8_1_1:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRO8_TIMER
REMOTE_PRO8_1_2:	;---不让产生VOX,BTONE,CTONE消息

	RET
;---------------处理消息
REMOTE_PRO8_REV_DTMF:
	LAC	DTMF_VAL
	SBHL	0X0F0
	SBHK	0X00E
	BS	ACZ,REMOTE_PRO8_REV_DTMF_STAR	;*
	SBHK	1
	BS	ACZ,REMOTE_PRO8_REV_DTMF_POUND	;#	
	
	RET
REMOTE_PRO8_REV_DTMF_STAR:
	LACK	0
	SAH	PRO_VAR1

	RET
REMOTE_PRO8_REV_DTMF_POUND:
	CALL	INIT_DAM_FUNC
	CALL	BCVOX_INIT
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1
	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
REMOTE_PRO8_TIMER:
	LACL	10000
	SAH	TMR_VOX
	SAH	TMR_CTONE
	
	LAC	PRO_VAR1	;计时
	ADHK	1
	SAH	PRO_VAR1
	SBHK	30		;30s计时
	BZ	SGN,REMOTE_PRO8_REV_DTMF_POUND
		
	RET
;-------
REMOTE_PRO8_VOPLAY:
	LAC	MSG
	XORL	CVP_STOP			;CVP_STOP
	BS	ACZ,REMOTE_PRO8_VOPLAY_END
	
	RET	
REMOTE_PRO8_VOPLAY_END:
	CALL	INIT_DAM_FUNC
	CALL	DAA_ROM_MOR
	CALL	LINE_START
	
	LACK	0X0018
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1


	RET	
;=======================================================================
;99999999999999999999999999999999999999999999999999999999999999999999999
REMOTE_PRO9:	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	RET
;=======================================================================
;#######################################################################
REMOTE_PRO10:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PRO10_END
	
	RET
REMOTE_PRO10_END:
	CALL	INIT_DAM_FUNC	
	CALL	CLR_FUNC	;先空
	LACL	LOCAL_PRO	;进入本地操作
     	CALL	PUSH_FUNC	
	LACK	0
	SAH	PRO_VAR

	;LACL	0X0018
	;CALL	STOR_VP		;延时192ms
	LACL	CMSG_INIT
	CALL	STOR_MSG

	CALL	DAA_OFF	
	CALL	HOOK_OFF

	RET

.END
	