.NOLIST
.INCLUDE	REG_D20.inc
.INCLUDE	MD20U.inc
.INCLUDE	VOPidx.inc
.INCLUDE	CONST.inc
.INCLUDE	EXTERN.inc

.GLOBAL	LOCAL_PROREC
.GLOBAL	LOCAL_PROPLY
.GLOBAL	LOCAL_PROOGM
.GLOBAL	LOCAL_PROTWR
.GLOBAL	LOCAL_PROERA
;-------------------------------------------------------------------------------
.EXTERN	GetOneConst
;---------------------------------------
.LIST
;-------------------------------------------------------------------------------
.ORG	ADDR_SECOND
;-------------------------------------------------------------------------------
LOCAL_PROREC:			;0Xyyy2 = record MEMO
	LAC	MSG
	XORL	CMSG_HSOFF
	BS	ACZ,LOCAL_PROREC_PMINE

	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PROREC0	;(0xyy02)prompt
	SBHK	1
	BS	ACZ,LOCAL_PROREC1	;(0xyy12)record
	SBHK	1
	BS	ACZ,LOCAL_PROREC2	;(0xyy22)Beep/Memful VP
	
	RET
;---------------------------------------
LOCAL_PROREC_PMINE:
	LACL	CRING_IN
	CALL	STOR_MSG	;当来铃一样的处理
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROREC0:
	LAC	MSG
	XORL	CMSG_TMR2
	BS	ACZ,LOCAL_PROREC_2TMR
	
	LAC	MSG
	XORL	CMSG_KEY4S		;MEMO key released worn and stop
	BS	ACZ,LOCAL_PROREC0_WORN
;LOCAL_PROREC0_1:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,LOCAL_PROREC0_RECSTART	;end beep and start record
;LOCAL_PROREC0_2:	
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROREC2_RING
	
	RET
;---------------------------------------
LOCAL_PROREC0_WORN:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP

	;CALL	PORT_HSPLYL
	;CALL	PORT_PLYL
	;CALL	CLR_TIMER
	;CALL	CLR_TIMER2
	
	LACK	0X022
	SAH	PRO_VAR
	
	RET

;---------------------------------------
LOCAL_PROREC0_RECSTART:
	CALL	INIT_DAM_FUNC
	CALL	DAA_REC

	LACL	CMODE9|1<<7	;set TailCutUnit = 200ms
	CALL	DAM_BIOSFUNC

	CALL	REC_START
	
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1

	LACK	0X012
	SAH	PRO_VAR
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROREC1:
	LAC	MSG
	XORL	CMSG_KEY4S		;stop record
	BS	ACZ,LOCAL_PROREC_STOP
LOCAL_PROREC1_1:
	LAC	MSG
	XORL	CREC_FULL		;timer
	BS	ACZ,LOCAL_PROREC_MFUL
LOCAL_PROREC1_2:
	LAC	MSG
	XORL	CMSG_TMR		;timer
	BS	ACZ,LOCAL_PROREC1_TMR
LOCAL_PROREC1_3:
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROREC1_RING
	RET
;---------------------------------------
LOCAL_PROREC1_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	
	RET

;---------------------------------------
LOCAL_PROREC_MFUL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
	CALL	VP_MemoryFull
	CALL	HANDSET_CHK
	LACL	DISP_FL		;"FL"
	SAH	LED_L

	LACK	0X022
	SAH	PRO_VAR

	RET
;---------------------------------------
LOCAL_PROREC_STOP:
	LAC	PRO_VAR1
	SBHK	2
	BS	SGN,LOCAL_PRORECFAIL_STOP
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	CALL	HANDSET_CHK
	;CALL	PORT_HSPLYL
	;CALL	PORT_PLYL
	;CALL	CLR_TIMER
	;CALL	CLR_TIMER2
	
	LACK	0X22
	SAH	PRO_VAR
	
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,LOCAL_PROREC_STOP1
	LAC	MSG_T
	CALL	LED_HLDISP	;VP总数

	RET			;录音完毕后(正常)退出
LOCAL_PROREC_STOP1:
	LAC	MSG_N
	CALL	LED_HLDISP	;VP总数
	
	RET
;---------------------------------------
LOCAL_PROREC1_RING:

	LAC	PRO_VAR1
	SBHK	2
	BS	SGN,LOCAL_PROREC1_RING_RECFAIL
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP

	CALL	PORT_HSPLYL
	CALL	PORT_PLYL
	CALL	CLR_TIMER
	CALL	CLR_TIMER2
	
	LACK	0X0
	SAH	PRO_VAR
	
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,LOCAL_PROREC_RING1
	LAC	MSG_T
	CALL	LED_HLDISP	;VP总数

	RET			;录音完毕后(正常)退出
LOCAL_PROREC_RING1:
	LAC	MSG_N
	CALL	LED_HLDISP	;VP总数
	
	RET
;---------------------------------------
LOCAL_PROREC1_RING_RECFAIL:	;录音失败(检到ring退出)
	LAC	CONF
	ORL	1<<11
	CALL	DAM_BIOSFUNC
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP
	
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL
	CALL	CLR_TIMER
	CALL	CLR_TIMER2
	
	LACK	0
	SAH	PRO_VAR
	
	RET
;---------------------------------------
LOCAL_PRORECFAIL_STOP:		;录音失败(正常退出)
	LAC	CONF
	ORL	1<<11
	CALL	DAM_BIOSFUNC
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
	CALL	HANDSET_CHK
	;CALL	PORT_HSPLYL
	;CALL	PORT_PLYL
	;CALL	CLR_TIMER
	;CALL	CLR_TIMER2
	
	LACK	0X22
	SAH	PRO_VAR
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROREC2:			;timer over|full了,等待松手
	LAC	MSG
	XORL	CMSG_TMR2
	BS	ACZ,LOCAL_PROREC_2TMR
	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,LOCAL_PROREC2_VPSTOP
;LOCAL_PROREC2_1:
	LAC	MSG
	XORL	CMSG_KEY4S
	BS	ACZ,LOCAL_PROREC2_STOP	;Release "Rec" KEY (MemFul)
;LOCAL_PROREC2_2:	
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROREC2_RING

	RET
;---------------------------------------
LOCAL_PROREC2_VPSTOP:		;End of VOP
LOCAL_PROREC2_STOP:
LOCAL_PROREC2_RING:

	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP

	CALL	PORT_HSPLYL
	CALL	PORT_PLYL
	CALL	CLR_TIMER
	CALL	CLR_TIMER2

	LACK	0X0
	SAH	PRO_VAR
	
	RET

;-------------------------------------------------------------------------------
LOCAL_PROPLY:			;0Xyyy1
	LAC	MSG
	XORL	CMSG_KEY8D		;VOL-
	BS	ACZ,LOCAL_PROX_VOLS
	LAC	MSG
	XORL	CMSG_KEY7D		;VOL+
	BS	ACZ,LOCAL_PROX_VOLA
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF(stop)
	BS	ACZ,LOCAL_PROPLY_STOP
	LAC	MSG
	XORL	CRING_IN		;ON/OFF(stop)
	BS	ACZ,LOCAL_PROPLY_RING
	LAC	MSG
	XORL	CMSG_HSOFF
	BS	ACZ,LOCAL_PROPLY_RING	;HandSet off base cradle
	LAC	MSG
	XORL	CMSG_TMR2
	BS	ACZ,LOCAL_PROPLY_2TMR

	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PROPLY_0	;(0X0y01)playing
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_1	;(0X0y11)pauseing
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_2	;(0X0y21)pauseing(waiting for REW key release)
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPLY_0:	
	LAC	MSG
	XORL	CMSG_KEY1D		;pause
	BS	ACZ,LOCAL_PROPLY_PAUSE

	LAC	PRO_VAR
	SFR	8
	ANDK	0X0F
	BS	ACZ,LOCAL_PROPLY_0_0	;(0X0001)playing Vop
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_0_1	;(0X0101)playing Message
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_0_2	;(0X0101)EndOfMessage/BBEEP
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPLY_0_0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,LOCAL_PROPLY_0_OVER
	
	RET
;---------------------------------------
LOCAL_PROPLY_0_OVER:
	LACL	0X0101
	SAH	PRO_VAR
	BS	B1,LOCAL_PROPLY_OVER
;---------------------------------------
LOCAL_PROREC_2TMR:
LOCAL_PROOGM_2TMR:
LOCAL_PROPLY_2TMR:
LOCAL_PROERA_2TMR:
	LIPK	8
	IN	SYSTMP0,GPBD
	BIT	SYSTMP0,12
	BS	TB,LOCAL_PROPLY_0_2_VPSTOP	;查到SPK,立即退出
	BIT	SYSTMP0,5			;查到HS ,
	BS	TB,LOCAL_PROPLY_2TMR_CHKHS	;是挂机状态
	
	RET
;---------
LOCAL_PROPLY_2TMR_CHKHS:
	CALL	PORT_HSPLYL
	CALL	PORT_PLYH
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPLY_0_1:	
	
LOCAL_PROPLY_0_1_1:
	LAC	MSG
	XORL	CMSG_KEY2D		;FFW
	BS	ACZ,LOCAL_PROPLY_FFW
LOCAL_PROPLY_0_1_2:
	LAC	MSG
	XORL	CMSG_KEY3D		;erase
	BS	ACZ,LOCAL_PROPLY_ERASE
LOCAL_PROPLY_0_1_3:
	LAC	MSG
	XORL	CMSG_KEY5D		;repeat
	BS	ACZ,LOCAL_PROPLY_REW
LOCAL_PROPLY_0_1_4:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,LOCAL_PROPLY_OVER

	RET
;---------------------------------------
LOCAL_PROPLY_FFW:

	BS	B1,LOCAL_PROPLY_OVER

;---------------------------------------
LOCAL_PROPLY_REW:
	LAC	PRO_VAR
	ANDL	0XFF0F
	ORK	0X020
	SAH	PRO_VAR

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	;LAC	CONF
	;ORL	1<<8
	;CALL	DAM_BIOSFUNC

	CALL	INIT_DAM_FUNC
	
	RET
LOCAL_PROPLY_REW1:
	
	RET	
;---------------------------------------
LOCAL_PROPLY_PAUSE:
	LAC	CONF
	ANDL	0XF000
	SBHL	0X2000
	BS	ACZ,LOCAL_PROPLY_PAUSE0		;0X2000(message)
	SBHL	0X9000
	BS	ACZ,LOCAL_PROPLY_PAUSE0		;0XB000(voice prompt)
	BS	B1,LOCAL_PROPLY_PAUSE1
LOCAL_PROPLY_PAUSE0:
	LACL	DISP_PA		;"PA"
	SAH	LED_L
	
	LAC	CONF
	ORL	1<<8
	CALL	DAM_BIOSFUNC
	
	LACL	320
	CALL	PAUBEEP		;BEEP Prompt
	
	CALL	DAA_OFF
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ORK	0X010
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	RET
LOCAL_PROPLY_PAUSE1:		;若是BEEP,则循环再发
	LACL	CMSG_KEY1D
	CALL	STOR_MSG

	RET

LOCAL_PROX_VOLS:
	LACL	CMSG_VOLS
	CALL	STOR_MSG
	
	RET

LOCAL_PROX_VOLA:
	LACL	CMSG_VOLA
	CALL	STOR_MSG
	
	RET
;---------------------------------------
LOCAL_PROPLY_ERASE:		;删除后放下一条
	CALL	INIT_DAM_FUNC
	LAC	MSG_ID
	CALL	SET_DELMARK

	CALL	DAA_SPK
	LACK	0X025
	CALL	STOR_VP
	
	LACL	0X0101
	SAH	PRO_VAR
	
	LACL	DISP_dE
	SAH	LED_L		;Display "dE"

	;CALL	BEEP
	
	RET
;---------------------------------------
LOCAL_PROPLY_OVER:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,LOCAL_PROPLY_STOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID
	
	LAC	FILE_ID			;next message
	ADHK	1
	SAH	FILE_ID
	
	BIT	ANN_FG,12
	BZ	TB,LOCAL_PROPLY_LOADVP

	CALL	FFW_MANAGE
	BZ	ACZ,LOCAL_PROPLY_STOP	;THE LAST ONE	

LOCAL_PROPLY_LOADVP:

	LAC	FILE_ID
	CALL	LED_HLDISP
	CALL	BEEP

	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP
	
	LAC	MSG_ID		;Check timer set or not
	ORL	0XA500
	CALL	DAM_BIOSFUNC
	BS	ACZ,LOCAL_PROPLY_LOADVP_END
	
	CALL	MSG_WEEK
	CALL	MSG_HOUR
	CALL	MSG_MIN
	CALL	MSG_AMPM
LOCAL_PROPLY_LOADVP_END:
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPLY_STOP:		;全播放完毕
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBEEP
	
	CALL	CLR_TIMER

	LACL	0X0201
	SAH	PRO_VAR

	CALL	REAL_DEL
	CALL	GC_CHK
	
	CALL	VPMSG_CHK

	BIT	ANN_FG,12
	BS	TB,LOCAL_PROPLY_STOP1
	
	LAC	MSG_T
	CALL	LED_HLDISP
	
	RET
LOCAL_PROPLY_STOP1:
	LAC	MSG_N
	CALL	LED_HLDISP
	
	RET
;---------------------------------------
LOCAL_PROPLY_RING:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP
	
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL
	
	CALL	CLR_TIMER
	
	LACK	0X0
	SAH	PRO_VAR

	CALL	REAL_DEL
	CALL	GC_CHK
	
	CALL	VPMSG_CHK

	BIT	ANN_FG,12
	BS	TB,LOCAL_PROPLY_RING1
	
	LAC	MSG_T
	CALL	LED_HLDISP
	
	RET
LOCAL_PROPLY_RING1:
	LAC	MSG_N
	CALL	LED_HLDISP
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPLY_1:			;PAUSE
	LAC	MSG
	XORL	CMSG_KEY1D		;pause
	BS	ACZ,LOCAL_PROPLY_1_PLAY
LOCAL_PROPLY_1_2:
	;LAC	MSG
	;XORL	CMSG_KEY3D		;erase
	;BS	ACZ,LOCAL_PROPLY_ERASE
LOCAL_PROPLY_1_3:
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPLY_TMR
	
	RET
LOCAL_PROPLY_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	8
	BZ	SGN,LOCAL_PROPLY_STOP
	
	
	RET
;---------------------------------------
LOCAL_PROPLY_1_PLAY:		;退出暂停;继续播放
	CALL	DAA_SPK
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR

	CALL	CLR_TIMER

	LAC	CONF
	ANDL	~(1<<8)
	CALL	DAM_BIOSFUNC

	;LAC	MSG_ID
	LAC	FILE_ID
	CALL	LED_HLDISP

	RET
;---------------------------------------
LOCAL_PROPLY_2:			;等待REW松开
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPLY_REWEXE
	LAC	MSG
	XORL	CMSG_KEY5S		;repeat
	BS	ACZ,LOCAL_PROPLY_REP
	
	RET
LOCAL_PROPLY_REP:
	CALL	INIT_DAM_FUNC
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	BS	B1,LOCAL_PROPLY_LOADVP
;---------------------------------------
LOCAL_PROPLY_REWEXE:	
	LAC	MSG_ID
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_REWEXE_1	;第一个吗?

	LAC	MSG_ID
	SBHK	1
	SAH	MSG_ID
	
	LAC	FILE_ID
	SBHK	1
	BS	ACZ,LOCAL_PROPLY_REWEXE_1
	SAH	FILE_ID
LOCAL_PROPLY_REWEXE_1:	
	BIT	ANN_FG,12
	BZ	TB,LOCAL_PROPLY_REWEXE_2

	CALL	REW_MANAGE
LOCAL_PROPLY_REWEXE_2:
	CALL	INIT_DAM_FUNC
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	CALL	CLR_TIMER
	BS	B1,LOCAL_PROPLY_LOADVP
;-------------------------------------------------------------------------------
LOCAL_PROPLY_0_2:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,LOCAL_PROPLY_0_2_VPSTOP

	RET
;---------------------------------------	
LOCAL_PROPLY_0_2_VPSTOP:	;全播放完毕或强行退出Play-Mode
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP

	CALL	CLR_TIMER
	CALL	CLR_TIMER2
	
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	LACK	0X0
	SAH	PRO_VAR

	CALL	REAL_DEL
	CALL	GC_CHK
	
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,LOCAL_PROPLY_0_2_VPSTOP1
	
	LAC	MSG_T
	CALL	LED_HLDISP
	
	RET

LOCAL_PROPLY_0_2_VPSTOP1:
	LAC	MSG_N
	CALL	LED_HLDISP

	RET
;-------------------------------------------------------------------------------
LOCAL_PROOGM:				;0Xyyy3
	LAC	MSG
	XORL	CMSG_HSOFF
	BS	ACZ,LOCAL_PROOGM_PMINE		;Handset off the base cradle	

	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PROOGM_PLYVOP		;0 - VOP
	SBHK	1
	BS	ACZ,LOCAL_PROOGM_RECOGM		;1 - record OGM
	SBHK	1
	BS	ACZ,LOCAL_PROOGM_PLYOGM		;2 - play OGM

	RET
;---------------
LOCAL_PROOGM_PMINE:
	LACL	CRING_IN
	CALL	STOR_MSG	;当来铃一样的处理
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROOGM_PLYVOP:
	LAC	MSG
	XORL	CMSG_TMR2
	BS	ACZ,LOCAL_PROOGM_2TMR		;查
	
	LAC	MSG
	XORL	CMSG_KEY5S
	BS	ACZ,LOCAL_PROOGM_RECOGMSUCC_STOP	;REC stop AND PLAY
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,LOCAL_PROOGM_PLYVOP_RING	;RING
;LOCAL_PROOGM_PLYVOP0_1:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,LOCAL_PROOGM_RECSTART	;end beep and start record
	
	RET
;---------------------------------------
LOCAL_PROOGM_PLYVOP_RING:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP
	
	LACK	0X0
	SAH	PRO_VAR
	
	RET	
LOCAL_PROOGM_RECSTART:
	CALL	INIT_DAM_FUNC
	CALL	DAA_REC
	LACK	0X0013
	SAH	PRO_VAR

	LACL	CMODE9|1<<7	;200ms
	CALL	DAM_BIOSFUNC

	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	CALL	OGM_SELECT
;---set user index data0"OGM_ID"	
    	LAC	MSG_N
    	ORL	0X8D00
    	CALL	DAM_BIOSFUNC
;---delete the old OGM fisrt------------
	;LAC	MSG_ID
	;CALL	VPMSG_DEL
	;CALL	GC_CHK
;---	
	CALL	REC_START
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	RET
;-------------------------------------------------------------------------------
LOCAL_PROOGM_RECOGM:			;record OGM
	LAC	MSG
	XORL	CMSG_KEY5S		;OGM key released on/off stop record
	BS	ACZ,LOCAL_PROOGM_RECOGM_STOP
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROOGM_RECOGM_RING
LOCAL_PROOGM_REC1_1:
	LAC	MSG
	XORL	CMSG_TMR		;timer
	BS	ACZ,LOCAL_PROOGM_RECTMR
LOCAL_PROOGM_REC1_2:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,LOCAL_PROOGM_RECOGMSUCC_STOP	;Beep/Memfull end and start PLAY OGM
LOCAL_PROOGM_REC1_3:
	LAC	MSG
	XORL	CREC_FULL		;FULL
	BS	ACZ,LOCAL_PROOGM_RECTMR_1_TMROVER		;end beep and start PLAY OGM

	RET
LOCAL_PROOGM_RECOGM_RING:
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,LOCAL_PROOGM_RECOGM_RING1
	LAC	CONF
	ORL	1<<11
	SAH	CONF
	CALL	DAM_BIOSFUNC
LOCAL_PROOGM_RECOGM_RING1:	
	CALL	INIT_DAM_FUNC
	LACK	0X005
	CALL	STOR_VP
	
	LACK	0
	SAH	PRO_VAR
	
	RET	
LOCAL_PROOGM_RECTMR:
	LAC	PRO_VAR
	SFR	8
	ANDK	0X0F
	BS	ACZ,LOCAL_PROOGM_RECTMR_0
	SBHK	1
	BS	ACZ,LOCAL_PROOGM_RECTMR_1

	RET
	
LOCAL_PROOGM_RECTMR_0:		;50s
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	50
	BZ	SGN,LOCAL_PROOGM_RECTMR_0_TMROVER
	
	RET
LOCAL_PROOGM_RECTMR_0_TMROVER:
	LACL	0X0113
	SAH	PRO_VAR
	
	LACL	200
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	RET
;---------------------------------------
LOCAL_PROOGM_RECTMR_1:		;200ms	timer
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	50
	BZ	SGN,LOCAL_PROOGM_RECTMR_1_TMROVER
	
	LAC	LED_L
	XORL	0XFFFF
	BS	ACZ,LOCAL_PROOGM_RECTMR_1_0
	
	LACL	0XFFFF		;LED OFF
	SAH	LED_L
	
	RET
LOCAL_PROOGM_RECTMR_1_0:
	LACL	DISP_rA		;"0XrA"
	SAH	LED_L
	
	RET
LOCAL_PROOGM_RECTMR_1_TMROVER:		;计时或FULL
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	LACK	0X03F
	CALL	STOR_VP
	
	LACL	DISP_rA		;"0XrA"
	SAH	LED_L

	CALL	CLR_TIMER

	RET

LOCAL_PROOGM_RECTMR_2_VPSTOP:
	BS	B1,LOCAL_PROOGM_RECOGMSUCC_STOP
;---------------------------------------
LOCAL_PROOGM_RECOGM_STOP:
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,LOCAL_PROOGM_RECOGMSUCC_STOP

;LOCAL_PROOGM_RECOGMFAIL_STOP:	
	LAC	CONF
	ORL	0X0800
	CALL	DAM_BIOSFUNC
LOCAL_PROOGM_RECOGMSUCC_STOP:	;准备播放OGM
	
	CALL	DAA_SPK
	LACK	0X0023
	SAH	PRO_VAR		;进入播放OGM子功能
PROOGM_LOADOGM:
	LAC	CONF
	ORK	1
	SAH	CONF		;
	CALL	INIT_DAM_FUNC
	CALL	OGM_SELECT
	CALL	CLR_TIMER
;---LED_L "PA"
	LACL	DISP_PA		;"PA"
	SAH	LED_L
	
	CALL	HANDSET_CHK
	
	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	CALL	OGM_SELECT
	BZ	ACZ,MAIN_PRO0_PLAY_OGM1

	CALL	INIT_DAM_FUNC
	CALL	BEEP
	CALL	VP_DefOGM
	
MAIN_PRO0_PLAY_OGM1:

	RET
;-------------------------------------------------------------------------------
LOCAL_PROOGM_PLYOGM:		;PLAY 
	LAC	MSG
	XORL	CMSG_KEY6D		;stop record(手动停止)
	BS	ACZ,LOCAL_PROOGM_PLYOGM_STOP
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROOGM_PLYOGM_RING

	LAC	MSG
	XORL	CMSG_TMR2
	BS	ACZ,LOCAL_PROOGM_2TMR		;查
	
LOCAL_PROOGM_PLYOGM0_1:	
	LAC	MSG
	XORL	CVP_STOP		;stop record(播完停止)
	BS	ACZ,LOCAL_PROOGM_PLYOGM_STOP
LOCAL_PROOGM_PLYOGM0_2:	
	LAC	MSG
	XORL	CMSG_KEY8D		;VOL-
	BS	ACZ,LOCAL_PROX_VOLS
LOCAL_PROOGM_PLYOGM0_3:
	LAC	MSG
	XORL	CMSG_KEY7D		;VOL+
	BS	ACZ,LOCAL_PROX_VOLA
		
	RET
LOCAL_PROOGM_PLYOGM_RING:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP

	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	LACK	0
	SAH	PRO_VAR
	
	RET
LOCAL_PROOGM_PLYOGM_STOP:	;播放完后发BEEP声
	CALL	INIT_DAM_FUNC
	CALL	BEEP

	CALL	CLR_TIMER
	CALL	CLR_TIMER2
	
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	LACK	0
	SAH	PRO_VAR
	
	RET

LOCAL_PROOGM_WORN:
	CALL	INIT_DAM_FUNC
	CALL	BBBEEP
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROTWR:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PROTWR0	;for initial
	SBHK	1
	BS	ACZ,LOCAL_PROTWR1	;for record/line
	SBHK	1
	BS	ACZ,LOCAL_PROTWR_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)

	RET

;---------------
LOCAL_PROTWR0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,LOCAL_PROTWR0_INIT

	RET
;---
LOCAL_PROTWR0_INIT:			;开始录音	
	CALL	INIT_DAM_FUNC
	CALL	DAA_TWAY_REC
	CALL	TWHOOK_ON

	LACK	0X015
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	;CALL	MSG_CHK
	;CALL	GET_FILEID
	;CALL	SET_USRDAT
	
	CALL	REC_START
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROTWR1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,LOCAL_PROTWR1_TMR
;LOCAL_PROTWR1_1:
	LAC	MSG
	XORL	CMSG_KEY4S		;MEMO stop record
	BS	ACZ,LOCAL_PROTWR1_STOP
;LOCAL_PROTWR1_2:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,LOCAL_PROTWR1_FUL
;LOCAL_PROTWR1_3:	
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROTWR1_STOP
;LOCAL_PROTWR1_4:	
	LAC	MSG
	XORL	CMSG_HSOFF		;并机
	BS	ACZ,LOCAL_PROTWR1_STOP

	RET
;---				;由于后BTONE,CTONE,VOX要持续一段时间,可不考虑小长度的录音删除问题

LOCAL_PROTWR1_STOP:
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,LOCAL_PROTWR1_STOP1
	LAC	CONF
	ORL	1<<11
	SAH	CONF
	CALL	DAM_BIOSFUNC
LOCAL_PROTWR1_STOP1:	
	CALL	INIT_DAM_FUNC
	LACK	0X005
	CALL	STOR_VP

	LACK	0X025
	SAH	PRO_VAR

	RET
LOCAL_PROTWR1_FUL:
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK
	
	BS	B1,LOCAL_PROTWR1_STOP
	;BIT	ANN_FG,13
	;BS	TB,LOCAL_PROTWR1_FUL_EXIT
	;CALL	REC_START

	;RET
;LOCAL_PROTWR1_FUL_EXIT:
	;BS	B1,LOCAL_PROTWR1_STOP
;---
LOCAL_PROTWR1_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1

	RET
;-------------------------------------------------------------------------------
LOCAL_PROTWR_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,LOCAL_PROTWR_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET

LOCAL_PROTWR_EXIT_END:
	CALL	INIT_DAM_FUNC
	LACK	0
	SAH	PRO_VAR
	CALL	TWHOOK_OFF
	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,LOCAL_PROTWR_EXIT_END1
	
	
	LAC	MSG_T
	CALL	LED_HLDISP	;VP总数
	
	RET
LOCAL_PROTWR_EXIT_END1:
	LAC	MSG_N
	CALL	LED_HLDISP	;VP总数
	
	
	RET
;-------------------------------------------------------------------------------
TWHOOK_ON:		;Set GPBD.4
	LIPK	8

	IN	SYSTMP0,GPBD
	LAC	SYSTMP0
	ORL	1<<4
	SAH	SYSTMP0
	OUT	SYSTMP0,GPBD
	ADHK	0

	RET
	
;---
TWHOOK_OFF:		;Reset GPBD.4
	LIPK	8

	IN	SYSTMP0,GPBD
	LAC	SYSTMP0
	ANDL	~(1<<4)
	SAH	SYSTMP0
	OUT	SYSTMP0,GPBD
	ADHK	0

	RET
;---
DAA_TWAY_REC:	;(SW0)(SW2) ==> (AUX->ADC)&(MIC->ADC)
	LIPK    6
	OUTK    (1<<2),SWITCH

	NOP
	IN	SYSTMP0,AGC
	LAC	SYSTMP0
	ANDL	0xF00F
	ORL	0X09FF
	SAH	SYSTMP0
	OUT     SYSTMP0,AGC	; Lin-pga=c ; AD-PGA=c(0dB) ; MIC-pre-pga=c (+21dB)
	ADHK	0
;---
	RET
;-------------------------------------------------------------------------------
LOCAL_PROERA:
	;LAC	MSG
	;XORL	CMSG_KEY3S		;erase key released
	;BS	ACZ,LOCAL_PROERA_RELE
	LAC	MSG
	XORL	CVP_STOP		;VOP end
	BS	ACZ,LOCAL_PROERA_VPSTOP
	LAC	MSG
	XORL	CRING_IN		;RING
	BS	ACZ,LOCAL_PROERA_RING
	LAC	MSG
	XORL	CMSG_TMR2		;TMR2
	BS	ACZ,LOCAL_PROERA_2TMR
	
	
	RET
;---------------------------------------
;LOCAL_PROERA_RELE:
LOCAL_PROERA_VPSTOP:
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR

	CALL	CLR_TIMER2
	CALL	PORT_HSPLYL
	CALL	PORT_PLYL

	RET
;---------------------------------------

LOCAL_PROERA_RING:
	BS	B1,LOCAL_PROERA_VPSTOP
;-------------------------------------------------------------------------------
.INCLUDE	l_ply.asm
;-------------------------------------------------------------------------------
.END
	