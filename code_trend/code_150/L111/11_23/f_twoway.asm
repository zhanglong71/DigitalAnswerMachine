.LIST
TWAY_STATE:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,TWAY_STATE0		;for initial
	SBHK	1
	BS	ACZ,TWAY_STATE1		;for record/line
	SBHK	1
	BS	ACZ,TWAY_STATE_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)

	RET

;=============================================================
TWAY_STATE0:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,TWAY_STATE_INIT
;TWAY_STATE0_1:	
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,TWAY_VP_STOP	;CVP_STOP,LBEEP播放完毕
;TWAY_STATE0_2:
	LAC	MSG
	XORL	CMSG_KEY6S		;STOP
	BS	ACZ,TWAY_STOP_DET
	
	RET

;---
TWAY_STATE_INIT:
	CALL	INIT_DAM_FUNC
	CALL	MSG_CHK
	BIT	ANN_FG,13
	BS	TB,TWAY_STATE_EXIT_END	;满了不准再录,退出
TWAY_STATE_INIT_1:
	CALL	DAA_LIN_SPK

	CALL	LBEEP
TWAY_STATE_INIT_END:
	RET

;---
TWAY_VP_STOP:			;开始录音	
	CALL	INIT_DAM_FUNC
	CALL	DAA_TWAY_RECORD
	CALL	TWHOOK_ON
	CALL	BCVOX_INIT
;!!!!!!!!!!!!!!!!!!!!!!!!!!
;!!!!!!!!!!!!!!!
	LACL	0XD0		;告诉MCU摘机了
	CALL	STOR_DAT
	LACK	0
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!	
	LACL	0XC5
	CALL	STOR_DAT
	LACK	2
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X010
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	CALL	MSG_CHK
	CALL	GET_FILEID
	CALL	SET_USRDAT
	LACK	CTAG_MEMO		;set memo flag
	CALL	SET_USRDAT1
	
	CALL	REC_START
			;;;;判断OGM1/OGM2---record/line
TWAY_VP_STOP_END:		
	RET
;=============================================================
TWAY_STATE1:
;TWAY_STATE1_0:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,TWAY_TMR_DET
;TWAY_STATE1_1:
	LAC	MSG
	XORL	CMSG_KEY6S		;STOP
	BS	ACZ,TWAY_STOP_DET
;TWAY_STATE1_2:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,TWAY_REC_FUL
;TWAY_STATE1_3:	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,TWAY_REC_EVP
	
	RET
;---				;由于后BTONE,CTONE,VOX要持续一段时间,可不考虑小长度的录音删除问题
TWAY_STOP_DET:
;!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC6		;停止TWAY录音
	CALL	STOR_DAT
	LACK	2
	CALL	STOR_DAT
	
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!
	CALL	INIT_DAM_FUNC
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT

	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

	CALL	BB_VOP
	CALL	DAA_SPK
	
	RET
TWAY_REC_FUL:
	CALL	INIT_DAM_FUNC
	
	CALL	MEMFULL_CHK
	BZ	ACZ,TWAY_MFULL_EXIT

	CALL	REC_START

	RET
TWAY_MFULL_EXIT:		;MFULL退出
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	VP_MEMFUL	;警告语音
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

	RET
;---
TWAY_TMR_DET:
	LAC	CONF
	ANDL	0XFFC0
	SAH	CONF
	
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1

	RET
;---
TWAY_REC_EVP:
	CALL	INIT_DAM_FUNC
	
	RET
;=================================================================
TWAY_STATE_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,TWAY_STATE_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET
TWAY_STATE_EXIT_END:
	CALL	INIT_DAM_FUNC
	CALL	CLR_FUNC	;先空
	LACK	0
	SAH	PRO_VAR
	CALL	TWHOOK_OFF
;!!!!!!!!!!!!!!!
	LACL	0XD0		;告诉MCU挂机了
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
;//----------------------------------------------------------------------

.END
