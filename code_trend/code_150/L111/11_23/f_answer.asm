.LIST
ICM_STATE:
	LAC	MSG
	XORL	CMSG_CPC		;接线后摘机(相当于CPC)
	BS	ACZ,ICM_CPC_RUN
	
	LAC	MSG
	XORL	CMSG_KEY6S		;接线按ON/OFF(相当于CPC)
	BS	ACZ,ICM_CPC_RUN
;---
	LAC	MSG
	XORL	CMSG_KEYAP		;VOL+
	BS	ACZ,ICM_STATE_VOLA

	LAC	MSG
	XORL	CMSG_KEYAS		;VOL+
	BS	ACZ,ICM_STATE_VOLA

	LAC	MSG
	XORL	CMSG_KEYBP		;VOL-
	BS	ACZ,ICM_STATE_VOLS

	LAC	MSG
	XORL	CMSG_KEYBS		;VOL-
	BS	ACZ,ICM_STATE_VOLS
;---
	LAC	PRO_VAR
	SFR	4
	ANDK	0XF
	BS	ACZ,ICM_STATE0		;for initial
	SBHK	1
	BS	ACZ,ICM_STATE_REC	;for record(ANSWER AND RECORD ICM)
	SBHK	1
	BS	ACZ,ICM_STATE_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)
	SBHK	1
	BS	ACZ,LINE_STATE1		;for line(ANSWER ONLY)

	RET
;---------------
ICM_STATE_VOLA:
	BS	B1,MAIN_PRO1_PLAY_VOLA
ICM_STATE_VOLS:
	BS	B1,MAIN_PRO1_PLAY_VOLS
;---------------
ICM_CPC_RUN:
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_CPC_RUN_2
;---正在录音
	LAC	PRO_VAR1
	SBHK	3
	BZ	SGN,ICM_CPC_RUN_0
;---录音时间小于3秒――删除之	
	SRAM	CONF,11
	CALL	DAM_BIOS
	CALL	INIT_DAM_FUNC
	
	CALL	GC_CHK
	BS	B1,ICM_CPC_RUN_END
ICM_CPC_RUN_0:
	CALL	INIT_DAM_FUNC
	CALL	GET_TOTALMSG
	SAH	MSG_T			;总数(同时也是最后一条录音的ID号)
	CALL	GET_VPTLEN
	SBHK	2
	BZ	SGN,ICM_CPC_RUN_1
;---录音长度小于2秒――删除	
	LAC	MSG_T
	CALL	MSG_DEL
	CALL	GC_CHK

	BS	B1,ICM_CPC_RUN_END
ICM_CPC_RUN_1:	
	BIT	ANN_FG,1
	BZ	TB,ICM_CPC_RUN_2	;有来电吗?
;---有来电
	CALL	INIT_DAM_FUNC
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACK	0
	CALL	SET_TELGROUP

	CALL	GET_TELT
	SAH	MSG_T
	LAC	MBOX_ID
	SAH	BUF1
	LAC	MSG_T
	ORL	0XED00
	SAH	CONF
	CALL	DAM_BIOS	;copy caller_id to the mailbox
	LAC	RESP
	BZ	ACZ,ICM_BCVOX_DET	;ERROR
	
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT	;get user data0
	SAH	MSG_N
	LAC	MBOX_ID
	CALL	SET_TELGROUP
	CALL	GET_TELT
	SAH	MSG_T
	
	LAC	MSG_N
	SAH	BUF1
	LAC	MSG_T
	CALL	SET_TELUSRDAT	;set user data0
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ICM_CPC_RUN_2:
	CALL	INIT_DAM_FUNC
;---ICM record ok ?
	
ICM_CPC_RUN_END:	
	BS	B1,ICM_STATE_EXIT_END
	
;=============================================================
ICM_STATE0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,ICM_STATE_INIT
;ICM_STATE0_1:	
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_VP_STOP		;CVP_STOP,OGM播放完毕
;ICM_STATE0_2:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ICM_REV_DTMF_CHKASTER

	RET
;---
ICM_STATE_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_ANS_SPK

	CALL	OGM_STATUS	;选择当前OGM
	BZ	ACZ,ICM_STATE_INIT_DEFOGM
;ICM_STATE_INIT_RECDOGM:	;播录制的语音
	LACK	0X07D
	CALL	STOR_VP		;延时1 second
	LAC	MSG_ID
	ANDL	0X0FF
	ORL	0XFE00
	CALL	STOR_VP
	
	BS	B1,ICM_STATE_INIT_CHK
ICM_STATE_INIT_DEFOGM:		;播默认的语音()
	LACK	0X030
	CALL	STOR_VP
	CALL	BEEP		;for OGM5
	
	LAC	MSG_N
	SBHK	5
	BS	ACZ,ICM_STATE_INIT_EXIT	
	
	CALL	INIT_DAM_FUNC	;for OGM(1,2,3,4)
	LACK	0X07D
	CALL	STOR_VP		;延时1 second
	CALL	VP_DEFAULTOGM	
	CALL	LBEEP
	BS	B1,ICM_STATE_INIT_EXIT
	
ICM_STATE_INIT_CHK:	
	LAC	MSG_N
	SBHK	5
	BS	ACZ,ICM_STATE_INIT_EXIT	

	CALL	LBEEP
ICM_STATE_INIT_EXIT:	
	RET

;-------
ICM_VP_STOP:			;开始录音(ICM)/ON_LINE(only)
	CALL	INIT_DAM_FUNC
	CALL	DAA_ANS_REC
	CALL	BCVOX_INIT

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X030		;OGM5/line
	SAH	PRO_VAR
	CALL	LINE_START
	
	;BIT	EVENT,8		;answer only?
	;BS	TB,ICM_VP_STOP1
	;BIT	EVENT,9		;answer off?
	;BS	TB,ICM_VP_STOP1
	;BIT	ANN_FG,13	;memory full?
	;BS	TB,ICM_VP_STOP1
	LAC	MSG_N
	SBHK	5
	BS	ACZ,ICM_VP_STOP1 ;answer only?	
	
	CALL	INIT_DAM_FUNC
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X010		;OGM1/record
	SAH	PRO_VAR
	
	CALL	MSG_CHK
	CALL	GET_FILEID
	CALL	SET_USRDAT

	LACK	CTAG_ICM		;set ICM flag(we can recognize it as an ICM message)
	CALL	SET_USRDAT1
	
	CALL	REC_START
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC5
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
ICM_VP_STOP1:
		
	RET
;---------------for DTMF
LINE_REV_DTMF:			;LineMode计时清零
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	BS	B1,ICM_REV_DTMF
ICM_REV_DTMF_CHKASTER:		;播放OGM时先查"*"键
	LAC	DTMF_VAL
	XORL	0XFE
	BS	ACZ,ICM_REV_DTMF_CHKASTER_YES
	
ICM_REV_DTMF:			;比较密码
	CALL	BCVOX_INIT	;有键按下BCVOX要清零
	
	LAC	DTMF_VAL
	ANDK	0X0F
	CALL	PSWORD_CHK
	BS	ACZ,ICM_REV_DTMF_PASS
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX1_YES
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX2_YES
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX3_YES
	
	RET

;-------
ICM_REV_DTMF_PASS:		;密码成功
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_REV_DTMF_YES1
	
	SRAM	CONF,11
	CALL	DAM_BIOS	;若是录音过程,则删除之
ICM_REV_DTMF_YES1:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC6		;stop record ICM
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	
	LACL	0XFF
	CALL	STOR_DAT
	
	LACL	0X9E		;enter remote
	CALL	STOR_DAT
	LACK	5
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	CALL	CLR_FUNC	;先空
	LACL	REMOTE_PRO	;进入遥控
	CALL	PUSH_FUNC

	LACL	CMSG_INIT
	CALL	STOR_MSG

	LACK	0
	SAH	PRO_VAR		;程序步骤
	SAH	PRO_VAR1	;计时清零
	CALL	BCVOX_INIT
	
	RET
;-------
ICM_REV_DTMF_MBOX1_YES:		;更换MBOX_ID成功
	LACK	1
	SAH	MBOX_ID
	BS	B1,ICM_REV_DTMF_MBOX_YES
ICM_REV_DTMF_MBOX2_YES:
	LACK	2
	SAH	MBOX_ID
	BS	B1,ICM_REV_DTMF_MBOX_YES
ICM_REV_DTMF_MBOX3_YES:
	LACK	3
	SAH	MBOX_ID
ICM_REV_DTMF_MBOX_YES:
;---如果是OGM5(只答不录),就不改变MBOX_ID	
	LAC	MSG_N
	SBHK	5
	BS	ACZ,ICM_REV_MBOX_YES_END
	
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_REV_MBOX_YES1
	
	SRAM	CONF,11		;若是录音过程,则删除之
	CALL	DAM_BIOS
ICM_REV_MBOX_YES1:
	CALL	INIT_DAM_FUNC
	CALL	DAA_ANS_SPK
	CALL	LBEEP
	
	LACK	0
   	SAH	PRO_VAR
   	
	RET
ICM_REV_MBOX_YES_END:
	LACK	1
	SAH	MBOX_ID
	RET

ICM_REV_DTMF_CHKASTER_YES:
	CALL	INIT_DAM_FUNC
	CALL	BBEEP
	
	RET
;=============================================================
ICM_STATE_REC:
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,ICM_REV_DTMF
;ICM_STATE_REC_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,ICM_TMR_DET
;ICM_STATE_REC_2:
	LAC	MSG
	XORL	CMSG_VOX		;VOX_ON 10s
	BS	ACZ,ICM_VOX_DET
;ICM_STATE_REC_3:
	LAC	MSG
	XORL	CMSG_CTONE		;CTONE 10s
	BS	ACZ,ICM_CTONE_DET
;ICM_STATE_REC_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 10s
	BS	ACZ,ICM_BTONE_DET
;ICM_STATE_REC_5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,ICM_REC_FUL
;ICM_STATE_REC_6:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,ICM_FUL_EVP
	
	RET
;-------
ICM_VOX_DET:				;由于后BTONE,CTONE,VOX要持续一段时间,可不考虑小长度的录音删除问题
ICM_CTONE_DET:
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_BCVOX_DET	;是录音吗?
;---Recording
	LAC	PRO_VAR1
	SBHK	11
	BZ	SGN,ICM_REC_SUCC
;---录音时间小于11秒,
	CALL	INIT_DAM_FUNC
	CALL	GET_TOTALMSG
	SAH	MSG_T			;总数(同时也是最后一条录音的ID号)
	CALL	GET_VPTLEN
	SBHK	2
	BZ	SGN,ICM_REC_SUCC
;---录音长度小于2秒――删除	
	LAC	MSG_T
	CALL	MSG_DEL
	CALL	GC_CHK
	BS	B1,ICM_BCVOX_DET
ICM_BTONE_DET:
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_BCVOX_DET	;是录音吗?
;---Recording
	CALL	INIT_DAM_FUNC
	CALL	GET_TOTALMSG
	SAH	MSG_T			;总数(同时也是最后一条录音的ID号)
	CALL	GET_VPTLEN
	SBHK	3
	BZ	SGN,ICM_REC_SUCC
;---录音长度小于3秒――删除	
	LAC	MSG_T
	CALL	MSG_DEL
	CALL	GC_CHK
	BS	B1,ICM_BCVOX_DET

ICM_REC_SUCC:
;---Record success
	BIT	ANN_FG,1
	BZ	TB,ICM_BCVOX_DET	;有来电吗?
	
	;BS	B1,ICM_REC_SUCC_CID

ICM_REC_SUCC_CID:		;---录音成功且有来电
	CALL	INIT_DAM_FUNC
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACK	0
	CALL	SET_TELGROUP

	CALL	GET_TELT
	SAH	MSG_T
	LAC	MBOX_ID
	SAH	BUF1
	LAC	MSG_T
	ORL	0XED00
	SAH	CONF
	CALL	DAM_BIOS	;copy caller_id to the mailbox
	LAC	RESP
	BZ	ACZ,ICM_BCVOX_DET	;ERROR
	
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT	;get user data0
	SAH	MSG_N
	
	LAC	MBOX_ID
	CALL	SET_TELGROUP
	CALL	GET_TELT
	SAH	MSG_T
	
	LAC	MSG_N
	SAH	BUF1
	LAC	MSG_T
	CALL	SET_TELUSRDAT	;set user data0
;!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ICM_BCVOX_DET:	
	CALL	INIT_DAM_FUNC
	CALL	MSG_CHK
;---
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR
	
	CALL	DAA_SPK	;???????????????????
	CALL	BBEEP		;替换语音BB
;!!!!!!!!!!!!!!!!!!!!!!!!!!!	
	LACL	0XC6
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	RET
ICM_REC_FAIL:
	SRAM	CONF,11
	CALL	DAM_BIOS
	
	BS	B1,ICM_BCVOX_DET
;---
ICM_TMR_DET:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	120	
	BS	SGN,ICM_TMR_DET_END
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_ANS_SPK
	CALL	BBEEP		;替换语音BB
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

ICM_TMR_DET_END:
	RET
;-------
ICM_REC_FUL:
	CALL	INIT_DAM_FUNC

	CALL	BBBEEP		;警告语音BBB
	CALL	VP_MEMFUL
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR		;退出答录
	
	RET
ICM_FUL_EVP:
	LACK	0
	SAH	PRO_VAR1
	CALL	INIT_DAM_FUNC
	CALL	LINE_START
	
	RET
;=================================================================
ICM_STATE_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_STATE_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET
ICM_STATE_EXIT_END:
	CALL	INIT_DAM_FUNC
	CALL	HOOK_OFF
	CALL	CLR_FUNC	;先空
;!!!!!!!!!!!!!!!
	LACL	0XD0		;告诉MCU挂机了
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!	
	LACL	0X9000		;Line OFF
	ORK	CMODE9
	SAH	CONF
	CALL	DAM_BIOS
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC6
	CALL	STOR_DAT
	LACK	1
	CALL	STOR_DAT
	LACL	0X0FF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	CALL	MSG_CHK
	CALL	NEWICM_CHK
	
	LACK	0
	SAH	PRO_VAR
	LACK	1
	SAH	MBOX_ID
	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
;========================================================================
LINE_STATE1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,LINE_REV_DTMF
;LINE_STATE1_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,LINE_TMR_DET
;LINE_STATE1_2:
	LAC	MSG
	XORL	CMSG_VOX		;VOX_ON 8s
	BS	ACZ,ICM_VOX_DET
;LINE_STATE1_3:
	LAC	MSG
	XORL	CMSG_CTONE		;CTONE 8s
	BS	ACZ,ICM_CTONE_DET
;LINE_STATE1_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 8s
	BS	ACZ,ICM_BTONE_DET

	RET
;-------
LINE_TMR_DET:		;for memful/answer only/answer off
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	3
	BS	SGN,LINE_TMR_DET_END

	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_ANS_SPK
	CALL	BBEEP		;替换语音BB

LINE_TMR_DET_END:
	RET
	
;//-----------------------------------------------------------------------------
DTMF_TABLE:
	;no	1	2	3	4	5	6	7	8	
.DATA	0X00	0XF1	0XF2	0XF3	0XF4	0XF5	0XF6	0XF7	0XF8

	;9	*	0	#	A	B	C	D
.DATA	0XF9	0XFE	0XF0	0XFF	0XFA	0XFB	0XFC	0XFD

.END
