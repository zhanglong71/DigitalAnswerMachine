.LIST
;//-----------------------------------------------------------------------------
MAIN:
	CALL	INITIAL
	
	CALL	INITMCU
	CALL	BLED_OFF
;//--------------------- STAND-BY ROUTINES -------------------------------------
STAND_BY:
        CALL	INT_BIOS		;DAM_BIOS软中断
	CALL	SYS_MONITOR		;系统事件监控
	CALL	GET_MSG			;GET MESSAGE
	BS	ACZ,STAND_BY
	SAH	MSG
	
	CALL	SYS_MSG
	BS	ACZ,STAND_BY		;系统消息

	CALL	GET_FUNC		;应用定向
	CALA
	
        BS	B1,STAND_BY
;-------------------------------主程序------------------------------------------
MAIN_PRO:
	LAC	PRO_VAR
	ANDL	0X0F
	BS	ACZ,MAIN_PRO0	;idle
	SBHK	1
	BS	ACZ,MAIN_PRO1	;PLAY MEMO
	SBHK	1
	BS	ACZ,MAIN_PRO2	;PAUSE PLAY
	SBHK	1
	BS	ACZ,MAIN_PRO3	;RECORD MEMO
	SBHK	1
	BS	ACZ,MAIN_PRO4	;PLAY OGM
	SBHK	1
	BS	ACZ,MAIN_PRO5	;RECORD OGM
	SBHK	1
	BS	ACZ,MAIN_PRO6	;CID(m_cid)
	SBHK	1
	BS	ACZ,MAIN_PRO7	;MBOX
	SBHK	1
	BS	ACZ,MAIN_PRO8	;VOL
	SBHK	1
	BS	ACZ,MAIN_PRO9	;MENU(m_menu)
	SBHK	1
	BS	ACZ,MAIN_PRO10	;PHONEBOOK(m_pbook)
	SBHK	1
	BS	ACZ,MAIN_PRO11	;format sflash
	
	RET
;======================================================消息生成区
MAIN_PRO0:	;空闲状态要考虑的消息
	LAC	PRO_VAR
	SFR	4
	ANDL	0X0F
	BS	ACZ,MAIN_PRO0_0	;(0Xyy00)idle
	SBHK	1
	BS	ACZ,MAIN_PRO0_1	;(0Xyy10)end VP
	
	RET
MAIN_PRO0_0:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,MAIN_PRO0_INIT
;---
;MAIN_PRO0_0_1:
	LAC	MSG
	XORL	CMSG_KEY1S
	BS	ACZ,MAIN_PRO0_PLAY	;play
;---
;MAIN_PRO0_0_1_1:
	LAC	MSG
	XORL	CMSG_KEY1L
	BS	ACZ,MAIN_PRO0_REC_TWAY	;TWO_WAY record
;---
;MAIN_PRO0_0_2:
	LAC	MSG
	XORL	CMSG_KEY2S
	BS	ACZ,MAIN_PRO0_TIME
;MAIN_PRO0_0_2_1:
	LAC	MSG
	XORL	CMSG_KEY2L
	BS	ACZ,MAIN_PRO0_PHONEBOOK	;电话本
;---
;MAIN_PRO0_0_3:
	LAC	MSG
	XORL	CMSG_KEY3S
	BS	ACZ,MAIN_PRO0_PLAY_OGM
;MAIN_PRO0_0_3_1:
	LAC	MSG
	XORL	CMSG_KEY3L
	BS	ACZ,MAIN_PRO0_RECO_OGM
;---
;MAIN_PRO0_0_4:
	LAC	MSG
	XORL	CMSG_KEY4S
	BS	ACZ,MAIN_PRO0_NEWVP_BVOP
	LAC	MSG
	XORL	CMSG_KEY4L
	BS	ACZ,MAIN_PRO0_RECO_MSG
;---
;MAIN_PRO0_0_5:
	LAC	MSG
	XORL	CMSG_KEY5L
	BS	ACZ,MAIN_PRO0_ERAS_MSG
;---
;MAIN_PRO0_6:
	LAC	MSG
	XORL	CMSG_KEY6S
	BS	ACZ,MAIN_PRO0_ONOFF
;MAIN_PRO0_6_1:
	LAC	MSG
	XORL	CMSG_KEY6L
	BS	ACZ,MAIN_PRO0_FORMAT
;---
;MAIN_PRO0_7:
	LAC	MSG
	XORL	CMSG_KEY7L
	BS	ACZ,MAIN_PRO0_MBOX
;MAIN_PRO0_7_1:
	LAC	MSG
	XORL	CMSG_KEY7S
	BS	ACZ,MAIN_PRO0_MBOX_1
;---
;MAIN_PRO0_8:
	LAC	MSG
	XORL	CMSG_KEY8S
	BS	ACZ,MAIN_PRO0_INCOMECALL
;---
;MAIN_PRO0_9:
	LAC	MSG
	XORL	CMSG_KEY9S
	BS	ACZ,MAIN_PRO0_SMENU
;MAIN_PRO0_9_1:
	LAC	MSG
	XORL	CMSG_KEY9L
	BS	ACZ,MAIN_PRO0_LMENU
;---
;MAIN_PRO0_10:
	LAC	MSG
	XORL	CMSG_KEYAS
	BS	ACZ,MAIN_PRO0_VOL
;MAIN_PRO0_10_1:
	LAC	MSG
	XORL	CMSG_KEYAP
	BS	ACZ,MAIN_PRO0_VOL
;---
;MAIN_PRO0_11:
	LAC	MSG
	XORL	CMSG_KEYBS
	BS	ACZ,MAIN_PRO0_VOL
;MAIN_PRO0_11_1:
	LAC	MSG
	XORL	CMSG_KEYBP
	BS	ACZ,MAIN_PRO0_VOL
;---
;MAIN_PRO0_12:
	LAC	MSG
	XORL	CMSG_KEYCS
	BS	ACZ,MAIN_PRO0_INCOMECALL
;---
;MAIN_PRO0_13:
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,MAIN_PRO0_TMR
;---
;MAIN_PRO0_14:	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,MAIN_PRO0_INIT
;---
;MAIN_PRO0_15:	
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PRO0_BLEDON
	LAC	MSG
	XORL	CMSG_KEY4S
	BS	ACZ,MAIN_PRO0_BLEDON
	LAC	MSG
	XORL	CMSG_KEY5S
	BS	ACZ,MAIN_PRO0_BLEDON
	
	RET

;-------------------------------;消息处理
MAIN_PRO0_1:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,MAIN_PRO0_1_VPSTOP
	LAC	MSG
	XORL	CMSG_KEY6S
	BS	ACZ,MAIN_PRO0_1_STOP
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PRO0_BLEDON

	RET
MAIN_PRO0_1_VPSTOP:
	CALL	INIT_DAM_FUNC
	LACK	0
	SAH	PRO_VAR
	BS	B1,MAIN_PRO0_INIT

MAIN_PRO0_1_STOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR

	RET
;---------------
MAIN_PRO0_FORMAT:
	LACK	0X0B
	SAH	PRO_VAR
	CALL	BLED_ON
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER	;进入待机计时	

	RET
MAIN_PRO0_INIT:
	CRAM	ANN_FG,0	;initial complete???????????????????????
	
	CALL	HOOK_OFF

	CALL	DAA_OFF		;要考虑加入的功能(11.18)
	CALL	INIT_DAM_FUNC

	CALL	TEL_GC_CHK
	CALL	GC_CHK
	
	CALL	MSG_CHK
	CALL	GET_VPMSGN
	SAH	MSG_N
	CALL	GET_VPMSGT
	SAH	MSG_T
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0X83		;录音数量同步(3bytes)
	CALL	STOR_DAT
	LAC	MSG_N
	CALL	STOR_DAT
	LAC	MSG_T
	CALL	STOR_DAT
	
	LACL	0XFF
	CALL	STOR_DAT
	
	LACL	0X82		;新旧来电同步(3bytes)
	CALL	STOR_DAT
	LACK	0	;???????????????
	CALL	STOR_DAT
	LACK	0
	CALL	SET_TELGROUP
	CALL	GET_TELT	;来电总数同步
	CALL	STOR_DAT
	
	LACL	0XFF
	CALL	STOR_DAT
	
	LACL	0X9E
	CALL	STOR_DAT
	LACK	6
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT

;	CALL	SENDTIME_UPDATE	;update time(6bytes)
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER	;进入待机计时

	RET
;-------
MAIN_PRO0_PLAY:	
	CALL	INIT_DAM_FUNC
	CALL	BLED_ON
	LACK	1
	SAH	PRO_VAR			;进入播放子功能

	CALL	CLR_FLAG		;clear new flag
	CALL	MSG_CHK
	LAC	MBOX_ID
	CALL	SET_TELGROUP		;set group
	
	LACK	0
	SAH	FILE_LEN
	SAH	FILE_ID
	SAH	MSG_ID

	BIT	ANN_FG,12
	BS	TB,MAIN_PRO0_PLAY_NEW
	BIT	ANN_FG,14
	BS	TB,MAIN_PRO0_PLAY_OLD

	CALL	VP_NO
	CALL	VP_MESSAGES
	CALL	BB_VOP
	
	LACK	0X0010
	SAH	PRO_VAR
	
	BS	B1,MAIN_PRO0_PLAY_END
;-------play new messages
MAIN_PRO0_PLAY_NEW:
	CALL	GET_VPMSGN
	SAH	MSG_N
	SBHK	1
	BS	ACZ,MAIN_PRO0_PLAY_ONENEWMESSAGE

	LAC	MSG_N
	;ANDL	0X0FF
	CALL	ANNOUNCE_NUM
	CALL	VP_NEW
	
	CALL	VP_MESSAGES
	
	BS	B1,MAIN_PRO0_PLAY_END
MAIN_PRO0_PLAY_ONENEWMESSAGE:
	CALL	VP_ONE
	CALL	VP_NEW
	BS	B1,MAIN_PRO0_PLAY_VPMESSAGE
;-------play old messages
MAIN_PRO0_PLAY_OLD:
	CALL	GET_VPMSGT
	SAH	MSG_T
	SBHK	1
	BS	ACZ,MAIN_PRO0_PLAY_ONEMESSAGE
	
	LAC	MSG_T
	CALL	ANNOUNCE_NUM
	CALL	VP_MESSAGES
		
	BS	B1,MAIN_PRO0_PLAY_END
MAIN_PRO0_PLAY_ONEMESSAGE:
	CALL	VP_ONE
	;BS	B1,MAIN_PRO0_PLAY_VPMESSAGE
MAIN_PRO0_PLAY_VPMESSAGE:	
	CALL	VP_MESSAGE
MAIN_PRO0_PLAY_END:
	CALL	GET_TOTALMSG	;save the number of TOTAL messages
	SAH	MSG_T

	LACL	0XC7
	CALL	STOR_DAT
	LACK	0X0
	CALL	STOR_DAT	;显示/准备播放录音
	LACL	0XFF
	CALL	STOR_DAT

	;CALL	DAA_SPK
	CALL	DAA_SPK
	
	RET

;-------
MAIN_PRO0_REC_TWAY:
	CALL	INIT_DAM_FUNC
	CALL	SET_COMPS
	LACK	0X0A		;// set silence threshold
	CALL	SET_SILENCE
	CALL	BLED_ON
	CALL	CLR_FUNC	;先空
    	LACL	TWAY_STATE
     	CALL	PUSH_FUNC
     	;CALL	TWHOOK_ON

     	LACK	1
     	SAH	MBOX_ID		;公共邮箱
     	
  	LACL	CMSG_INIT
   	CALL	STOR_MSG
   	
   	LACK	0
   	SAH	PRO_VAR

	RET				
;-------	
MAIN_PRO0_TIME:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	CURR_WEEK
	CALL	CURR_HOUR
	CALL	CURR_MIN
	CALL	BLED_ON

	LACK	0X0010
	SAH	PRO_VAR

	RET
;-------
MAIN_PRO0_PLAY_OGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BLED_ON
	LACK	4
	SAH	PRO_VAR		;进入播放OGM子功能

	LAC	VOI_ATT
	SFR	8
	ANDK	0X0F
	CALL	OGM_SELECT
	
	LAC	MSG_ID
	ANDL	0X0FF
	ORL	0XFE00
	CALL	STOR_VP
	
;!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC3
	CALL	STOR_DAT
	LAC	MSG_N		;LANGUAGE/OGM_ID/RING_CNT/ATT1
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!
	
	LAC	MSG_N
	CALL	OGM_SELECT
	BS	ACZ,MAIN_PRO0_PLAY_OGM1
	
	CALL	INIT_DAM_FUNC

	LAC	MSG_N
	CALL	DEFOGM_LOCALPLY		;load default OGM

	;CALL	VP_DEFAULTOGM	;default OGM
MAIN_PRO0_PLAY_OGM1:		
	
	RET
;-------
MAIN_PRO0_RECO_OGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	LBEEP
	CALL	BLED_ON	
	CALL	SET_COMPS
	LACK	0X0A		;// set silence threshold
	CALL	SET_SILENCE
	
	LACL	0X015
	SAH	PRO_VAR

	BIT	ANN_FG,13	;check memoful?
	BZ	TB,MAIN_PRO0_RECO_OGM1
	
;	LAC	VOI_ATT
;	SFR	8
;	ANDK	0X0F
;	CALL	OGM_SELECT
;	BS	ACZ,MAIN_PRO0_RECO_OGM1	;full but current OGM exist,so you can delete it then record new OGM !
	
	CALL	INIT_DAM_FUNC
	CALL	VP_MEMFUL
	CALL	BBBEEP		;memory full(BBB)
	
	LACK	0
	SAH	PRO_VAR

MAIN_PRO0_RECO_OGM1:	
	RET
;---------------------------------------
MAIN_PRO0_NEWVP_BVOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BLED_ON
	LAC	VOI_ATT
	XORL	0X8000
	SAH	VOI_ATT
	
	BIT	VOI_ATT,15
	BS	TB,MAIN_PRO0_NEWVP_LBVOP
	
	CALL	BEEP

	RET
MAIN_PRO0_NEWVP_LBVOP:
	CALL	LBEEP

	RET	
	
MAIN_PRO0_RECO_MSG:
	CALL	INIT_DAM_FUNC
	CALL	LBEEP
	CALL	DAA_SPK
	CALL	BLED_ON
	CALL	SET_COMPS
	;LACK	0X0D		;// set silence threshold
	LACK	0X0A
	CALL	SET_SILENCE

	LACL	0X013
	SAH	PRO_VAR

	BIT	ANN_FG,13	;check memoful?
	BS	TB,MAIN_PRO0_RECO_MSG1

	RET
MAIN_PRO0_RECO_MSG1:
	CALL	INIT_DAM_FUNC
	
	LACK	0
	SAH	PRO_VAR
	
	CALL	VP_MEMFUL
	CALL	BBBEEP		;memory full(BBB)
			
	RET
;-------
MAIN_PRO0_ERAS_MSG:
	CALL	BLED_ON
	CALL	MSG_CHK
	LAC	MSG_T
	BS	ACZ,MAIN_PRO0_ERAS_MSG1

	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	LACL	0X6080
	SAH	CONF
	CALL	DAM_BIOS
	
	CALL	TELNUMALL_DEL	;删除没有对应message的电话号码
	
	;CALL	VP_MESSAGES	;messages
	;CALL	VP_ERASE	;erase
	CALL	BB_VOP
	CALL	DAA_SPK
	
	BS	B1,MAIN_PRO0_ERAS_MSG_END
MAIN_PRO0_ERAS_MSG1:	


	CALL	VP_NO		;no
	CALL	VP_MESSAGES	;messages
	
	CALL	BB_VOP		;替换语音BB
	
	CALL	DAA_SPK
MAIN_PRO0_ERAS_MSG_END:	
	RET
;-------
MAIN_PRO0_ONOFF:
	CALL	BLED_ON
	LAC	MBOX_ID
	SBHK	1
	BZ	ACZ,MAIN_PRO0_ONOFF1
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_BSPK
	CALL	BEEP		;Beep 
	
	LAC	EVENT
	XORL	0X0200
	SAH	EVENT
	
	RET
MAIN_PRO0_ONOFF1:	;在私人信箱里工作后退回,若没有回到公共信箱则按下ON/OFF键时先回到公共信箱
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP		;BEEP
	
	LACK	1
	SAH	MBOX_ID
	
	RET
;-------
MAIN_PRO0_MBOX:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BLED_ON
	CALL	VP_MAILBOX	;mailbox
	LAC	MBOX_ID		;messages
	CALL	ANNOUNCE_NUM
	LACL	0X0050
	CALL	STOR_VP
	
	LACK	0X07
	SAH	PRO_VAR

	RET
MAIN_PRO0_MBOX_1:
	CALL	DAA_SPK
	CALL	BLED_ON
	LACK	0X07
	SAH	PRO_VAR
	
	LAC	MSG
	CALL	STOR_MSG
	
	RET
;-------
MAIN_PRO0_PHONEBOOK:
	CALL	INIT_DAM_FUNC
	CALL	DAA_BSPK
	CALL	BEEP
	CALL	BLED_ON
	LACK	4
	CALL	SET_TELGROUP	;set group 4

	CALL	GET_TELT
	SAH	MSG_T		;取总数
	
	LACK	0
	SAH	MSG_N

	LACK	10
	SAH	PRO_VAR
	
	LACL	0X9E		;SEND COMMAND
	CALL	STOR_DAT
	LAC	MSG_N
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT

	RET
MAIN_PRO0_SMENU:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BLED_ON	
	LAC	VOI_ATT
	XORL	0X4000
	SAH	VOI_ATT
	
	BIT	VOI_ATT,14
	BS	TB,MAIN_PRO0_SMENU_BEEP
	
	CALL	BEEP
	
	RET
MAIN_PRO0_SMENU_BEEP:
	
	CALL	LBEEP
	
	RET
;-------

MAIN_PRO0_LMENU:		;INITIAL(PRO_VAR=0X0009)
	CALL	INIT_DAM_FUNC
	CALL	DAA_BSPK
	CALL	BEEP		;BEEP
	CALL	BLED_ON
;---发送菜单命令---语言选择
	LACK	9
	SAH	PRO_VAR
;---读出一些参数
	
	LACK	5
	CALL	SET_TELGROUP
	CALL	GET_TELT
	SAH	MSG_ID
	
	LACK	NEW8
	SAH	ADDR_D		;保存地址(在菜单调节部分不能修改)
	SAH	ADDR_S		;提取地址(在菜单调节部分不能修改)
	LAC	MSG_ID		;条目号
	CALL	READ_TELNUM	;读当前条目数据
	CALL	STOPREADDAT
MAIN_PRO0_MENU_2:
;---语言选择	
	LACL	0X90		;COMMAND = 0X90
	SAH	NEW7
	CALL	STOR_DAT

	LAC	VOI_ATT		;LANGUAGE/OGM_ID/RING_CNT/ATT1
	SFR	12
	ANDK	0X03
	SAH	NEW0		;CURRENT VALUE
	CALL	STOR_DAT
	LACK	0
	SAH	NEW1		;MIN VALUE
	LACK	1
	SAH	NEW2		;MAX VALUE

	RET
;-------------------------------------------------------------------------------
MAIN_PRO0_VOL:
	LACK	0X18
	SAH	PRO_VAR
	CALL	BLED_ON	
	LAC	MSG
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	RET
;-------------------------------------------------------------------------------
MAIN_PRO0_TMR:
	
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	3
	BS	ACZ,MAIN_PRO0_TMR_3s
	SBHK	7
	BS	ACZ,MAIN_PRO0_TMR_10s
	SBHK	50
	BS	ACZ,MAIN_PRO0_TMR_60s
	
MAIN_PRO0_TMR_END:	
	RET
;---------------
MAIN_PRO0_TMR_3s:
	;CRAM	ANN_FG,2	;防止收CID出错而屏蔽按键而导致死机
	
	RET
;---------------
MAIN_PRO0_TMR_10s:
	;CALL	CLR_TIMER
	CALL	BLED_OFF
	
	LAC	MBOX_ID
	SBHK	1
	BS	ACZ,MAIN_PRO0_TMR_END
	
	LACK	1
	SAH	MBOX_ID
	
	CALL	DAA_SPK	
	CALL	BBEEP		;BB
	
	RET
;---------------
MAIN_PRO0_TMR_60s:
	LAC	ANN_FG
	ANDL	0X00E0
	BS	ACZ,MAIN_PRO0_TMR_END
;---New ICM VP exist
	BIT	VOI_ATT,15
	BZ	TB,MAIN_PRO0_TMR_END
			
	CALL	DAA_SPK	
	CALL	BEEP		;New message exist Beep every 1 minute
	
	RET
;-------------------------------------------------------------------------------
MAIN_PRO0_BLEDON:
	CALL	BLED_ON
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER	;重进入待机计时
	
	RET
;-------------------------------------------------------------------------------
MAIN_PRO0_INCOMECALL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_BSPK
	CALL	BEEP
	CALL	BLED_ON
	CALL	CLR_LED1FG	;clear new call flag

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	6
	SAH	PRO_VAR
	
	LAC	MSG
	CALL	STOR_MSG
	
	LACK	0
	CALL	SET_TELGROUP	;
	CALL	GET_TELT
	SAH	MSG_T		;总条目号MSG_T
	
	LACK	0
	SAH	MSG_ID		;当前条目号

	LACL	200
	CALL	DELAY
	
	RET
MAIN_PROX_RINGIN:
	CALL	INIT_DAM_FUNC
	LACK	0
	SAH	PRO_VAR
	CALL	BLED_ON
	LACL	CMSG_INIT
	CALL	STOR_MSG
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0X9E
	CALL	STOR_DAT
	LACL	6
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!	
	
	RET
;===================================================================
MAIN_PRO1:	;播放(MSG)状态要考虑的消息:

	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PROX_RINGIN
	
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,MAIN_PRO1_0
	SBHK	1
	BS	ACZ,MAIN_PRO1_1
MAIN_PRO1_0:	
;MAIN_PRO1_0_1:	
	LAC	MSG
	XORL	CMSG_KEY1S		;pause
	BS	ACZ,MAIN_PRO1_PLAY_PAUSE
;MAIN_PRO1_0_2:
	LAC	MSG
	XORL	CMSG_KEY5S		;erase
	BS	ACZ,MAIN_PRO1_PLAY_ERASE
;MAIN_PRO1_0_3:
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF(stop)
	BS	ACZ,MAIN_PRO1_PLAY_STOP
;MAIN_PRO1_0_4:
	LAC	MSG
	XORL	CMSG_KEYAP		;VOL+
	BS	ACZ,MAIN_PRO1_PLAY_VOLA
;MAIN_PRO1_5:
	LAC	MSG
	XORL	CMSG_KEYAS		;VOL+
	BS	ACZ,MAIN_PRO1_PLAY_VOLA
;MAIN_PRO1_6:
	LAC	MSG
	XORL	CMSG_KEYBP		;VOL-
	BS	ACZ,MAIN_PRO1_PLAY_VOLS
;MAIN_PRO1_7:
	LAC	MSG
	XORL	CMSG_KEYBS		;VOL-
	BS	ACZ,MAIN_PRO1_PLAY_VOLS
;MAIN_PRO1_8:
;MAIN_PRO1_9:
	LAC	MSG
	XORL	CMSG_KEYCS		;FFW
	BS	ACZ,MAIN_PRO1_PLAY_FFW
;MAIN_PRO1_10:
	LAC	MSG
	XORL	CMSG_KEY8S		;REW
	BS	ACZ,MAIN_PRO1_PLAY_REW
;MAIN_PRO1_11:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,MAIN_PRO1_PLAY_OVER
;MAIN_PRO1_12:	
	;LAC	MSG
	;XORL	CLINE_CONNT
	;BS	ACZ,MAIN_PRO1_PLAY_LINE
		
	RET

;-------消息处理
;MAIN_PRO1_PLAY_LINE:
;	CALL	DAA_BSPK
;	RET

MAIN_PRO1_PLAY_PAUSE:
	LAC	CONF
	SFR	12
	SBHK	2
	BS	ACZ,MAIN_PRO1_PLAY_PAUSE_DONE	;0X2000
	SBHK	9
	BS	ACZ,MAIN_PRO1_PLAY_PAUSE_DONE	;0XB000
	
	RET
MAIN_PRO1_PLAY_PAUSE_DONE:	
	LACK	2
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	SRAM	CONF,8
	CALL	DAM_BIOS
;???????????????????????????
	LACL	0XC8
	CALL	STOR_DAT
	LAC	MSG_ID
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;???????????????????????????	
	RET
MAIN_PRO1_PLAY_ERASE:
;???????????????????????????
	;LACL	0X83
	;CALL	STOR_DAT
	;LAC	MSG_ID
	;CALL	STOR_DAT
	;LACL	0XF3
	;CALL	STOR_DAT
;???????????????????????????
	CALL	INIT_DAM_FUNC
	CALL	DEL_VPMSG
	
	;LAC	MSG_ID
	;CALL	SET_DELMARK

	LAC	MSG_ID
	CALL	GET_USRDAT
	CALL	GET_TELID
	CALL	DEL_ONETEL
	CALL	TEL_GC_CHK
	CALL	GC_CHK

	CALL	INIT_DAM_FUNC
	CALL	B_VOP

	RET

;-------
MAIN_PRO1_PLAY_VOLA:
	
	LACL	CMSG_VOLA
	CALL	STOR_MSG

	RET

MAIN_PRO1_PLAY_VOLS:
	
	LACL	CMSG_VOLS
	CALL	STOR_MSG

	RET

;-------
MAIN_PRO1_PLAY_FFW:
	CALL	INIT_DAM_FUNC
	CALL	SETOLD_VPMSG

	LACL	CVP_STOP
	CALL	STOR_MSG
MAIN_PRO1_PLAY_DONE:
	RET
;-------

MAIN_PRO1_PLAY_REW:		;从
	LAC	FILE_ID
	BS	ACZ,MAIN_PRO1_PLAY_DONE 	;开始吗?
	
	CALL	INIT_DAM_FUNC
	CALL	SETOLD_VPMSG
	
	LAC	FILE_ID
	SBHK	1
	BS	ACZ,MAIN_PRO1_PLAY_REW_1 	;开始吗?
	LAC	MSG_ID
	ANDL	0X0FF
	SBHK	1
	BS	ACZ,MAIN_PRO1_PLAY_REW_1	;第一个吗?

	LAC	MSG_ID
	SBH	FILE_LEN
	SBHK	1
	SAH	MSG_ID
	
	LAC	FILE_ID
	SBHK	1
	SAH	FILE_ID
MAIN_PRO1_PLAY_REW_1:	
	BIT	ANN_FG,12
	BZ	TB,MAIN_PRO1_PLAY_REW_2

	CALL	REW_MANAGE
MAIN_PRO1_PLAY_REW_2:
	LAC	MSG_ID
	CALL	GET_RVPLEN
	
	BS	B1,MAIN_PRO1_PLAYID_READY
;---------------------------------------------------------------------
MAIN_PRO1_PLAY_OVER:
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,MAIN_PRO1_PLAY_STOP
;MAIN_PRO1_PLAY_OVER1:
	LAC	FILE_ID			;next message
	ADHK	1
	SAH	FILE_ID

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

	BIT	ANN_FG,12
	BZ	TB,MAIN_PRO1_PLAYID_READY
	CALL	FFW_MANAGE
	BZ	ACZ,MAIN_PRO1_PLAY_STOP	;THE LAST ONE	
MAIN_PRO1_PLAYID_READY:			;Get the msg_id you will play
	CALL	DAA_SPK
;???????????????????????????????
;bs	b1,MAIN_PRO1_PLAY_LOADVP
;bs	b1,MAIN_PRO1_PLAY_OVER2_1
;???????????????????????????????
;!!!!!!!!!!!!!!!!!!!!!!!!!!!	SEND_TELNUM
	LAC	MSG_ID
	CALL	GET_USRDAT
	CALL	GET_TELID
	BS	ACZ,MAIN_PRO1_PLAY_OVER2_1	;
	SAH	MSG_N

	LACK	NEW8
	SAH	ADDR_D		;保存地址
	LAC	MSG_N		;条目号
	CALL	READ_TELNUM	;读当前条目数据
	CALL	STOPREADDAT
	
MAIN_PRO1_PLAY_OVER2_0:		;号码准备完毕,可以发送
	LACK	NEW8
	SAH	ADDR_S
	CALL	SET_WAITFG
	SRAM	EVENT,10
;---发标志
	LACL	0X80
	CALL	STOR_DAT
	LACL	0X80
	CALL	STOR_DAT
	LACK	NEW8
	SAH	ADDR_S		;BASE
	LACK	0
	SAH	COUNT		;offset
	LACK	4		;length
	CALL	TOSENDBUF
	LACL	0XFF
	CALL	STOR_DAT
	
	LACK	0X0011
	SAH	PRO_VAR		;send command

	LACK	0
	SAH	PRO_VAR1
	LACL	500
	CALL	SET_TIMER

	RET

;!!!!!!!!!!!!!!!
MAIN_PRO1_PLAY_OVER2_1:
	LACK	NEW8
	SAH	ADDR_S
	SAH	ADDR_D
	
	SRAM	EVENT,10
;---	
	LACK	0
	SAH	COUNT
	CALL	STORBYTE_DAT
	LACK	1
	SAH	COUNT
	LACK	0
	CALL	STORBYTE_DAT
	LACK	2
	SAH	COUNT
	LACL	0X84
	CALL	STORBYTE_DAT
	LACK	3
	SAH	COUNT
	LACK	0
	CALL	STORBYTE_DAT
;---	
	LACK	4
	SAH	COUNT
	LAC	MSG_ID
	CALL	GET_MSGMONTH	;month
	CALL	STORBYTE_DAT
	LACK	5
	SAH	COUNT
	LAC	MSG_ID
	CALL	GET_MSGDAY	;day
	CALL	STORBYTE_DAT
	LACK	6
	SAH	COUNT
	LAC	MSG_ID
	CALL	GET_MSGHOUR	;hour
	CALL	STORBYTE_DAT
	LACK	7
	SAH	COUNT
	LAC	MSG_ID
	CALL	GET_MSGMINUTE	;minute
	CALL	STORBYTE_DAT
	BS	B1,MAIN_PRO1_PLAY_OVER2_0
MAIN_PRO1_PLAY_LOADVP:
	CALL	INIT_DAM_FUNC
.if	1
	CALL	VP_MESSAGE		;VP"message"
	LAC	FILE_ID			;message_ID	;报出的
	CALL	ANNOUNCE_NUM	
	
	CALL	MSG_WEEK
	CALL	MSG_HOUR
	CALL	MSG_MIN

	LAC	MSG_ID
	CALL	GET_VPLEN
	CALL	GET_VPMSG
.else

.endif
	RET	
MAIN_PRO1_1:
;MAIN_PRO1_1_0:
	LAC	MSG	
	XORL	CBUF_EMPTY
	BS	ACZ,MAIN_PRO1_1_SENDCOMM
;MAIN_PRO1_1_1:
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,MAIN_PRO1_1_TMR	;2*0.5 = 1秒钟,防通信故障死机
	
	LAC	MSG
	CALL	STOR_MSG	;其它消息不在此地处理
	
	RET
MAIN_PRO1_1_TMR:
	LAC	PRO_VAR1	;+1/0.5s
	ADHK	1
	SAH	PRO_VAR1
	SBHK	2
	BZ	SGN,MAIN_PRO1_1_SENDCOMM_1
	
	RET
MAIN_PRO1_1_SENDCOMM:
.if	0
	LACL	0XCA
	CALL	STOR_DAT
	LAC	FILE_ID		;message_ID	;显示的(要与报的VP一致)
	CALL	STOR_DAT	;显示号码
	LACL	0XFF
	CALL	STOR_DAT
	
	LACL	0XC7
	CALL	STOR_DAT
	LAC	FILE_ID
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
	BS	B1,MAIN_PRO1_1_SENDCOMM_1
.endif	
	CALL	TEL_SENDCOMM	
	BS	ACZ,MAIN_PRO1_1_SENDCOMM_END	;0 --- 发送中(号码/姓名/时间/...)
	SBHK	1
	BS	ACZ,MAIN_PRO1_1_SENDCOMM_1	;1 --- over
	;SBHK	1
	;BS	ACZ,MAIN_PRO1_1_SENDCOMM_END	;2 --- display

	LACL	0XCA
	CALL	STOR_DAT
	LAC	FILE_ID		;message_ID	;显示的(要与报的VP一致)
	CALL	STOR_DAT	;显示号码
	LACL	0XFF
	CALL	STOR_DAT
	
	LACL	0XC7
	CALL	STOR_DAT
	LAC	FILE_ID
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT

MAIN_PRO1_1_SENDCOMM_END:
	
	RET
MAIN_PRO1_1_SENDCOMM_1:	
	
	LACK	0X001
	SAH	PRO_VAR
	BS	B1,MAIN_PRO1_PLAY_LOADVP

;-------
MAIN_PRO1_PLAY_STOP:
	CALL	INIT_DAM_FUNC
	CALL	SETOLD_VPMSG
	CALL	REAL_DEL	;0x6100
	
	CALL	MSG_CHK
	CALL	NEWICM_CHK	;
	
	CALL	DAA_SPK
	CALL	VP_ENDOF
	CALL	VP_MESSAGES
	CALL	BB_VOP

;!!!!!!!!!!!!!!!!!!!!!!!!
;	LACL	0XC9
;	CALL	STOR_DAT
;	LAC	FILE_ID
;	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!
	LACK	0X0010
	SAH	PRO_VAR
	
	RET

;=================================================================================
MAIN_PRO2:	;暂停(MSG)状态要考虑的消息:
	LAC	MSG
	XORL	CMSG_KEY1S		;PLAY
	BS	ACZ,MAIN_PRO2_CPLAY
;MAIN_PRO2_1:
	LAC	MSG
	XORL	CMSG_KEY6S		;stop
	BS	ACZ,MAIN_PRO1_PLAY_STOP
;MAIN_PRO2_2:
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,MAIN_PRO2_TMR
	
	RET
;-------
MAIN_PRO2_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	8
	BZ	SGN,MAIN_PRO1_PLAY_STOP
	
	RET
;-------
MAIN_PRO2_CPLAY:
	LAC	PRO_VAR
	ANDL	0XFFF0
	ORK	1
	SAH	PRO_VAR
	
	CRAM	CONF,8
	CALL	DAM_BIOS
;!!!!!!!!!!!!!!!!!!!!!!!!!!!	
	LACL	0XC7
	CALL	STOR_DAT
	LAC	MSG_ID
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT	
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
MAIN_PRO2_CPLAY_END:
	RET

;=================================================================================
MAIN_PRO3:	;录音(MSG)状态要考虑的消息:
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PROX_RINGIN
	
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,MAIN_PRO3_2
;MAIN_PRO3_1_0:	
	LAC	MSG
	XORL	CMSG_KEY6S		;press ON/OFF key when playing voice prompt
	BS	ACZ,MAIN_PRO3_REC_END_1
;MAIN_PRO3_1_1:	
	LAC	MSG
	XORL	CVP_STOP		;end of the voice prompt and begin to record
	BS	ACZ,MAIN_PRO3_RECMEMO
;MAIN_PRO3_1_2:	
	
	RET
;---	
MAIN_PRO3_2:	
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF
	BS	ACZ,MAIN_PRO3_REC_END
;MAIN_PRO3_2_1:
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,MAIN_PRO3_REC_TMR
;MAIN_PRO3_2_2:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,MAIN_PRO3_REC_FUL
	RET
;-------处理消息

MAIN_PRO3_RECMEMO:
	CALL	MSG_CHK
	CALL	GET_FILEID
	CALL	SET_USRDAT
	
	LACK	CTAG_MEMO		;set memo flag
	CALL	SET_USRDAT1
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC5
	CALL	STOR_DAT
	LACK	0
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	;CALL	DAA_REC_LINMEMO
	CALL	DAA_REC
;MAIN_PRO3_REC:		;for MEMO/OGM record
MAIN_PRO5_REC:
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	CALL	REC_START
	
	RET
MAIN_PRO3_REC_FUL:
	CALL	INIT_DAM_FUNC	;stop recording
	CALL	GC_CHK
	
	CALL	MEMFULL_CHK
	BS	ACZ,MAIN_PRO3_REC_FUL1

	CALL	VP_MEMFUL
	CALL	BB_VOP
	CALL	DAA_SPK	

	LACK	0
	SAH	PRO_VAR
		
	RET
MAIN_PRO3_REC_FUL1:
	CALL	REC_START
	
	RET
;---
MAIN_PRO3_REC_END:
	LAC	CONF
	ANDL	0XFFC0
	SAH	CONF

	LAC	PRO_VAR1
	SBHK	3
	BZ	SGN,MAIN_PRO3_REC_END_1		;要求满足录音时间t>=3s才能保留
	SRAM	CONF,11
	CALL	DAM_BIOS
MAIN_PRO3_REC_END_1:
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK	
	CALL	BB_VOP	;替换语音BB
	
	;CALL	GC_CHK
;!!!!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC6
	CALL	STOR_DAT
	LACK	0
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!!!!

	LACK	0
	SAH	PRO_VAR

	RET

MAIN_PRO3_REC_TMR:
	LAC	CONF
	ANDL	0XFFC0
	SAH	CONF		;防止掐尾
	
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	
	RET

	
;================================================================================
MAIN_PRO4:	;播放(OGM)状态要考虑的消息:
	LAC	MSG
	XORL	CMSG_KEY5S		;ERASE
	BS	ACZ,MAIN_PRO4_EXE1
;MAIN_PRO4_1:	
	LAC	MSG
	XORL	CMSG_KEY3S		;next OGM
	BS	ACZ,MAIN_PRO4_NEXTOGM
;MAIN_PRO4_2:
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF(stop)
	BS	ACZ,MAIN_PRO4_EXE2
;MAIN_PRO4_3:
	LAC	MSG
	XORL	CVP_STOP		;play end
	BS	ACZ,MAIN_PRO4_EXE2
;MAIN_PRO4_4:	
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PROX_RINGIN
;MAIN_PRO4_5:
	LAC	MSG
	XORL	CMSG_KEYAP		;VOL+
	BS	ACZ,MAIN_PRO1_PLAY_VOLA
;MAIN_PRO4_6:
	LAC	MSG
	XORL	CMSG_KEYAS		;VOL+
	BS	ACZ,MAIN_PRO1_PLAY_VOLA
;MAIN_PRO4_7:
	LAC	MSG
	XORL	CMSG_KEYBP		;VOL-
	BS	ACZ,MAIN_PRO1_PLAY_VOLS
;MAIN_PRO4_8:
	LAC	MSG
	XORL	CMSG_KEYBS		;VOL-
	BS	ACZ,MAIN_PRO1_PLAY_VOLS
	
	RET
;-------响应按键,消息转换
MAIN_PRO4_NEXTOGM:
	CALL	INIT_DAM_FUNC
	
	LACK	1
	SAH	SYSTMP1
	LACK	5
	SAH	SYSTMP2

	LAC	MSG_N
	CALL	VALUE_ADD
	SAH	MSG_N
	CALL	OGM_SELECT

	LAC	MSG_ID
	ANDL	0X0FF
	ORL	0XFE00
	CALL	STOR_VP
	
	LAC	MSG_N
	CALL	OGM_SELECT
	BS	ACZ,MAIN_PRO4_NEXTOGM_1
	
	CALL	INIT_DAM_FUNC
	
	LAC	MSG_N
	CALL	DEFOGM_LOCALPLY		;load default OGM
	;CALL	VP_DEFAULTOGM	;default OGM
MAIN_PRO4_NEXTOGM_1:
	BIT	EVENT,8
	BS	TB,MAIN_PRO4_NEXTOGM_2		;只答不录状态不可变OGM_ID
	
	LAC	VOI_ATT
	ANDL	0XF0FF
	SAH	VOI_ATT
	
	LAC	MSG_N
	SFL	8
	ANDL	0X0F00
	OR	VOI_ATT
	SAH	VOI_ATT
MAIN_PRO4_NEXTOGM_2:
;!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC3
	CALL	STOR_DAT
	LAC	MSG_N
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!	

	RET
MAIN_PRO4_EXE1:
	LAC	MSG_ID
	ANDL	0X0FF
	ORL	0X2080
	SAH	CONF
	CALL	DAM_BIOS
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	CALL	INIT_DAM_FUNC
	
	LACK	0
	SAH	PRO_VAR

	CALL	BB_VOP
	
	RET
MAIN_PRO4_EXE2:
	CALL	INIT_DAM_FUNC
	CALL	BB_VOP
	
	LACK	0
	SAH	PRO_VAR

	RET
;================================================================================
MAIN_PRO5:	;录音(OGM)状态要考虑的消息:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,MAIN_PRO5_4
;MAIN_PRO5_0_1:	
	;LAC	MSG
	;XORL	CMSG_KEY3S		;release OGM key when playing voice prompt
	;BS	ACZ,MAIN_PRO5_5		;当LBEEP响起后再不响应OGM键
;MAIN_PRO5_0_2:	
	LAC	MSG
	XORL	CVP_STOP		;end of the voice prompt and begin to record
	BS	ACZ,MAIN_PRO5_0_VPSTOP
;MAIN_PRO5_0_3:		
	LAC	MSG
	XORL	CRING_IN
	BS	ACZ,MAIN_PROX_RINGIN
	
	RET
MAIN_PRO5_:
	
MAIN_PRO5_0_VPSTOP:
	LAC	VOI_ATT
	SFR	8
	ANDK	0X0F
	CALL	OGM_SELECT
;-------delete the old OGM----------------
	LAC	MSG_ID
	CALL	MSG_DEL
	CALL	GC_CHK
;---	
    	LAC	MSG_N
    	ADHK	100
    	ORL	0X7500
    	SAH	CONF
    	CALL	DAM_BIOS		;set user index data0"OGM_ID"
    	
    	CALL	DAA_REC
;!!!!!!!!!!!!!!!!!!!!!!!!
	LACL	0XC1
	CALL	STOR_DAT
	LAC	VOI_ATT		;LANGUAGE/OGM_ID/RING_CNT/ATT1
	SFR	8
	ANDK	0X0F
	CALL	STOR_DAT
;!!!!!!!!!!!!!!!!!!!!!!!!

	BS	B1,MAIN_PRO5_REC
;---	
MAIN_PRO5_4:	
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF
	BS	ACZ,MAIN_PRO5_5
;MAIN_PRO5_4_1:
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,MAIN_PRO5_6	
;MAIN_PRO5_4_2:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,MAIN_PRO5_REC_FUL
	
	RET
MAIN_PRO5_5:
	lac	RESP
	SFR	9
	SBHK	2
	BZ	SGN,MAIN_PRO5_5_1
	
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,MAIN_PRO5_5_1
	SRAM	CONF,11
	CALL	DAM_BIOS
MAIN_PRO5_5_1:
	CALL	INIT_DAM_FUNC
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_KEY3S		;停止录音(OGM)相当于播放(OGM)
	CALL	STOR_MSG
		
	RET
MAIN_PRO5_6:
;????????????????
.if	0

	LACL	0XC1
	CALL	STOR_DAT
	lac	RESP
	SFR	9
	CALL	STOR_DAT
.endif
;????????????????
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	120
	BZ	SGN,MAIN_PRO5_5
	
	RET
MAIN_PRO5_REC_FUL:		;for OGM record
	CALL	CLR_TIMER

	CALL	INIT_DAM_FUNC		;stop recording
	
	RET

;-------------------------------------------------------------------------------
MAIN_PRO7:	;MBOX设置状态要考虑的消息
	LAC	MSG
	XORL	CMSG_KEY7S
	BS	ACZ,MAIN_PRO7_END
	
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,MAIN_PRO7_0		;Play VP
	SBHK	1
	BS	ACZ,MAIN_PRO7_1		;End VP
	
	RET
;---------------
MAIN_PRO7_0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,MAIN_PRO7_0_1
	
	RET
MAIN_PRO7_0_1:
	LACK	0X0017
	SAH	PRO_VAR
	
	RET
	
MAIN_PRO7_1:
;MAIN_PRO7_1_1:	
	LAC	MSG
	XORL	CMSG_KEY7P
	BS	ACZ,MAIN_PRO7_1_2
	
	RET
MAIN_PRO7_1_2:
	CALL	INIT_DAM_FUNC
	
	LAC	MBOX_ID
	ADHK	1
	ANDK	0X3
	BZ	ACZ,MAIN_PRO7_1_2_1
	ADHK	1
MAIN_PRO7_1_2_1:
	SAH	MBOX_ID		;mbox-id(1,2,3)
	CALL	ANNOUNCE_NUM
	
	LACL	0X0050
	CALL	STOR_VP
	
	LACK	0X0007
	SAH	PRO_VAR
	
	RET

;---------------
MAIN_PRO7_END:
	CALL	INIT_DAM_FUNC
	
	LACK	0X0
	SAH	PRO_VAR

	CALL	VP_MAILBOX	;mailbox
	
	LAC	MBOX_ID		;mbox-id
	CALL	ANNOUNCE_NUM
	
	RET
;-------------------------------------------------------------------------------
MAIN_PRO8:	;VOL设置状态要考虑的消息
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,MAIN_PRO8_TMR
	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,MAIN_PRO8_VOL_STP
	
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,MAIN_PRO8_0_0_0
;---------------按键持续按下	
	LAC	MSG
	XORL	CMSG_KEYAS
	BS	ACZ,MAIN_PRO8_VOLAEND
;MAIN_PRO8_1_0_1:
	LAC	MSG
	XORL	CMSG_KEYBS
	BS	ACZ,MAIN_PRO8_VOLSEND
;MAIN_PRO8_1_0_2:
	LAC	MSG
	XORL	CMSG_KEYAP
	BS	ACZ,MAIN_PRO8_VOLA
;MAIN_PRO8_1_0_3:
	LAC	MSG
	XORL	CMSG_KEYBP
	BS	ACZ,MAIN_PRO8_VOLS
	RET
;---------------按键已松开
MAIN_PRO8_0_0_0:
	LAC	MSG
	SBHL	0X80
	BS	SGN,MAIN_PRO8_0_0_1
	LAC	MSG
	SBHL	0XE0
	BZ	SGN,MAIN_PRO8_0_0_1
	
	RET
MAIN_PRO8_0_0_1:
;---有其它键按下或其它外部事件	
	LAC	MSG
	CALL	STOR_MSG

	LACK	0
	SAH	PRO_VAR
		
	RET
;---------------
MAIN_PRO8_VOLAEND:
	LACK	0X08
	SAH	PRO_VAR

MAIN_PRO8_VOLA:

	LACL	CMSG_VOLA
	CALL	STOR_MSG
	
	LAC	VOI_ATT
	ANDK	0X7
	SBHK	5
	BZ	SGN,MAIN_PRO8_VOL_VPW
	
	LACL	0XCB
	CALL	STOR_DAT
	LAC	VOI_ATT
	ANDK	0X7
	ADHK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
	BS	B1,MAIN_PRO8_VOL_VPN
;-------------------------------------------------------------------------------
MAIN_PRO8_VOLSEND:
	LACK	0X8
	SAH	PRO_VAR

MAIN_PRO8_VOLS:

	CALL	INIT_DAM_FUNC
	LACL	CMSG_VOLS
	CALL	STOR_MSG
	LAC	VOI_ATT
	ANDK	0X7
	SBHK	2
	BS	SGN,MAIN_PRO8_VOL_VPW
	
	LACL	0XCB
	CALL	STOR_DAT
	LAC	VOI_ATT
	ANDK	0X7
	SBHK	1
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
MAIN_PRO8_VOL_VPN:
	CALL	INIT_DAM_FUNC	
	CALL	BEEP
	BS	B1,MAIN_PRO8_VOL_END
MAIN_PRO8_VOL_VPW:
	LACL	0XCB
	CALL	STOR_DAT
	LAC	VOI_ATT
	ANDK	0X7
	CALL	STOR_DAT
	LACL	0XFF
	CALL	STOR_DAT
	CALL	INIT_DAM_FUNC
	CALL	BBBEEP
MAIN_PRO8_VOL_END:
	CALL	DAA_SPK
	
	RET	

MAIN_PRO8_VOL_STP:
	CALL	DAA_OFF
	
	LACK	0
	SAH	PRO_VAR1
	
	RET	


MAIN_PRO8_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	3
	BS	SGN,MAIN_PRO8_TMR_END
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG	;超时退出
MAIN_PRO8_TMR_END:	
	RET
;=========================================================================
MAIN_PRO11:
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,MAIN_PRO11_TMR
;--
	LAC	MSG
	XORL	CMSG_KEY6S
	BS	ACZ,MAIN_PRO11_ONOFF
	
	RET
MAIN_PRO11_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	3
	BS	SGN,MAIN_PRO11_TMR_END
	
	CALL	DAA_SPK

	LACL	0X9041
	SAH	CONF
	CALL	DAM_BIOS

	CALL	INITBEEP

	LACK	0
	SAH	PRO_VAR
	CALL	GET_INITLANGUAGE
	CALL	INITMCU

	CALL	CLR_LED1FG	;clear new call flag
	
	LACK	3
	SAH	MBOX_ID
	CALL	MSG_CHK
        CALL	NEWICM_CHK
        
	LACK	2
	SAH	MBOX_ID
	CALL	MSG_CHK
        CALL	NEWICM_CHK
        
	LACK	1
	SAH	MBOX_ID
	CALL	MSG_CHK
	CALL	NEWICM_CHK

MAIN_PRO11_TMR_END:	
	RET
MAIN_PRO11_ONOFF:
	LACL	CMSG_KEY6S
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR
	
	RET
;=========================================================================
	
.END

