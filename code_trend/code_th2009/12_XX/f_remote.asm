.LIST
REMOTE_PRO:
	LAC	MSG			;接线后按ON/OFF(相当于CPC)
	XORL	CMSG_KEY7S
	BS	ACZ,REMOTE_PRO_STOP

	;LAC	MSG			;接线后摘机(相当于CPC)
	;XORL	CPARA_MINE
	;BS	ACZ,REMOTE_PRO_PMINE
	
	LAC	PRO_VAR
	ANDL	0X0F
	BS	ACZ,REMOTE_PRO0		;0 = IDEL
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW	;1 = play new
	SBHK	1
	BS	ACZ,REMOTE_PROPALL	;2 = play all
	SBHK	1
	BS	ACZ,REMOTE_PRODALL	;3 = Del all old VP
	SBHK	1
	BS	ACZ,REMOTE_PROMVOP	;4 = Play Menu vop
	SBHK	1
	;BS	ACZ,REMOTE_PROONFF	;5 = on/off vop
	SBHK	2
	BS	ACZ,REMOTE_PROOGM	;7 = record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PROROMMONITOR	;8 = room monitor
	SBHK	1
	BS	ACZ,REMOTE_PROEXIT	;9 = exit

	RET
;---------------
REMOTE_PRO_STOP:
;REMOTE_PRO_PMINE:
	LACL	CMSG_CPC
	CALL	STOR_MSG	;当作CPC一样处理
	
	RET

;===============================================================================
REMOTE_PRO0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL(发两声BEEP)
	BS	ACZ,REMOTE_PRO0_INIT
;REMOTE_PRO0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRO0_REV_DTMF
;REMOTE_PRO0_2:	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PRO0_STOPVP	;VP完
;REMOTE_PRO0_3:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRO0_TONE
;REMOTE_PRO0_4:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRO0_TONE
;REMOTE_PRO0_5:	
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,REMOTE_PRO0_TMR
;REMOTE_PRO0_6:		
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRO0_CPC
		
	RET
REMOTE_PRO0_CPC:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACL	0X005
	CALL	STOR_VP
	CALL	VPMSG_CHK
	
	LACK	9
	SAH	PRO_VAR

	RET
REMOTE_PRO0_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP		;BB
	
	BS	B1,REMOTE_PRO0_MENUEXE
;---------------------------------------
REMOTE_PRO0_STOPVP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	CALL	LINE_START
	
	LACK	0			;开始计时
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	CALL	BCVOX_INIT
	
	RET
	
REMOTE_PRO0_REV_DTMF:
	CALL	CLR_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	CALL	BCVOX_INIT
	
	LAC	DTMF_VAL
	ANDK	0X0F
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PLYNEW		;1---play new
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PLYALL		;2---play all
	SBHK	5
	BS	ACZ,REMOTE_PRO0_RECOGM		;7---record/play OGM
	SBHK	1
	BS	ACZ,REMOTE_PRO0_INDOORMONITOR	;8---in door monitor
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PROONOFF	;9---on/off
	SBHK	5
	BS	ACZ,REMOTE_PRO0_MENU		;*---menu
	SBHK	1
	BS	ACZ,REMOTE_PRO0_EXIT		;#---Exit
	
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	XORK	0X023
	BS	ACZ,REMOTE_PRO0_REAALL	;"3","0"

	RET
REMOTE_PRO0_REAALL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	VP_DoYouWantToDelAllMessage
	
	LACK	3
	SAH	PRO_VAR
	
	RET

;---------------------------------------
REMOTE_PRO0_PLYNEW:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	VP_YouHave	;VOP

	CALL	CLR_FLAG
	CALL	VPMSG_CHK
	BIT	ANN_FG,12
	BS	TB,REMOTE_PRO0_PLYNEW_1

	CALL	VP_No
	CALL	VP_NEW
	CALL	VP_Messages
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR
	RET
REMOTE_PRO0_PLYNEW_1:
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	1
	SAH	PRO_VAR

	LACK	0
	SAH	MSG_ID

	LAC	MSG_N
	CALL	ANNOUNCE_NUM
	CALL	VP_NEW

	LAC	MSG_N
	SBHK	1
	BS	ACZ,REMOTE_PRO0_PLYNEW_1_1

	CALL	VP_Messages
	RET
REMOTE_PRO0_PLYNEW_1_1:
	CALL	VP_Message
	
	RET	
	
REMOTE_PRO0_PLYALL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	VP_YouHave	;VOP

	CALL	VPMSG_CHK
	LACK	0
	SAH	MSG_ID
	SAH	FILE_ID
	
	BIT	ANN_FG,14
	BS	TB,REMOTE_PRO0_REV_DTMF_2_1

	CALL	VP_No
	CALL	VP_Messages
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR
	
	RET

REMOTE_PRO0_REV_DTMF_2_1:
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	2
	SAH	PRO_VAR

	LAC	MSG_T
	CALL	ANNOUNCE_NUM

	LAC	MSG_T
	SBHK	1
	BS	ACZ,REMOTE_PRO0_REV_DTMF_2_1_1
	
	CALL	VP_Messages
	RET
REMOTE_PRO0_REV_DTMF_2_1_1:	
	CALL	VP_Message
	RET

REMOTE_PRO0_RECOGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	BIT	ANN_FG,13
	BS	TB,REMOTE_PROX_DTMF_MFUL
	
	LACK	0X0007
	SAH	PRO_VAR
	CALL	LBEEP		;Lbee...

	
	RET

REMOTE_PRO0_RECMEMO:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	BIT	ANN_FG,13
	BS	TB,REMOTE_PROX_DTMF_MFUL
	
	LACK	0X0003
	SAH	PRO_VAR
	CALL	LBEEP		;Lbee...

	RET
REMOTE_PROX_DTMF_MFUL:		;mfull 直接退出
	CALL	BBBEEP
	LACK	0
	SAH	PRO_VAR

	RET

REMOTE_PRO0_INDOORMONITOR:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	LBEEP		;Lbee...
		
	LACK	0X0008
	SAH	PRO_VAR
	
	RET

REMOTE_PRO0_PROONOFF:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LAC	EVENT
	XORL	1<<9
	SAH	EVENT
	
	LACK	0
	SAH	PRO_VAR

	BIT	EVENT,9
	BS	TB,REMOTE_PRO0_PROOFF
;REMOTE_PRO0_PROON:
	CALL	VP_AnswerOn
	;CALL	BBEEP
	
	RET
REMOTE_PRO0_PROOFF:
	CALL	VP_AnswerOff
	;CALL	BBEEP

	RET

;---------------------------------------
REMOTE_PRO0_MENU:

	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
REMOTE_PRO0_MENUEXE:
        LACL    VP_MENU_QUEUE
        SAH	ADDR_S

	LAC	ADDR_S
        CALL	GetOneConst
        SAH	MSG_T

	LACK	1
	SAH	MSG_ID

	LACK	0X03D
	CALL	STOR_VP		;延时0.5 second

	LAC	ADDR_S
	ADH	MSG_ID
	CALL	GetOneConst
        ORL	0XFF00
        CALL	STOR_VP
        
	LACK	4
	SAH	PRO_VAR

	RET
;---
REMOTE_PRO0_EXIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
		
	LACK	9
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP

	RET
REMOTE_PRO0_TONE:
	BS	B1,REMOTE_PRO0_EXIT
REMOTE_PRO0_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10		;10s无操作退出
	;SBHK	20		;20s无操作退出
	BZ	SGN,REMOTE_PRO0_EXIT
	
	RET	
;=======================================================================
;11111111111111111111111111111111111111111111111111111111111111111111111
REMOTE_PROPNEW:				;播放新message
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PROPNEW_CPC
	
	LAC	PRO_VAR
	SFR	4
	ANDK	0XF
	BS	ACZ,REMOTE_PROPNEW0
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE		;功能暂停
	SBHK	1
	BS	ACZ,REMOTE_PROXTMR_PAUSE	;时间暂停
	
	RET
;---------------
REMOTE_PROPNEW_CPC:
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACL	0X005
	CALL	STOR_VP
	
	CALL	REAL_DEL
	CALL	GC_CHK
	CALL	VPMSG_CHK
	
	LACK	9
	SAH	PRO_VAR
	
	RET
;---------------
REMOTE_PROPNEW0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PROPNEW_OVER
;REMOTE_PROPNEW0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROPNEW_DTMF
;REMOTE_PROPNEW0_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PROEXIT_VPSTOP
;REMOTE_PROPNEW0_3:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROPNEW0_TMR
;REMOTE_PROPNEW0_4:	

	RET
;---------------处理消息
REMOTE_PROPNEW0_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1

	RET

REMOTE_PROPNEW_OVER:
	LAC	MSG_ID
	SBH	MSG_N
	BZ	SGN,REMOTE_PROX_PLYSTOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

REMOTE_PROPNEW_LOADVP:
	CALL	VP_Message
	LAC	MSG_ID
	CALL	ANNOUNCE_NUM

	CALL	MSG_WEEKNEW
	CALL	MSG_HOURNEW
	CALL	MSG_MINNEW

	LAC	MSG_ID
	ORL	0XFD00
	CALL	STOR_VP

	RET

;-------
REMOTE_PROPNEW_DTMF:	
	LAC	DTMF_VAL
	ANDK	0X0F
	SBHK	3
	BS	ACZ,REMOTE_PROPNEW_ERAPLYING	;3---erase playing message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_PLYPRE	;4---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_REPEAT	;5---repeat message
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_NEXT		;6---skip to next message
	SBHK	8
	BS	ACZ,REMOTE_PROX_DO_PAUSE	;*---remote prompt
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP		;#---end play VP

	RET
;---------------
REMOTE_PROPNEW_ERAPLYING:
	LAC	MSG_ID
	CALL	SET_DELMARKNEW
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	
	RET

;-------
REMOTE_PROPNEW_PLYPRE:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_PLYPRE_DONE

	LAC	MSG_ID
	SBHK	1
	SAH	MSG_ID	
REMOTE_PROPNEW_PLYPRE_DONE:
	BS	B1,REMOTE_PROPNEW_LOADVP
;-------
REMOTE_PROPNEW_REPEAT:
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPNEW_LOADVP
;-------
REMOTE_PROPNEW_NEXT:
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPNEW_OVER
;-------
REMOTE_PROX_DO_PAUSE:
	LAC	CONF
	ORL	1<<8
	CALL	DAM_BIOSFUNC
	
	LAC	PRO_VAR
	ORK	0X10
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
	
	RET
REMOTE_PROX_DO_CONPLY:
	LAC	CONF
	ANDL	~(1<<8)
	CALL	DAM_BIOSFUNC
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	RET
;---------------------------------------
REMOTE_PROX_PAUSE:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROX_PAUSE_DTMF
REMOTE_PROX_PAUSE_1:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROX_PAUSE_TIMER
	
	RET
;---------------
REMOTE_PROX_PAUSE_DTMF:
	
	LAC	DTMF_VAL
	ANDL	0X0F
	SBHK	3
	BS	ACZ,REMOTE_PROX_PAUSE_ERAPLYING	;3---erase playing message
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE_PLYPRE	;4---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE_REPEAT	;5---repeat message
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE_NEXT	;6---skip to next message
	SBHK	8
	BS	ACZ,REMOTE_PROX_DO_CONPLY	;*---play again
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP		;#---End play VP	

	RET
;---------------
REMOTE_PROX_PAUSE_ERAPLYING:
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LAC	PRO_VAR
	ANDK	0X0F
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_ERAPLYING
	BS	B1,REMOTE_PROPALL_ERAPLYING
REMOTE_PROX_PAUSE_PLYPRE:
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LAC	PRO_VAR
	ANDK	0X0F
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_PLYPRE
	BS	B1,REMOTE_PROPALL_PLYPRE
REMOTE_PROX_PAUSE_REPEAT:
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LAC	PRO_VAR
	ANDK	0X0F
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_REPEAT
	BS	B1,REMOTE_PROPALL_REPEAT
REMOTE_PROX_PAUSE_NEXT:
	LAC	PRO_VAR
	ANDL	0XFF0F
	SAH	PRO_VAR
	
	LAC	PRO_VAR
	ANDK	0X0F
	SBHK	1
	BS	ACZ,REMOTE_PROPNEW_NEXT
	BS	B1,REMOTE_PROPALL_NEXT
		
REMOTE_PROX_PAUSE_TIMER:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10
	BS	SGN,REMOTE_PROX_PAUSE_TIMER_1
	
	CALL	INIT_DAM_FUNC
	CALL	BBEEP
	LACK	0
	SAH	PRO_VAR

REMOTE_PROX_PAUSE_TIMER_1:	
	RET

;-------------------------------------------------------------------------------
REMOTE_PROXTMR_PAUSE:		;时间暂停????????????????
	
	RET
;=======================================================================
;22222222222222222222222222222222222222222222222222222222222222222222222
REMOTE_PROPALL:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PROPALL_CPC
	
	LAC	PRO_VAR
	SFR	4
	ANDK	0XF
	BS	ACZ,REMOTE_PROPALL0		;播放
	SBHK	1
	BS	ACZ,REMOTE_PROX_PAUSE		;功能暂停
	SBHK	1
	BS	ACZ,REMOTE_PROXTMR_PAUSE	;时间暂停

	RET
;---------------
REMOTE_PROPALL_CPC:
	BS	B1,REMOTE_PROPNEW_CPC
;---------------
REMOTE_PROPALL0:	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PROPALL_OVER
REMOTE_PROPALL0_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROPALL_DTMF
REMOTE_PROPALL0_2:
	LAC	MSG
	XORL	CMSG_BTONE		;CMSG_BTONE
	BS	ACZ,REMOTE_PROEXIT_VPSTOP
REMOTE_PROPALL0_3:	
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,REMOTE_PROPALL0_TMR
	
	
	RET
;---------------处理消息
REMOTE_PROPALL0_TMR:
	BS	B1,REMOTE_PROPNEW0_TMR

REMOTE_PROPALL_OVER:	
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,REMOTE_PROX_PLYSTOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID

REMOTE_PROPALL_LOADVP:
	CALL	VP_Message
	LAC	MSG_ID
	CALL	ANNOUNCE_NUM

	CALL	MSG_WEEK
	CALL	MSG_HOUR
	CALL	MSG_MIN

	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	RET
	
REMOTE_PROPALL_DTMF:	
	LAC	DTMF_VAL
	ANDL	0X0F
	SBHK	3
	BS	ACZ,REMOTE_PROPALL_ERAPLYING	;3---erase playing message
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_PLYPRE	;4---skip to previous message
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_REPEAT	;5---repeat message
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_NEXT		;6---skip to next message
	SBHK	8
	BS	ACZ,REMOTE_PROX_DO_PAUSE	;*---do pause
	SBHK	1
	BS	ACZ,REMOTE_PROX_PLYSTOP		;#---end play VP	

	RET
;---------------
REMOTE_PROPALL_ERAPLYING:
	LAC	MSG_ID
	CALL	SET_DELMARK
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	
	RET

REMOTE_PROPALL_PLYPRE:		;前一个
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	1
	BS	ACZ,REMOTE_PROPALL_PLYPRE_DONE ;第一个吗?
	
	LAC	MSG_ID
	SBHK	1
	SAH	MSG_ID

REMOTE_PROPALL_PLYPRE_DONE:
	BS	B1,REMOTE_PROPALL_LOADVP
;-------
REMOTE_PROPALL_REPEAT:		;重复当前的一个
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPALL_LOADVP
;-------
REMOTE_PROPALL_NEXT:		;下一个
	CALL	INIT_DAM_FUNC
	BS	B1,REMOTE_PROPALL_OVER
;-------
REMOTE_PROX_PLYSTOP:
	CALL	INIT_DAM_FUNC
	CALL	VP_EndOfMessage
	CALL	BBEEP		;BB
	
	CALL	REAL_DEL
	CALL	GC_CHK
	CALL	VPMSG_CHK

	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1
	
	RET
;=======================================================================
;33333333333333333333333333333333333333333333333333333333333333333333333
REMOTE_PRODALL:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,REMOTE_PRODALL0	;play VOP
	SBHK	1
	BS	ACZ,REMOTE_PRODALL1	;wait for input command

	RET
;---------------------------------------
REMOTE_PRODALL0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRODALL0_VPSTOP
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRO0_CPC
	
	RET
;---	
REMOTE_PRODALL0_VPSTOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	LACK	0X0013
	SAH	PRO_VAR

	CALL	LINE_START

	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1

	RET
;---------------------------------------
REMOTE_PRODALL1:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRODALL1_TMR
;REMOTE_PRODALL1_1:
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,REMOTE_PRODALL1_DTMF
;REMOTE_PRODALL1_2:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PRODALL1_BTONE
;REMOTE_PRODALL1_3:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PRODALL1_CTONE
;REMOTE_PRODALL1_4:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRODALL1_CPC
	
	RET

;---------------
REMOTE_PRODALL1_CPC:
	BS	B1,REMOTE_PRO0_CPC


REMOTE_PROMREC1_DTMFTIMER:
	LAC	PRO_VAR2
	ADHK	1
	SAH	PRO_VAR2
	
	RET

REMOTE_PRODALL1_BTONE:
REMOTE_PRODALL1_CTONE:
	BS	B1,REMOTE_PRO0_TONE
REMOTE_PRODALL1_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1	
	SBHK	8		;8s
	BZ	SGN,REMOTE_PRODALL1_TMROVER
	
	RET
REMOTE_PRODALL1_TMROVER:
	BS	B1,REMOTE_PRODALL1_DTMF_COMMERR
;---------------------------------------
REMOTE_PRODALL1_DTMF:
	LAC	DTMF_VAL
	CALL	RMTFUNC_CHK
	SAH	SYSTMP1
	XORK	0X023
	BS	ACZ,REMOTE_PRODALL1_DTMF_ERASE	;次数到3,并且PASSWORD匹配成功
	ANDL	0X0F0
	BS	ACZ,REMOTE_PRODALL1_DTMF_COMMERR

	RET

;---------------------------------------
REMOTE_PRODALL1_DTMF_COMMERR:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LACK	0X005
	CALL	STOR_VP
	
	CALL	CLR_TIMER
	LACK	0
	SAH	PRO_VAR1
	SAH	PRO_VAR
	CALL	BCVOX_INIT

	RET
;---------------
REMOTE_PRODALL1_DTMF_ERASE:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	
	LACK	0
	SAH	PRO_VAR1
	
	CALL	BCVOX_INIT
;---	
	CALL	VPMSG_CHK
	CALL	VPMSG_DELALL
	CALL	GC_CHK

	LACK	0X003F		;延时
	CALL	STOR_VP
	CALL	VP_AllOldMessagesErased

	LACK	0X0
	SAH	PRO_VAR
	
	RET

;=======================================================================
;44444444444444444444444444444444444444444444444444444444444444444444444
REMOTE_PROMVOP:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PROMVOP_STOPVP	;VP完
;REMOTE_PROMVOP_1:	
	LAC	MSG
	XORL	CREV_DTMF			;CREV_DTMF
	BS	ACZ,REMOTE_PROMVOP_DTMF
;REMOTE_PROMVOP_2:
	LAC	MSG				;CPC/ONOFF
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PROMVOP_CPC
	
	RET
;---------------
REMOTE_PROMVOP_CPC:
	BS	B1,REMOTE_PRO0_CPC	;相当于待机状态

REMOTE_PROMVOP_DTMF:
	BS	B1,REMOTE_PRO0_REV_DTMF

REMOTE_PROMVOP_STOPVP:
	LAC	ADDR_S
        CALL	GetOneConst
        SAH	MSG_T
	
	LAC	MSG_ID	
	ADHK	1
	SAH	MSG_ID
	
	SBHK	1
	SBH	MSG_T
	BZ	SGN,REMOTE_PROMVOP_VPOVER
	
	CALL	INIT_DAM_FUNC
	LAC	ADDR_S
	ADH	MSG_ID
	CALL	GetOneConst
        ORL	0XFF00
        CALL	STOR_VP

	RET

REMOTE_PROMVOP_VPOVER:
	CALL	BBEEP

	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1
	
	
	RET
	
	RET
;=======================================================================
;55555555555555555555555555555555555555555555555555555555555555555555555
REMOTE_PROONFF:
	RET
;=======================================================================
;66666666666666666666666666666666666666666666666666666666666666666666666
;REMOTE_PRO6:
	RET
;=======================================================================
;77777777777777777777777777777777777777777777777777777777777777777777777
REMOTE_PROOGM:
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,REMOTE_PROOGM0	;play LBEEP
	SBHK	1
	BS	ACZ,REMOTE_PROOGM_REC	;record OGM
	SBHK	1
	BS	ACZ,REMOTE_PROOGM_PLY	;play OGM
	
	RET
;-------
REMOTE_PROOGM_REC:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PROOGM_REC_TMR
	LAC	MSG
	XORL	CMSG_TMR2		;TIMER2
	BS	ACZ,REMOTE_PROOGM_REC_TMR2
;REMOTE_PRO7_RECORD_1:
	LAC	MSG
	XORL	CREV_DTMFS		;DTMFS
	BS	ACZ,REMOTE_PROOGM_REC_DTMFS
	LAC	MSG
	XORL	CREV_DTMF		;DTMF
	BS	ACZ,REMOTE_PROOGM_REC_DTMF
;REMOTE_PRO7_RECORD_2:	
	LAC	MSG
	XORL	CMSG_BTONE
	BS	ACZ,REMOTE_PROOGM_REC_BTONE
;REMOTE_PRO7_RECORD_3:	
	LAC	MSG
	XORL	CMSG_CTONE
	BS	ACZ,REMOTE_PROOGM_REC_CTONE
;REMOTE_PRO7_RECORD_4:	
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,REMOTE_PROOGM_REC_STOP
;REMOTE_PRO7_RECORD_5:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PROOGM_REC_CPC

	RET
;---------------
REMOTE_PROOGM_REC_CPC:
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,REMOTE_PROOGM_REC_CPC1
	
	LAC	CONF
	ORL	1<<11
	SAH	CONF
	CALL	DAM_BIOSFUNC
REMOTE_PROOGM_REC_CPC1:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0X005
	CALL	STOR_VP

	CALL	VPMSG_CHK
	
	LACK	9
	SAH	PRO_VAR

	RET
;---------------------------------------
	
REMOTE_PROOGM_REC_CTONE:
REMOTE_PROOGM_REC_BTONE:
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK
	LAC	MSG_T
	CALL	GET_VPTLEN
	SBHK	3
	BZ	SGN,REMOTE_PRO7_RECORD_END
;---删除比3s短的录音
	LAC	MSG_T
	CALL	VPMSG_DEL
	CALL	GC_CHK
REMOTE_PRO7_RECORD_END:
	BS	B1,REMOTE_PRO0_TONE
;---------------------------------------
REMOTE_PROOGM_REC_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	60
	BZ	SGN,REMOTE_PROOGM_REC_STOP

	RET

REMOTE_PROOGM_REC_TMR2:
	LAC	PRO_VAR2
	ADHK	1
	SAH	PRO_VAR2
	
	RET
;-------
REMOTE_PROOGM_REC_DTMFS:
	CALL	BCVOX_INIT
	
	LAC	DTMF_VAL
	XORL	0XFF
	BS	ACZ,REMOTE_PROOGM_REC_7DTMFS	;pressed "#"

	RET
;---------------
REMOTE_PROOGM_REC_7DTMFS:
	LACK	0
	SAH	PRO_VAR2
	LACL	400
	CALL	SET_TIMER2

	RET

REMOTE_PROOGM_REC_DTMF:	
	LAC	DTMF_VAL
	XORL	0XFF
	BS	ACZ,REMOTE_PROOGM_REC_DTMF_STOP		;pressed "#"

	RET
;---------------
REMOTE_PROOGM_REC_DTMF_STOP:
	LAC	PRO_VAR1
	SBHK	2
	BZ	SGN,REMOTE_PROOGM_REC_DTMF_STOP1
	
	LAC	CONF
	ORL	1<<11
	CALL	DAM_BIOSFUNC	;录音太短,直接删除
REMOTE_PROOGM_REC_DTMF_STOP1:	
	LAC	CONF
	OR	PRO_VAR2
	ADHK	1
	CALL	DAM_BIOSFUNC
	
REMOTE_PROOGM_REC_STOP:		;处理"#","mfull"

	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LACK	0X0027
	SAH	PRO_VAR
	
	CALL	CLR_TIMER2
	
	CALL	OGM_SELECT
	
	CALL	BEEP
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	CALL	OGM_SELECT
	BZ	ACZ,MAIN_PRO0_PLAY_OGM1
	
	CALL	INIT_DAM_FUNC
	CALL	BEEP
	CALL	VP_DefOGM
	
	LACK	0X0
	SAH	PRO_VAR
MAIN_PRO0_PLAY_OGM1:

	RET	
;---------------
REMOTE_PROOGM_RECFULL:
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LACK	0X0027
	SAH	PRO_VAR
	
	CALL	CLR_TIMER2
	
	CALL	OGM_SELECT
	
	CALL	BEEP
	CALL	VP_MemoryFull
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	CALL	OGM_SELECT
	BZ	ACZ,REMOTE_PROOGM_RECFULL1
	
	CALL	INIT_DAM_FUNC
	CALL	BBBEEP
	CALL	VP_DefOGM
REMOTE_PROOGM_RECFULL1:
	
	RET
;-------	
REMOTE_PROOGM0:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,REMOTE_PRO7_PLAY_OVER
	
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRO0_CPC
	
	RET
REMOTE_PRO7_PLAY_OVER:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	LACK	0X0017
	SAH	PRO_VAR
;---
	CALL	OGM_SELECT
;-------delete the old OGM fisrt------------
	LAC	MSG_ID
	CALL	VPMSG_DEL
	CALL	GC_CHK
;---set user index data0"OGM_ID"------------
    	LAC	MSG_N
    	ORL	0X8D00
    	CALL	DAM_BIOSFUNC
;---	
	CALL	REC_START
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	RET
;---------------------------------------
REMOTE_PROOGM_PLY:
	LAC	MSG
	XORL	CVP_STOP		;CVP_STOP
	BS	ACZ,REMOTE_PROOGM_PLY_STOP
;REMOTE_PRO7_2_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PROOGM_PLY_DTMF
;REMOTE_PRO7_2_2:	
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRO0_CPC
	
	RET
REMOTE_PROOGM_PLY_STOP:
	CALL	INIT_DAM_FUNC	
	CALL	BBEEP
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1

	RET

REMOTE_PROOGM_PLY_DTMF:
	LAC	DTMF_VAL
	XORL	0X0FF
	BS	ACZ,REMOTE_PROOGM_PLY_STOP	;#

	RET

REMOTE_PROOGM_PLY_DTMF_3:
	CALL	INIT_DAM_FUNC
	CALL	OGM_SELECT
;---delete the old OGM fisrt------------
	LAC	MSG_ID
	CALL	VPMSG_DEL
	CALL	GC_CHK

	CALL	BBEEP		;BB
	LACK	0
	SAH	PRO_VAR
	
	RET
;=======================================================================
;88888888888888888888888888888888888888888888888888888888888888888888888
REMOTE_PROROMMONITOR:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,REMOTE_PRO0_CPC
	
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,REMOTE_PRO8_VOPLAY
	SBHK	1
	BS	ACZ,REMOTE_PROROMMONITING
	
	RET
;---------------------------------------
REMOTE_PROROMMONITING:

;REMOTE_PRORMONIT_1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,REMOTE_PRORMONIT_DTMF
;REMOTE_PRORMONIT_2:
	LAC	MSG
	XORL	CMSG_TMR		;TIMER
	BS	ACZ,REMOTE_PRORMONIT_TIMER
;REMOTE_PRORMONIT_3:	

	RET
;---------------处理消息
REMOTE_PRORMONIT_DTMF:
	LAC	DTMF_VAL
	ANDK	0X0F
	SBHK	0X0E
	BS	ACZ,REMOTE_PRORMONIT_RESTART	;"*"
	SBHK	1
	BS	ACZ,REMOTE_PRORMONIT_STOP	;"#"

	RET

REMOTE_PRORMONIT_RESTART:
	LACK	0
	SAH	PRO_VAR1

	RET

REMOTE_PRORMONIT_STOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP
	CALL	BCVOX_INIT
	
	LACK	0
	SAH	PRO_VAR
	SAH	PRO_VAR1

	RET

REMOTE_PRORMONIT_TIMER:
	LACL	CTMR_CTONE	;---不让产生VOX,CTONE消息,可产生BTONE
	SAH	TMR_VOX
	SAH	TMR_CTONE
	
	LAC	PRO_VAR1	;计时
	ADHK	1
	SAH	PRO_VAR1
	SBHK	30		;30s计时
	BZ	SGN,REMOTE_PRORMONIT_STOP
		
	RET
;-------
REMOTE_PRO8_VOPLAY:
	LAC	MSG
	XORL	CVP_STOP			;CVP_STOP
	BS	ACZ,REMOTE_PRO8_VOPLAY_END
	
	RET	
REMOTE_PRO8_VOPLAY_END:
	CALL	INIT_DAM_FUNC
	CALL	DAA_ROMMOR_ON
	CALL	LINE_START
	
	LACK	0X0018
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1


	RET	
;EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE
REMOTE_PROEXIT:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,REMOTE_PROEXIT_VPSTOP
	
	RET

REMOTE_PROEXIT_VPSTOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF	
	CALL	HOOK_OFF

	CALL	CLR_FUNC	;先空
	LACL	LOCAL_PRO	;进入本地操作
     	CALL	PUSH_FUNC	
	
	LACL	CMODE9
	CALL	DAM_BIOSFUNC

	LACK	0
	SAH	PRO_VAR

	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
;-------------------------------------------------------------------------------
;===============================================================================
VP_MENU_QUEUE:	;for English
;
.DATA	30	;length	
;---
	;press	1	t0	play	new	messages
.DATA	59	1	66	67	117	61
;---
	;press_two_t0_play_all_messages
.DATA	215
;---
	;press_30_to_erase_all_messages
.DATA	216
;---
	;press	7	t0	record	outgoingmessage
.DATA	59	7	66	93	198
;---
	;press	8	t0	ActivateRoom	monitor
.DATA	59	8	66	83	84	85
;---
	;press	9	t0	turn_on_or_off
.DATA	59	9	66	134
;---
	;press	#	t0	release_line
.DATA	59	87	66	89
;---
	;press	*	for_menu
.DATA	59	86	88
;---

;-------------------------------------------------------------------------------
.END
	