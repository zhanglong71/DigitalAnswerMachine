.LIST
ICM_STATE:
	LAC	MSG
	XORL	CMSG_KEY6S		;接线后摘机(相当于CPC)
	BS	ACZ,ICM_CPC_RUN
	
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_CPC
	BS	ACZ,ICM_CPC_RUN
	
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,ICM_STATE1		;for initial
	SBHK	1
	BS	ACZ,ICM_STATE2		;for record(ANSWER AND RECORD ICM)
	SBHK	1
	BS	ACZ,ICM_STATE_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)
	SBHK	1
	BS	ACZ,LINE_STATE1		;for line(ANSWER ONLY)

	RET
;-------
ICM_CPC_RUN:
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_CPC_RUN_3
	LAC	CONF
	ANDL	0XFFC0
	SAH	CONF
	
	LAC	PRO_VAR1
	SBHK	3
	BZ	SGN,ICM_CPC_RUN_1
	SRAM	CONF,11				;少于3秒,录音删除
	CALL	DAM_BIOS
ICM_CPC_RUN_1:
	CALL	INIT_DAM_FUNC
	
	LACL	0XE605
	SAH	CONF
	CALL	DAM_BIOS
	CALL	GET_TELT
	BZ	ACZ,ICM_CPC_RUN_2		;当无来电时才写入相应的信息
	
	CALL	GET_TOTALMSG
	CALL	GET_MSGMSG
	CALL	WRITE_ICMMSG
ICM_CPC_RUN_2:
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT
	CALL	COL_ICMTEL
ICM_CPC_RUN_3:
	CALL	INIT_DAM_FUNC
	CALL	TMPNUM_DEL
ICM_CPC_RUN_END:	
	BS	B1,ICM_STATE_EXIT_END
	
;=============================================================
ICM_STATE1:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,ICM_STATE_INIT
ICM_STATE1_1:	
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_VP_STOP		;CVP_STOP,OGM播放完毕
ICM_STATE1_2:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ICM_REV_DTMF
	RET

;---
ICM_STATE_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD
	CALL	HOOK_ON
	
	CALL	SET_OGMFG	;综合当前情况设定OGM
	;CALL	OGM_STATUS
	CALL	OGM_CHK
	BS	ACZ,ICM_STATE_INIT1
				;判断有无OGM1/OGM2录音
	CALL	SELECT_VP
;---	
	LACK	0X07D
	CALL	STOR_VP		;延时1 second
	
	LAC	LANGUAGE
	SFR	4		;default OGM vp
	ANDK	0X0F
	ORL	0XFF00
	CALL	STOR_VP
	BS	B1,ICM_STATE_INIT2
ICM_STATE_INIT1:
	LACK	0X07D
	CALL	STOR_VP		;延时1 second
	
	LAC	MSG_ID		;record OGM
	ANDL	0X0FF
	ORL	0XFE00
	CALL	STOR_VP
ICM_STATE_INIT2:		
	CALL	LBEEP
	
	RET
;---
ICM_VP_STOP:			;开始录音(ICM)/ON_LINE(only)
	CALL	INIT_DAM_FUNC
	CALL	DAA_ICM_RECORD
	CALL	BCVOX_INIT

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X010		;OGM1/record
	SAH	PRO_VAR

	CALL    MSG_CHK
	;CALL	GET_FILEID
	
	LAC	FILE_ID
	CALL	SET_USRDAT
	
	CALL	REC_START

	;CALL	MSG_CHK
	BIT	ANN_FG,3	;memory full?
	BZ	TB,ICM_VP_STOP1
	
	CALL	INIT_DAM_FUNC
	CALL	LINE_START
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X030		;OGM2/line
	SAH	PRO_VAR
ICM_VP_STOP1:	
	;BIT	EVENT,8		;判断OGM1/OGM2---record/line
	;BZ	TB,ICM_VP_STOP_END
	
	;CALL	LINE_START
			
ICM_VP_STOP_END:
		
	RET
;---------------for DTMF
ICM_REV_DTMF:			;比较密码
	CALL	BCVOX_INIT	;有键按下BCVOX要清零
	
	LAC	PRO_VAR
	ANDL	0X0F
	BS	ACZ,ICM_REV_DTMF0
	SBHK	1
	BS	ACZ,ICM_REV_DTMF1
	SBHK	1
	BS	ACZ,ICM_REV_DTMF2
	
	LAC	PASSWORD	;第四位(bit3..0)
	ANDK	0X0F
	ORL	0XF0
	SBH	DTMF_VAL
	BS	ACZ,ICM_REV_DTMF_YES
	BS	B1,ICM_REV_DTMF_ERR
ICM_REV_DTMF0:			;第一位(bit15..12)
	LAC	PASSWORD
	SFR	12
	ORL	0XF0
	SBH	DTMF_VAL
	BS	ACZ,ICM_REV_DTMF_OK
	BS	B1,ICM_REV_DTMF_ERR
ICM_REV_DTMF1:			;第二位(bit11..8)
	LAC	PASSWORD
	SFR	8
	ANDK	0X0F
	ORL	0XF0
	SBH	DTMF_VAL
	BS	ACZ,ICM_REV_DTMF_OK
	BS	B1,ICM_REV_DTMF_ERR
ICM_REV_DTMF2:			;第三位(bit7..4)
	LAC	PASSWORD
	SFR	4
	ANDK	0X0F
	ORL	0XF0
	SBH	DTMF_VAL
	BS	ACZ,ICM_REV_DTMF_OK
	BS	B1,ICM_REV_DTMF_ERR
	
ICM_REV_DTMF_ERR:
	LAC	PRO_VAR
	ANDL	0XFFF0
	SAH	PRO_VAR
	RET
ICM_REV_DTMF_OK:
	LAC	PRO_VAR
	ADHK	1
	SAH	PRO_VAR
	RET
ICM_REV_DTMF_YES:		;密码成功
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_REV_DTMF_YES1
	
	SRAM	CONF,11
	CALL	DAM_BIOS	;若是录音过程,则删除之
ICM_REV_DTMF_YES1:	
	CALL	INIT_DAM_FUNC
	
	CALL	CLR_FUNC	;先空
	LACL	REMOTE_PRO	;进入遥控
	CALL	PUSH_FUNC
	
	CALL	TMPNUM_DEL
	
	CALL	DAA_ICM_RECORD
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR		;程序步骤
	SAH	PRO_VAR1	;计时清零
	
	CALL	BCVOX_INIT
	
	RET

;=============================================================
ICM_STATE2:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ICM_REV_DTMF
ICM_STATE2_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,ICM_TMR_DET
ICM_STATE2_2:
	LAC	MSG
	XORL	CMSG_VOX		;VOX_ON 8s
	BS	ACZ,ICM_VOX_DET
ICM_STATE2_3:
	LAC	MSG
	XORL	CMSG_CTONE		;CTONE 8s
	BS	ACZ,ICM_CTONE_DET
ICM_STATE2_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 8s
	BS	ACZ,ICM_BTONE_DET
ICM_STATE2_5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,ICM_REC_FUL
ICM_STATE2_6:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,ICM_FUL_EVP
	
	RET
;-------
ICM_VOX_DET:				;由于后BTONE,CTONE,VOX要持续一段时间,可不考虑小长度的录音删除问题
ICM_CTONE_DET:
ICM_BTONE_DET:
	;CALL	INIT_DAM_FUNC
	;CALL	DAA_LIN_RECORD
;---	
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_BCVOX_DET
	
	CALL	INIT_DAM_FUNC
	
	LACL	0XE605
	SAH	CONF
	CALL	DAM_BIOS
	CALL	GET_TELT
	BZ	ACZ,ICM_BCVOX_1		;当无来电时才写入相应的信息
	
	CALL	GET_TOTALMSG
	CALL	GET_MSGMSG
	CALL	WRITE_ICMMSG
ICM_BCVOX_1:
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT
	CALL	COL_ICMTEL
ICM_BCVOX_DET:	
	CALL	INIT_DAM_FUNC
	CALL	TMPNUM_DEL
;---
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR
	
	CALL	DAA_LIN_RECORD	
	CALL	BBEEP		;替换语音BB
	
	RET
;---
ICM_TMR_DET:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	120
	BZ	ACZ,ICM_TMR_DET_END	
	BS	SGN,ICM_TMR_DET_END
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

	CALL	BBEEP		;替换语音BB
	
ICM_TMR_DET_END:
	RET
;-------
ICM_REC_FUL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD

	LACL	0X025
	CALL	STOR_VP	
	CALL	BBBEEP		;警告语音BBB
	
	RET
ICM_FUL_EVP:
	CALL	DAA_ICM_RECORD
	CALL	INIT_DAM_FUNC
	CALL	LINE_START
	
	RET
;=================================================================
ICM_STATE_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_STATE_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET
ICM_STATE_EXIT_END:
	CALL	INIT_DAM_FUNC
	CALL	HOOK_OFF
	CALL	CLR_FUNC	;先空
	
	CALL	TMPNUM_DEL
	
	LACK	0
	SAH	PRO_VAR

	LACL	0X0018
	CALL	STOR_VP		;延时192ms
	
	LAC	RING_ID
	SFR	12
	SAH	RING_ID
	
	RET
;========================================================================
LINE_STATE1:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ICM_REV_DTMF
LINE_STATE1_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,LINE_TMR_DET
LINE_STATE1_2:
	;LAC	MSG
	;XORL	CMSG_VOX		;VOX_ON 8s
	;BS	ACZ,ICM_VOX_DET
LINE_STATE1_3:
	;LAC	MSG
	;XORL	CMSG_CTONE		;CTONE 8s
	;BS	ACZ,ICM_CTONE_DET
LINE_STATE1_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 8s
	BS	ACZ,ICM_BTONE_DET

	RET
;-------
LINE_TMR_DET:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	10
	;BZ	ACZ,LINE_TMR_DET_END	
	BS	SGN,LINE_TMR_DET_END
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR
	
	CALL	BBEEP		;替换语音BB
LINE_TMR_DET_END:
	RET
	
;//----------------------------------------------------------------------
DTMF_TABLE:

	;no	1	2	3	4	5	6	7	8	
.DATA	0X00	0XF1	0XF2	0XF3	0XF4	0XF5	0XF6	0XF7	0XF8

	;9	*	0	#	A	B	C	D
.DATA	0XF9	0XFE	0XF0	0XFF	0XFA	0XFB	0XFC	0XFD

.END
