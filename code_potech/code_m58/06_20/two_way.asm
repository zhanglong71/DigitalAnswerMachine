.LIST
TWAY_STATE:
	LAC	MSG
	XORL	CMSG_CPC
	BS	ACZ,TWAY_CPC_RUN	;
	
	LAC	PRO_VAR
	SFR	4
	BS	ACZ,TWAY_STATE1		;for initial
	SBHK	1
	BS	ACZ,TWAY_STATE2		;for record/line
	SBHK	1
	BS	ACZ,TWAY_STATE_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)

	RET

TWAY_CPC_RUN:
	BS	B1,TWAY_STATE_EXIT_END
	
;=============================================================
TWAY_STATE1:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,TWAY_STATE_INIT
TWAY_STATE1_1:	
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,TWAY_VP_STOP	;CVP_STOP,OGM播放完毕
TWAY_STATE1_2:
	;LAC	MSG
	;XORL	CREV_DTMF		;CREV_DTMF
	;BS	ACZ,TWAY_REV_DTMF
TWAY_STATE1_3:
	LAC	MSG
	XORL	CMSG_KEY6S		;STOP
	BS	ACZ,TWAY_STOP_DET
	
	RET

;---
TWAY_STATE_INIT:
	CALL	INIT_DAM_FUNC
	CALL	MSG_CHK
	BIT	ANN_FG,13
	BS	TB,TWAY_STATE_INIT_EXIT
TWAY_STATE_INIT_1:
	CALL	DAA_LINE_SPK
	;CALL	DAA_LIN_RECORD
	
	CALL	LBEEP
TWAY_STATE_INIT_END:
	RET
TWAY_STATE_INIT_EXIT:		;满了不准再录,退出
	BS	B1,TWAY_STATE_EXIT_END	
;---
TWAY_VP_STOP:			;开始录音(ICM)/ON_LINE(only)	
	CALL	INIT_DAM_FUNC
	CALL	DAA_TWAY_RECORD
	CALL	BCVOX_INIT
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X010
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	;LACL	0XD000
	;SAH	CONF
	;CALL	DAM_BIOS
	
	;LACL	0XD200
	;SAH	CONF
	;CALL	DAM_BIOS
	
	CALL	MSG_CHK

	;CALL	GET_FILEID
	LAC	FILE_ID
	CALL	SET_USRDAT
	
	CALL	REC_START
			;;;;判断OGM1/OGM2---record/line
TWAY_VP_STOP_END:		
	RET
;---------------for DTMF
TWAY_REV_DTMF:			;不理会DTMF
	
	RET

;=============================================================
TWAY_STATE2:
	;LAC	MSG
	;XORL	CREV_DTMF		;CREV_DTMF
	;BS	ACZ,TWAY_REV_DTMF
TWAY_STATE3:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,TWAY_TMR_DET
TWAY_STATE4:
	LAC	MSG
	XORL	CMSG_KEY6S		;STOP
	BS	ACZ,TWAY_STOP_DET
TWAY_STATE5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,TWAY_REC_FUL
TWAY_STATE6:	
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,TWAY_REC_EVP
	
	RET
;---
TWAY_STOP_DET:
	LAC	PRO_VAR1
	SBHK	3
	BZ	SGN,TWAY_STOP_DET1
	SRAM	CONF,11				;少于3秒,录音删除
	CALL	DAM_BIOS
	CALL	INIT_DAM_FUNC
	BS	B1,TWAY_BCVOX_1
TWAY_STOP_DET1:
	LAC	CONF
	ANDL	0X1000		;不掐尾
	SAH	CONF
	CALL	INIT_DAM_FUNC
;---对应来电	
	LACL	0XE605
	SAH	CONF
	CALL	DAM_BIOS
	CALL	GET_TELT
	BZ	ACZ,TWAY_BCVOX_1
	
	CALL	GET_TOTALMSG
	CALL	GET_MSGMSG
	CALL	WRITE_ICMMSG
TWAY_BCVOX_1:	
	CALL	GET_TOTALMSG
	CALL	GET_USRDAT
	CALL	COL_ICMTEL

	CALL	TMPNUM_DEL

	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

	LACL	0X005
	CALL	STOR_VP
	
	RET
;---
TWAY_TMR_DET:
	LAC	CONF
	ANDL	0XFFC0
	SAH	CONF
	
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	
TWAY_TMR_DET_END:
	RET
;---
TWAY_REC_FUL:
	LAC	CONF
	ANDL	0XF000
	SAH	CONF
	CALL	INIT_DAM_FUNC
	
	CALL	GC_CHK
	CALL	MSG_CHK
	BIT	ANN_FG,13
	BS	TB,TWAY_MFULL_EXIT
	
	CALL	REC_START

	RET
TWAY_MFULL_EXIT:		;MFULL退出
	CALL	INIT_DAM_FUNC
	CALL	DAA_LINE_SPK
	CALL	HOOK_ON
	
	CALL	BBBEEP		;警告语音BBB
	
	LAC	PRO_VAR
	ANDL	0XFF0F
	ADHK	0X020
	SAH	PRO_VAR

	RET
;---
TWAY_REC_EVP:
	CALL	INIT_DAM_FUNC
	
	RET
;=================================================================
TWAY_STATE_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,TWAY_STATE_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET
TWAY_STATE_EXIT_END:
	CALL	INIT_DAM_FUNC
	CALL	HOOK_OFF
	CALL	CLR_FUNC	;先空
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	LAC	RING_ID
	SFR	12
	SAH	RING_ID
	
	RET
;//----------------------------------------------------------------------

.END
