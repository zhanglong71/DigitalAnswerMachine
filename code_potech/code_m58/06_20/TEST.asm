.LIST
TEST_STATE:
	LAC	PRO_VAR
	ANDK	0X0F
	BS	ACZ,TEST_PRO0	;idel
	SBHK	1
	BS	ACZ,TEST_PRO1	;idel
	SBHK	1
	BS	ACZ,TEST_PRO2	;ICM record
	SBHK	1
	BS	ACZ,TEST_PRO3	;remote play
	SBHK	1
	BS	ACZ,TEST_PRO4	;mome record
	SBHK	1
	BS	ACZ,TEST_PRO5	;local	play
	SBHK	1
	BS	ACZ,TEST_PRO6	;vox detect
	SBHK	1
	BS	ACZ,TEST_PRO7	;cpc detect

	RET
;======================================================消息生成区
TEST_PRO0:	;进入TEST状态要考虑的消息
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO0_1
	
	RET
TEST_PRO0_1:
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACL	1		;new
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	1
	SAH	PRO_VAR
	
	RET
;-----------------------------------------------------------
TEST_PRO1:
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO1_0_0
TEST_PRO1_0:		;空闲状态要考虑的消息:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO1_1_0
	
	RET
	
TEST_PRO1_0_0:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20		;beep(initial)
	CALL	STOR_VP
	;LACL	2		;2
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	2
	SAH	PRO_VAR	
	
	RET
	
	
TEST_PRO1_1_0:

	RET
;-----------------------------------------------------------
TEST_PRO2:	;TEST ICM RECORD状态要考虑的消息
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO2_1
TEST_PRO2_0_0:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,TEST_PRO2_4
TEST_PRO2_0_1:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO2_2
		
	RET
TEST_PRO2_1:
	LAC	CONF
	ANDL	0XF000
	SAH	CONF
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACK	3		;3
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	3
	SAH	PRO_VAR	
	
	RET
	
TEST_PRO2_2:		;TEST ICM RECORD状态
	CALL	INIT_DAM_FUNC
	CALL	DAA_ICM_RECORD
	CALL	HOOK_ON
	
	CALL	MSG_CHK
	CALL	GET_FILEID
	CALL	SET_USRDAT

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	CALL	REC_START
	
	RET

TEST_PRO2_4:
	CALL	INIT_DAM_FUNC
	
	RET
;-----------------------------------------------------------
TEST_PRO3:	;TEST REMOTE PLAY状态要考虑的消息
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,TEST_PRO3_0_3
	SBHK	1
	BS	ACZ,TEST_PRO3_0_1
	
	RET
;---
TEST_PRO3_0_1:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO3_4
TEST_PRO3_0_2:
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO3_2
	
	RET
TEST_PRO3_0_3:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO3_3

	
	RET
;---

TEST_PRO3_2:	;进入TEST MEMO RECORD
	CALL	HOOK_OFF
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACL	4		;4
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	4
	SAH	PRO_VAR	
	
	
	RET
TEST_PRO3_3:		;正式进入TEST REMOTE PLAY	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	LACK	0X013
	SAH	PRO_VAR

	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD
	CALL	HOOK_ON

	CALL	MSG_CHK
	LAC	MSG_T
	ADHK	1
	SAH	MSG_ID
	
	BIT	ANN_FG,14
	BS	TB,TEST_PRO3_3_1
	
	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
TEST_PRO3_3_1:
	LACL	0X005
	CALL	STOR_VP

	RET
	
TEST_PRO3_4:		;进入TEST REMOTE PLAY(next)状态
	LAC	MSG_ID
	ANDL	0X0FF
	SBHK	1
	BS	ACZ,TEST_PRO3_4_1
	BS	SGN,TEST_PRO3_4_1
	
	LAC	MSG_ID			;next message
	SBHK	1
	SAH	MSG_ID

	LACL	0X025
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP

	LAC	MSG_ID
	CALL	GET_VPLEN
	CALL	GET_VPMSG

	RET
TEST_PRO3_4_1:
	CALL	INIT_DAM_FUNC
	CALL	SETOLD_VPMSG
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
	
	LACK	0X023
	SAH	PRO_VAR
	
	RET
;-----------------------------------------------------------
TEST_PRO4:	;TEST RECORD MEMO状态要考虑的消息
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO4_2
TEST_PRO4_0:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,TEST_PRO4_0_0
TEST_PRO4_0_1:	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO4_3
	
	RET

TEST_PRO4_3:
	CALL	INIT_DAM_FUNC
	CALL	DAA_RECORD
	CALL	HOOK_OFF

	CALL	MSG_CHK
	CALL	GET_FILEID
	CALL	SET_USRDAT

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	CALL	REC_START
	
	RET

TEST_PRO4_2:
	LAC	CONF
	ANDL	0XF000
	SAH	CONF
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACL	5		;5
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	5
	SAH	PRO_VAR	

	RET

TEST_PRO4_0_0:
	CALL	INIT_DAM_FUNC
	
	CALL	MSG_CHK
	BIT	ANN_FG,13
	BS	TB,TEST_PRO4_0_0_1
	
	CALL	REC_START
TEST_PRO4_0_0_1:
		
	RET
;-----------------------------------------------------------
TEST_PRO5:	;TEST LOCAL PLAY状态要考虑的消息
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,TEST_PRO5_0_3
	SBHK	1
	BS	ACZ,TEST_PRO5_0_0
	
	RET
	
TEST_PRO5_0_0:	
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO5_3
TEST_PRO5_0_1:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO5_2
		
	RET

TEST_PRO5_0_3:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO5_1_1
	
	RET
TEST_PRO5_1_1:			;正式开始
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	
	LACK	0X015
	SAH	PRO_VAR

	CALL	MSG_CHK
	LAC	MSG_T
	ADHK	1
	SAH	MSG_ID
	
	BIT	ANN_FG,14
	BS	TB,TEST_PRO5_1_2
	
	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
TEST_PRO5_1_2:
	LACL	0X005
	CALL	STOR_VP

	RET
TEST_PRO5_3:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACL	6		;6
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	6
	SAH	PRO_VAR	
	
	RET

TEST_PRO5_2:	
	LAC	MSG_ID
	ANDL	0X0FF
	SBHK	1
	BS	ACZ,TEST_PRO5_2_1
	BS	SGN,TEST_PRO5_2_1
	
	LAC	MSG_ID			;next message
	SBHK	1
	SAH	MSG_ID

	LACL	0X025
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP

	LAC	MSG_ID
	CALL	GET_VPLEN
	CALL	GET_VPMSG

	RET
TEST_PRO5_2_1:
	CALL	INIT_DAM_FUNC
	CALL	SETOLD_VPMSG
	
	LACK	0X025
	SAH	PRO_VAR
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS

	LACL	0X3010
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3010
	CALL	STOR_VP
	
	RET
;-----------------------------------------------------------
TEST_PRO6:	;TEST CHECK VOX状态要考虑的消息
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO6_1
TEST_PRO6_0:	
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO6_2
	
	RET
TEST_PRO6_1:	;进入TEST 并机
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3C20			;beep(initial)
	CALL	STOR_VP
	;LACK	7		;7
	;ORL	0XFF00
	;CALL	STOR_VP
	
	LACK	7
	SAH	PRO_VAR	


	RET
TEST_PRO6_2:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_RECORD
	CALL	HOOK_ON
TEST_PRO6_2_0:
	CALL	LINE_START
	CALL	DAM_BIOS
	
	BIT     RESP,6
        BZ      TB,TEST_PRO6_2_1	;VOX LEVEL
        BIT	EVENT,13
	BS	TB,TEST_PRO6_2_0	;TEST KEY pressed?
	
	;LACK   10
        ;SAH	TMR_VOX
	;CALL	INIT_DAM_FUNC

	RET
TEST_PRO6_2_1:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACL	0X3020
	CALL	STOR_VP
	
	RET

;-----------------------------------------------------------
TEST_PRO7:	;TEST CHECK 并机要考虑的消息
	LAC	MSG
	XORL	CMSG_CPC
	BS	ACZ,TEST_PRO7_1	
TEST_PRO7_0_0:
	LAC	MSG			;接线后摘机(相当于CPC)
	XORL	CMSG_KEY6S
	BS	ACZ,TEST_PRO7_1
TEST_PRO7_0_1:	
	LAC	MSG
	XORL	CMSG_TEST
	BS	ACZ,TEST_PRO7_2
TEST_PRO7_0_2:
	LAC	MSG
	XORL	CVP_STOP
	BS	ACZ,TEST_PRO7_3
	
	RET

TEST_PRO7_3:
	CALL	HOOK_ON
	CALL	INIT_DAM_FUNC

	RET
	
TEST_PRO7_1:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK

	LACL	0X3020
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3020
	CALL	STOR_VP	
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3020
	CALL	STOR_VP
	LACL	0X005
	CALL	STOR_VP
	LACL	0X3020
	CALL	STOR_VP		;BBBB
	
	RET
	
TEST_PRO7_2:
	CALL	DAA_SPK
	LACL	0X1620			;beep(initial)
	CALL	STOR_VP
	LACL	0X2020			;beep(initial)
	CALL	STOR_VP
	LACL	0X3020			;beep(initial)
	CALL	STOR_VP
	LACL	0X3920			;beep(initial)
	CALL	STOR_VP
	
	LAC	PRO_VAR
	ADHK	1
	SAH	PRO_VAR	
	
	RET
;-----------------------------------------------------------	
	

.END
