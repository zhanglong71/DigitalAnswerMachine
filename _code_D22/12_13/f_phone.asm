.LIST
;-------------------------------------------------------------------------------
LOCAL_PROPHO:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PROPHO0	;wait from start
	SBHK	1
	BS	ACZ,LOCAL_PROPHO1	;Speakerphone
	SBHK	1
	BS	ACZ,LOCAL_PROPHO2	;pressed KEY(Del)
	SBHK	1
	BS	ACZ,LOCAL_PROPHO3	;released KEY(End Del)
	SBHK	1
	BS	ACZ,LOCAL_PROPHO4	;Flash
	SBHK	1
	BS	ACZ,LOCAL_PROPHO5	;Pause
	SBHK	1
	BS	ACZ,LOCAL_PROPHO6	;Redel
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO0:
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,LOCAL_PROPHO0_TMR
	
	RET
;---------------
LOCAL_PROPHO0_TMR:
	
	LACL	1000
	CALL	SET_TIMER
	LACK	0X015
	SAH	PRO_VAR
.if	0
	LACL	0x5F20		;set SPK volume
	CALL	DAM_BIOSFUNC
	LACK	SPK_GAIN	;+6dB
	CALL	DAM_BIOSFUNC
.else
	LIPK    6

	IN	CODECREG2,LOUTSPK
	LAC	CODECREG2
	ANDL	0XFFE0
	ORK	SPK_GAIN
	SAH	CODECREG2	
	OUT	CODECREG2,LOUTSPK
	ADHK	0
.endif	
	CALL	PHONE_START

	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO1:	
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPHO_TMR
LOCAL_PROPHO1_0_1:			;按下数字拔号键
	LAC	MSG
	XORL	CMSG_KEYDD
	BS	ACZ,LOCAL_PROPHO0_DIAL_1
	LAC	MSG
	XORL	CMSG_KEY9D
	BS	ACZ,LOCAL_PROPHO0_DIAL_2
	LAC	MSG
	XORL	CMSG_KEY5D
	BS	ACZ,LOCAL_PROPHO0_DIAL_3
	LAC	MSG
	XORL	CMSG_KEYED
	BS	ACZ,LOCAL_PROPHO0_DIAL_4
	LAC	MSG
	XORL	CMSG_KEYAD
	BS	ACZ,LOCAL_PROPHO0_DIAL_5
	LAC	MSG
	XORL	CMSG_KEY6D
	BS	ACZ,LOCAL_PROPHO0_DIAL_6
	LAC	MSG
	XORL	CMSG_KEYFD
	BS	ACZ,LOCAL_PROPHO0_DIAL_7
	LAC	MSG
	XORL	CMSG_KEYBD
	BS	ACZ,LOCAL_PROPHO0_DIAL_8
	LAC	MSG
	XORL	CMSG_KEY7D
	BS	ACZ,LOCAL_PROPHO0_DIAL_9
	LAC	MSG
	XORL	CMSG_KEYGD
	BS	ACZ,LOCAL_PROPHO0_DIAL_ASTERISK
	LAC	MSG
	XORL	CMSG_KEYCD
	BS	ACZ,LOCAL_PROPHO0_DIAL_0
	LAC	MSG
	XORL	CMSG_KEY8D
	BS	ACZ,LOCAL_PROPHO0_DIAL_POUND
LOCAL_PROPHO1_0_2:			;音量
	LAC	MSG
	XORL	CMSG_KEY1S
	BS	ACZ,LOCAL_PROPHO1_FLASH
	
	LAC	MSG
	XORL	CMSG_KEY1L
	BS	ACZ,LOCAL_PROPHO1_REDIAL	;重拔
	
	LAC	MSG
	XORL	CMSG_KEY2S
	BS	ACZ,LOCAL_PROPHO0_VOLADD

	LAC	MSG
	XORL	CMSG_KEY3S
	BS	ACZ,LOCAL_PROPHO0_VOLSUB	;音量
LOCAL_PROPHO1_0_3:			;松开Speakerphone键
	LAC	MSG
	XORL	CMSG_KEY4S
	BS	ACZ,LOCAL_PROPHO_STOP
LOCAL_PROPHO1_0_4:	
	LAC	MSG
	SBHL	CMSG_KEY5S
	BS	SGN,LOCAL_PROPHO1_NOFUNC
	LAC	MSG
	SBHL	CMSG_KEYGS
	SBHK	1
	BZ	SGN,LOCAL_PROPHO1_NOFUNC
	
	;BS	B1,LOCAL_PROPHO0_DIALEND
;LOCAL_PROPHO0_DIALEND:		;松开数字拔号键
	LACK	0X035
	SAH	PRO_VAR

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER

	LACL	0XC200
	CALL	DAM_BIOSFUNC

	LACL	0x5F10		;set mic-pre-gain
	CALL	DAM_BIOSFUNC
	LACK	MIC_GAIN	; --dB
	CALL	DAM_BIOSFUNC
	
	LACL	0XC000
	CALL	DAM_BIOSFUNC

	RET

LOCAL_PROPHO1_NOFUNC:

	RET

;-------Exit SpeakerPhone mode
LOCAL_PROPHO_STOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	CALL	HOOK_OFF
;---以0XFFF结束,方便写入Flash(确保最后两个ff以一个字节的形式出现)
	LAC	COUNT
	ADHK	1
	SAH	COUNT
	LACK	0X0F
	CALL	STORHALFBYTE_DAT
	LAC	COUNT
	ADHK	1
	SAH	COUNT
	LACK	0X0F
	CALL	STORHALFBYTE_DAT
	LAC	COUNT
	ADHK	1
	SAH	COUNT
	LACK	0X0F
	CALL	STORHALFBYTE_DAT
;---将长度保存在偏移0处
	LAC	COUNT	;其中保存了最后一次存放数字的偏移地址.(0,1处是号码长度占1Byte,最后3个是fff)
	SBHK	5
	SAH	DTMF_VAL

	LACK	0
	SAH	COUNT
	LAC	DTMF_VAL
	CALL	STORBYTE_DAT	;长度值占用1Byte
;---写入Flash
	LAC	MSG_T
	BZ	ACZ,LOCAL_PROPHO_STOP1	;若没有数字(1,2,3,4,5,6,7,8,9,*,0,#键按下清零)按下过,就不用更新回拔号码
	
	LACK	6
	CALL	SET_TELGROUP
	CALL	DEL_ALLTEL	;先删除
	CALL	TELNUM_WRITE	;再写入
	CALL	DAT_WRITE_STOP
LOCAL_PROPHO_STOP1:
;-
	LACL	CMODE9
	CALL	DAM_BIOSFUNC	;---开ALC

   	LACK	0
   	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	RET
;-------
LOCAL_PROPHO0_DIAL_1:
	LACK	1
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_2:
	LACK	2
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_3:
	LACK	3
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_4:
	LACK	4
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_5:
	LACK	5
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_6:
	LACK	6
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_7:
	LACK	7
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_8:
	LACK	8
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_9:
	LACK	9
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_ASTERISK:
	LACK	10
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_0:
	LACK	0
	SAH	DTMF_VAL
	BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIAL_POUND:
	LACK	11
	SAH	DTMF_VAL
	;BS	B1,LOCAL_PROPHO0_DIALSTART
LOCAL_PROPHO0_DIALSTART:
	LACK	0
	SAH	MSG_T	;清零,表示重拔键无效
;---Store data first
	LAC	COUNT
	SBHK	33
	BZ	SGN,LOCAL_PROPHO0_DIALSTART1	;只保存前32个号码
	
	LAC	DTMF_VAL
	CALL	STORHALFBYTE_DAT

	LAC	COUNT
	ADHK	1
	SAH	COUNT
LOCAL_PROPHO0_DIALSTART1:
;-
	LACL	0X025
	SAH	PRO_VAR
;---mute mic	
	LACL	0x5F10		;set mic-pre-gain
	CALL	DAM_BIOSFUNC
	LACK	0		; --dB
	CALL	DAM_BIOSFUNC
;---dial	
	LAC	DTMF_VAL
	ORL	0XC2A0
	CALL	DAM_BIOSFUNC
	
	LACK	0
	SAH	PRO_VAR1
	LACL	100
	CALL	SET_TIMER	;100ms later
;---将数字左移显示
	LAC	LED_L
	SFL	8
	SAH	LED_L
	
	LAC	DTMF_VAL
	CALL	GET_SEGCODE
	CALL	SET_LED4

	RET
LOCAL_PROPHO0_VOLADD:
	LAC	VOI_ATT
	SFR	4
        ANDK	0X07
        SBHK	7		;上限
        BZ	SGN,LOCAL_PROPHO0_VOLSET
        LAC	VOI_ATT
        ADHK	0X010
        SAH	VOI_ATT
	BS	B1,LOCAL_PROPHO0_VOLSET

;---	
LOCAL_PROPHO0_VOLSUB:
	LAC	VOI_ATT
	SFR	4
        ANDK	0X07
        SBHK	0		;下限
        BS	ACZ,LOCAL_PROPHO0_VOLSET
        BS	SGN,LOCAL_PROPHO0_VOLSET
        LAC	VOI_ATT
	SBHK	0X010
        SAH	VOI_ATT
LOCAL_PROPHO0_VOLSET:
	LAC	VOI_ATT			;Set SPK_GAIN
	SFR	4
	ANDK	0X07
	ADHL	PHOVOL_TAB
	CALL    GetOneConst
	SAH	MSG_N
	
	LAC	VOI_ATT			;Set LINE _GAIN
	SFR	8
	ANDK	0X07
	ADHL	PHOVOL_TAB
	CALL    GetOneConst
	SFL	4
	OR	MSG_N
	
	CALL	SET_PHOVOL		;Set speakerphone vol	
;---Display volum	
	LAC	VOI_ATT			;Set SPK_GAIN
	SFR	4
	ANDK	0X07
	CALL	LED4_DISP
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER	

	RET
;---------------
LOCAL_PROPHO1_REDIAL:
	LAC	MSG_T		;重拔号码长度
	BS	ACZ,LOCAL_PROPHO1_REDIAL_END	;长度为零,就退出(有1,2,3,4,5,6,7,8,9,*,0,#键按下清零)
;---set souce address(ram)
	LACK	0
	SAH	MSG_ID		;已拔号码序号

	LACK	2
	SAH	COUNT		;第一个重拔数的偏移地址
	
	LACK	NEW1
	SAH	ADDR_S
	SAH	ADDR_D
;---mute mic	
	LACL	0x5F10		;set mic-pre-gain
	CALL	DAM_BIOSFUNC
	LACK	0		; --dB
	CALL	DAM_BIOSFUNC
;---	
	CALL	PHONE_START

	LACL	0X0065
	SAH	PRO_VAR		;回拔前等待状态
	
	LACK	0
	SAH	PRO_VAR1
	LACL	300
	CALL	SET_TIMER	;拔号及等待时间
LOCAL_PROPHO1_REDIAL_END:
	RET
;---------------
LOCAL_PROPHO1_FLASH:
	LACL	0X0045
	SAH	PRO_VAR
	
	;CALL	HOOK_OFF
;先关SPK---50ms	
	LIPK    6

	IN	CODECREG2,LOUTSPK
	LAC	CODECREG2
	ANDL	0XFFE0
	ORK	1
	SAH	CODECREG2	
	OUT	CODECREG2,LOUTSPK
	ADHK	0
	
	LACK	0
	SAH	PRO_VAR1
	LACL	50
	CALL	SET_TIMER	;
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO2:			;Do nothing until time over
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPHO2_0_TMR
	
	LAC	MSG
	CALL	STOR_MSG
	
	RET
LOCAL_PROPHO2_0_TMR:
	LACK	0X0015
	SAH	PRO_VAR
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO3:			;Key released(for timer display)
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPHO2_TMR
	
	BS	B1,LOCAL_PROPHO1_0_1
LOCAL_PROPHO2_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	5
	BS	SGN,LOCAL_PROPHO2_TMR1
	
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER
	
	LACK	0X0015
	SAH	PRO_VAR
LOCAL_PROPHO2_TMR1:	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO4:			;Flash
	LAC	MSG
	XORL	CMSG_TMR		;TMR
	BS	ACZ,LOCAL_PROPHO4_TMR
	
	RET

LOCAL_PROPHO4_TMR:
	LAC	PRO_VAR
	SFR	8
	ANDK	0X0F
	BS	ACZ,LOCAL_PROPHO4_0_TMR	;Spk_off --- HOOK_OFF
	SBHK	1
	BS	ACZ,LOCAL_PROPHO4_1_TMR	;HOOK_OFF
	SBHK	1
	BS	ACZ,LOCAL_PROPHO4_2_TMR	;HOOK_ON
	
	RET
LOCAL_PROPHO4_0_TMR:
		
	LACL	0X0145
	SAH	PRO_VAR
	
	CALL	HOOK_OFF
	
	LACK	0
	SAH	PRO_VAR1
	LACL	105
	CALL	SET_TIMER
	
	RET
LOCAL_PROPHO4_1_TMR:
		
	LACL	0X0245
	SAH	PRO_VAR
	
	CALL	HOOK_ON
	
	LACK	0
	SAH	PRO_VAR1
	LACL	50
	CALL	SET_TIMER
	
	
	RET
LOCAL_PROPHO4_2_TMR:
	LACK	0X0015
	SAH	PRO_VAR
;---
	LIPK    6

	IN	CODECREG2,LOUTSPK
	LAC	CODECREG2
	ANDL	0XFFE0
	ORK	SPK_GAIN
	SAH	CODECREG2	
	OUT	CODECREG2,LOUTSPK
	ADHK	0

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO5:
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO6:
	LAC	PRO_VAR
	SFR	8
	ANDK	0X0F
	BS	ACZ,LOCAL_PROPHO6_0	;Wait before dial
	SBHK	1
	BS	ACZ,LOCAL_PROPHO6_1	;dial
	SBHK	1
	BS	ACZ,LOCAL_PROPHO6_2	;Wait between dial
	
	RET
;-----------------------------------------------------------
LOCAL_PROPHO6_0:		;wait for dial
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,LOCAL_PROPHO6_0_TMR
	
	RET
;---
LOCAL_PROPHO6_0_TMR:		
	LACL	0X0165
	SAH	PRO_VAR
	
	LACK	100
	CALL	SET_TIMER

	;LAC	MSG_ID
	;SBH	MSG_T
	;BZ	SGN,LOCAL_PROPHO6_2_TMR_END	;拔完了吗?(能执行到此,肯定满足拔号条件)
;---取号码
	CALL	GETHALFBYTE_DAT
	SAH	DTMF_VAL

	LAC	COUNT
	ADHK	1
	SAH	COUNT		;下一号码偏移地址
	
	LAC	MSG_ID
	ADHK	1
	SAH	MSG_ID		;下一号码序号
;---dial	
	LAC	DTMF_VAL
	ORL	0XC2A0
	CALL	DAM_BIOSFUNC
	
	RET
;---------------------------------------
LOCAL_PROPHO6_1:		;dial and wait for end
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,LOCAL_PROPHO6_1_TMR
	
	RET
LOCAL_PROPHO6_1_TMR:
	LACL	0X0265
	SAH	PRO_VAR
;---dial end
	LACL	0XC200
	CALL	DAM_BIOSFUNC

	RET
;---------------------------------------
LOCAL_PROPHO6_2:		;wait for dial
	LAC	MSG
	XORL	CMSG_TMR
	BS	ACZ,LOCAL_PROPHO6_2_TMR
	
	RET
;---
LOCAL_PROPHO6_2_TMR:	
	LACL	0X0165
	SAH	PRO_VAR
;??????????????????????????????????????????	
	LAC	MSG_ID
	SBH	MSG_T
	BZ	SGN,LOCAL_PROPHO6_2_TMR_END;	;拔完了吗?
;---拔完了吗?	
	CALL	GETHALFBYTE_DAT
	SAH	DTMF_VAL
	
	LAC	COUNT
	ADHK	1
	SAH	COUNT
	
	LAC	MSG_ID
	ADHK	1
	SAH	MSG_ID
;??????????????????????????????????????????
;---dial	
	LAC	DTMF_VAL
	ORL	0XC2A0
	CALL	DAM_BIOSFUNC
	
	RET
LOCAL_PROPHO6_2_TMR_END:
	LACL	0x5F10		;set mic-pre-gain
	CALL	DAM_BIOSFUNC
	LACK	MIC_GAIN	; --dB
	CALL	DAM_BIOSFUNC
	
	LACL	0XC000
	CALL	DAM_BIOSFUNC
	
	LACL	0X0015
	SAH	PRO_VAR
	
	LACL	1000
	CALL	SET_TIMER

	LACK	0
	SAH	COUNT
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PROPHO_TMR:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1

	LAC	PRO_VAR1
	ANDK	0X03F
	CALL	LED_HLDISP

	RET
;###############################################################################
;	Function : SET_PHONERUN
;
;###############################################################################
SET_PHONERUN:
;---start free run--------------------------------------------------------------
   	CALL	PHONE_START
;---Line mute
   	LACL  	0XC040
   	CALL  	DAM_BIOSFUNC
;---DTMF GIAN  BIT0-3 HI BIT4-7 LO
   	;LACL   0XC154
   	LACL   	0XC121
   	CALL    DAM_BIOSFUNC
;---Set ERL_AEC & ERL_LEC
   	LACL	0XC430		;default Value
   	CALL	DAM_BIOSFUNC
;---Set T/R & R/T ratio
	LACL	0XC603
   	CALL	DAM_BIOSFUNC
;---Set LINE _GAIN & SPK_GAIN
	LAC	VOI_ATT			;Get SPK_GAIN
	SFR	4
	ANDK	0X07
	ADHL	PHOVOL_TAB
	CALL    GetOneConst
	SAH	MSG_N
	
	;LAC	VOI_ATT			;Get LINE _GAIN
	;SFR	8
	;ANDK	0X07
	;ADHL	PHOVOL_TAB
	;CALL	GetOneConst
	;SFL	4

	LACL	0X0A0			;Get LINE _GAIN
	OR	MSG_N			;Get SPK_GAIN
	CALL	SET_PHOVOL		;0XC8XX(default Value = 0XC8AA)
;---Set Loop Attenuation
	LACL	0XC904
	CALL	DAM_BIOSFUNC
;---Line mute
    	LACL    0xC040         		;wait line mute for 400 ms
    	CALL    DAM_BIOSFUNC
;---free run
;    	CALL	PHONE_START

	RET
;-------------------------------------------------------------------------------
.END
