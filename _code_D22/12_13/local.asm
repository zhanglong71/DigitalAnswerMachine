.LIST
LOCAL_PRO:
	LAC	PRO_VAR
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0		;(F0)0 = idle
	SBHK	1
	BS	ACZ,LOCAL_PROPLY	;(F1) = play memo/icm
	SBHK	1
	BS	ACZ,LOCAL_PROREC	;2 = record memo
	SBHK	1
	BS	ACZ,LOCAL_PROOGM	;3 = record/play OGM
	SBHK	1
	BS	ACZ,LOCAL_PROMNU	;4 = for menu
	SBHK	1
	BS	ACZ,LOCAL_PROPHO	;5 = speaker phone
	SBHK	1
	BS	ACZ,LOCAL_PROVOP	;6 = for VOP test
	
	RET
;---------------事件响应区
LOCAL_PRO0:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0_0	;0 = idel
	SBHK	1
	BS	ACZ,LOCAL_PRO0_1	;1 = VP end(for key mode)
	SBHK	1
	BS	ACZ,LOCAL_PRO0_2	;2 = erase all
	SBHK	1
	BS	ACZ,LOCAL_PRO0_3	;3 = format
	
	RET
	
LOCAL_PRO0_0:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,LOCAL_PRO0_INIT
;LOCAL_PRO0_0_1:	
	LAC	MSG
	XORL	CMSG_KEY1S		;play
	BS	ACZ,LOCAL_PRO0_PLYMSG
;LOCAL_PRO0_0_2:
	LAC	MSG
	XORL	CMSG_KEY2S		;VOL+++
	BS	ACZ,LOCAL_PRO0_VOLA
;LOCAL_PRO0_0_3:
	LAC	MSG
	XORL	CMSG_KEY3S		;VOL---
	BS	ACZ,LOCAL_PRO0_VOLS
;LOCAL_PRO0_0_4:
	LAC	MSG
	XORL	CMSG_KEY4S		;speakerphone
	BS	ACZ,LOCAL_PRO0_PROPHO
;LOCAL_PRO0_0_6:
	LAC	MSG
	XORL	CMSG_KEY6S		;on/off
	BS	ACZ,LOCAL_PRO0_ONOFF
;LOCAL_PRO0_0_6_1:
	LAC	MSG
	XORL	CMSG_KEY6L		;OGM select
	BS	ACZ,LOCAL_PRO0_SELOGM
;LOCAL_PRO0_7:
	
;LOCAL_PRO0_0_7_1:
	LAC	MSG
	XORL	CMSG_KEY7L		;Record message
	BS	ACZ,LOCAL_PRO0_RECMSG
;LOCAL_PRO0_0_9:	
	LAC	MSG
	XORL	CMSG_KEY9S		;VOP_PLAY
	BS	ACZ,LOCAL_PRO0_PLYVOP
;LOCAL_PRO0_0_10:
	LAC	MSG
	XORL	CMSG_KEYAS		;DISPLAY RING
	BS	ACZ,LOCAL_PRO0_DISPRING
;LOCAL_PRO0_0_10_1:
	LAC	MSG
	XORL	CMSG_KEYAL		;SET RING
	BS	ACZ,LOCAL_PRO0_SETRING
;LOCAL_PRO0_0_11:
	LAC	MSG
	XORL	CMSG_KEYBS		;PLAY TIME
	BS	ACZ,LOCAL_PRO0_PLYTIME
;LOCAL_PRO0_0_11_1:
	LAC	MSG
	XORL	CMSG_KEYBL		;SET TIME
	BS	ACZ,LOCAL_PRO0_SETTIME
;LOCAL_PRO0_0_12_1:
	LAC	MSG
	XORL	CMSG_KEYCL		;ERASE ALL
	BS	ACZ,LOCAL_PRO0_REAALL
;LOCAL_PRO0_0_13:
	LAC	MSG
	XORL	CMSG_KEYDS		;DISPLAY RATE
	BS	ACZ,LOCAL_PRO0_DISPRATE
;LOCAL_PRO0_0_13_1:
	LAC	MSG
	XORL	CMSG_KEYDL		;SET RATE
	BS	ACZ,LOCAL_PRO0_SETRATE
;LOCAL_PRO0_14:
	LAC	MSG
	XORL	CMSG_KEYES		;Display code
	BS	ACZ,LOCAL_PRO0_DISPCODE
;LOCAL_PRO0_14_1:
	LAC	MSG
	XORL	CMSG_KEYEL		;set code
	BS	ACZ,LOCAL_PRO0_SETCODE
;LOCAL_PRO0_0_15:
	LAC	MSG
	XORL	CMSG_KEYFS		;OGM_PLAY
	BS	ACZ,LOCAL_PRO0_PLYOGM
;LOCAL_PRO0_0_15_1:
	LAC	MSG
	XORL	CMSG_KEYFL		;OGM_RECORD
	BS	ACZ,LOCAL_PRO0_RECOGM

;LOCAL_PRO0_16:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,LOCAL_PRO0_TMR
;LOCAL_PRO0_17:	
	LAC	MSG
	XORL	CCAS_TONE		;Cas-tone end
	BS	ACZ,LOCAL_PRO0_CASTONE
;LOCAL_PRO0_18:
	LAC	MSG
	XORL	CRING_ON		;ringing
	BS	ACZ,LOCAL_PRO0_RINGON
;LOCAL_PRO0_19:
	LAC	MSG
	XORL	CVP_STOP		;VP_STOP
	BS	ACZ,LOCAL_PRO0_VPSTOP

	RET
;---------------------------------------
LOCAL_PRO0_1:
	RET
;---------------------------------------
LOCAL_PRO0_2:
	LAC	MSG
	XORL	CRING_OFF		;RING end
	BS	ACZ,LOCAL_PRO0_RINGOFF
	LAC	MSG
	XORL	CVP_STOP		;VP_STOP
	BS	ACZ,LOCAL_PRO0_2_VPSTOP
	LAC	MSG
	XORL	CMSG_KEY4S		;speakerphone
	BS	ACZ,LOCAL_PRO0_PROPHO
	
	RET
LOCAL_PRO0_2_VPSTOP:
	;CALL	WBEEP
	CALL	RBEEP
	
	RET
LOCAL_PRO0_RINGOFF:
	CALL	INIT_DAM_FUNC
	LACK	0
	SAH	PRO_VAR
	
	BS	B1,LOCAL_PRO0_INIT
;---------------------------------------	
LOCAL_PRO0_3:
	RET
;---------------
LOCAL_PRO0_FLASH:
LOCAL_PRO0_REDIAL:
LOCAL_PRO0_SENDCOMM:
	;LACL	0XAA
	;CALL	PUT_DAT
	
	RET
LOCAL_PRO0_RINGON:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	;CALL	WBEEP
	CALL	RBEEP
	
	LACK	0X020
	SAH	PRO_VAR
	
	RET
LOCAL_PRO0_DISPRATE:
	RET
LOCAL_PRO0_SETRATE:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LACK	0X0034
	SAH	PRO_VAR
	
	CALL	LOAD_MNUF_VP	;提取文件

	LAC	TAD_ATT
	SFR	4
	ANDK	0X0F
	SAH	MSG_N
	ADHL	RATE_TAB
	CALL	GetOneConst
	CALL	LED_HLDISP

	LACK	0
	SAH	NEW1
	LACK	5
	SAH	NEW2

	;LACL	0XF4		;F4
	;SAH	DugRam1
	;SAH	DugRam2

	LACL	0XAF		;"rA"
	CALL	SET_LED1
	LACL	0X88
	CALL	SET_LED2
;---for check VOP
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER
	
	RET
LOCAL_PRO0_DISPCODE:
	RET
LOCAL_PRO0_SETCODE:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LACK	0X0014
	SAH	PRO_VAR
	
	CALL	LOAD_MNUF_VP	;提取文件
	
	LAC	PASSWORD
	SFR	8
	ANDK	0X0F
	SAH	MSG_N
	CALL	LED4_DISP

	LACK	0
	SAH	NEW1
	LACK	9
	SAH	NEW2
	
	LACL	0XF7
	CALL	SET_LED3	;下划线

	LACL	0XC6	;"C1"
	CALL	SET_LED1
	LACL	0XF9
	CALL	SET_LED2
;---for check VOP
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER
	
	RET
;---------------
LOCAL_PRO0_INIT:
	CALL	DAA_OFF
	CALL	HOOK_OFF
	CALL	GC_CHK
	CALL	VPMSG_CHK
	CALL	IicInt_Off

	CALL	GET_FILEID
	;CALL	SET_FILEID	;set use_dat()

	CALL	LINE_START	;Note : check CID in standby

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER
;---		
;	LACL	0XF0	;F000
;	SAH	DugRam1
;	SAH	DugRam2
;---
	LACL	0X8E
	CALL	SET_LED1
	LACL	0XC0
	CALL	SET_LED2
	LACL	0XC0
	CALL	SET_LED3
	LACL	0XC0
	CALL	SET_LED4

	RET
;-----------------------------------------------------------
LOCAL_PRO0_PROPHO:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPKPHONE
	CALL	HOOK_ON
	LACK	0X005
	SAH	PRO_VAR
;---	
	CALL	LOAD_LOCF_VP
;---set address first
	LACK	NEW1
	SAH	ADDR_S
	SAH	ADDR_D
	LACK	0
	SAH	MSG_T
;---Read data from flash and store in ram
	LACK	6
	CALL	SET_TELGROUP
	CALL	GET_TELT	;取条目号(the last one in current group)
	BS	ACZ,LOCAL_PRO0_PROPHO1	
	CALL	READ_TELNUM	;读当前条目数据
	CALL	STOPREADDAT
;---Get length	
	LACK	0
	SAH	COUNT
	CALL	GETBYTE_DAT
	SAH	MSG_T		;总号码长度(the first byte)
LOCAL_PRO0_PROPHO1:	
	LACK	2
	SAH	COUNT		;若重拔有效,此为第一个重拔数的偏移地址;也是拔第一个数的存放地址
		
	LACK	0
	SAH	MSG_ID		;当前已拔的个数
;---close ALC
	LACL	0X9000
	CALL	DAM_BIOSFUNC
;---for Debug(LED display)
	LACL	0X8E	;"F5"
	CALL	SET_LED1
	LACL	0X92
	CALL	SET_LED2
;---for wait
	LACK	0
	SAH	PRO_VAR1
	LACL	400
   	CALL	SET_TIMER
;---start free run--------------------------------------------------------------
.if	0
   	CALL	PHONE_START
;---Line mute
   	LACL  	0XC040
   	CALL  	DAM_BIOSFUNC
;---DTMF GIAN  BIT0-3 HI BIT4-7 LO
   	;LACL   	0XC154
   	LACL   	0XC121
   	CALL    DAM_BIOSFUNC
;---Set ERL_AEC & ERL_LEC
	LAC	PHONE_ATT
	SFR	8
	ANDL	0X0FF
	ORL	0XC400
   	;LACL	0XC430		;default Value
   	CALL	DAM_BIOSFUNC
;---Set T/R & R/T ratio
	LAC	PHONE_ATT
	ANDK	0X07
   	;LACL	0XC603
   	ORL	0XC600
   	CALL	DAM_BIOSFUNC
;---Set LINE _GAIN & SPK_GAIN
	LAC	VOI_ATT			;Set SPK_GAIN
	SFR	4
	ANDK	0X07
	ADHL	PHOVOL_TAB
	CALL    GetOneConst
	SAH	MSG_N
	
	LAC	VOI_ATT			;Set LINE _GAIN
	SFR	8
	ANDK	0X07
	ADHL	PHOVOL_TAB
	CALL    GetOneConst
	SFL	4
	OR	MSG_N
	CALL	SET_PHOVOL		;0XC8XX(default Value = 0XC8AA)

;---Set Loop Attenuation
	LAC	PHONE_ATT
	SFR	4
	ANDK	0X07
	;LACL	0XC903			;default Value
	ORL	0XC900
	CALL	DAM_BIOSFUNC
;---Line mute
    	LACL    0xC040         		;wait line mute for 400 ms
    	CALL    DAM_BIOSFUNC
;---free run
    	CALL	PHONE_START
.else
	CALL	SET_PHONERUN
.endif	
	RET
;-----------------------------------------------------------
LOCAL_PRO0_VPSTOP:
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
LOCAL_PRO0_RATE:
	RET
LOCAL_PRO0_REAALL:
	LACL	0X6080
	CALL	DAM_BIOSFUNC
	
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	RET
LOCAL_PRO0_PLYTIME:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	
	CALL	CURR_WEEK
	CALL	CURR_HOUR
	CALL	CURR_MIN
	
	RET
LOCAL_PRO0_SETTIME:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LACL	0X0024
	SAH	PRO_VAR

	CALL	LOAD_MNUF_VP	;提取文件
	
	CALL	CWEEK_GET
	ANDK	0X0F
	SAH	MSG_N
	CALL	LED_HLDISP

	LACK	0
	SAH	NEW1
	LACK	6
	SAH	NEW2

	LACL	0X8E	;"F4"
	CALL	SET_LED1
	LACL	0X99
	CALL	SET_LED2
;---for check VOP
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER
	
	RET
LOCAL_PRO0_DISPRING:
	
	RET
LOCAL_PRO0_SETRING:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LACK	0X0044
	SAH	PRO_VAR
	
	CALL	LOAD_MNUF_VP	;提取文件
	
	LAC	VOI_ATT
	SFR	12
	ANDK	0X0F
	SAH	MSG_N
	CALL	LED_HLDISP

	LACK	1
	SAH	NEW1
	LACK	14
	SAH	NEW2

;	LACL	0XF4	;F4
;	SAH	DugRam1
;	SAH	DugRam2
	
	LACL	0XAF	;"rn"
	CALL	SET_LED1
	LACL	0XAB
	CALL	SET_LED2
;---for check VOP
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER

	RET
;---------------------------------------	
LOCAL_PRO0_VOLS:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
LOCAL_PROX_VOLS:
	LACL	CMSG_VOLS
	CALL	STOR_MSG
	
	RET
LOCAL_PRO0_VOLA:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
LOCAL_PROX_VOLA:
	LACL	CMSG_VOLA
	CALL	STOR_MSG
	
	RET
;---------------------------------------
LOCAL_PRO0_RECMSG:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	LBEEP
	
	CALL	SET_COMPS
	LACK	0X0002
	SAH	PRO_VAR

	CALL	LOAD_LOCF_VP
;---
	LACL	0XAF	;"rE"
	CALL	SET_LED1
	LACL	0X86
	CALL	SET_LED2

	BIT	ANN_FG,13	;check memoful?
	BS	TB,LOCAL_PRO0_RECMSG_MFUL

	RET
LOCAL_PRO0_RECMSG_MFUL:

	BS	B1,LOCAL_PROWORN

LOCAL_PRO0_RECOGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	LBEEP
	CALL	SET_COMPS
	
	CALL	LOAD_LOCF_VP
;---
	;LACL	0XF3	;F3
	;SAH	DugRam1
	;SAH	DugRam2

	LACK	0X0003
	SAH	PRO_VAR

	BIT	ANN_FG,13	;check memoful?
	BS	TB,LOCAL_PROWORN
	
	RET
LOCAL_PRO0_SELOGM:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LACL	0X0054
	SAH	PRO_VAR

	CALL	LOAD_MNUF_VP	;提取文件

	LAC	EVENT
	SFR	8
	ANDK	0X01
	ADHK	1
	CALL	LED_HLDISP

	LACL	0X88	;"An"
	CALL	SET_LED1
	LACL	0XAB
	CALL	SET_LED2
;---for check VOP
	LACK	0
	SAH	PRO_VAR1
	LACL	1000
   	CALL	SET_TIMER

	RET
LOCAL_PRO0_PLYOGM:
	CALL	LOAD_LOCF_VP

	CALL	DAA_SPK
	LACK	0X0023
	SAH	PRO_VAR		;进入播放OGM子功能

LOCAL_LOADOGM:
	CALL	INIT_DAM_FUNC
	;LACK	COGM_ID
	CALL	OGM_SELECT

;---LED_L "PA"
	LACL	0X8C88
	SAH	LED_L
	
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	;LACK	COGM_ID
	CALL	OGM_SELECT
	BZ	ACZ,LOCAL_PRO0_PLAY_OGM1
	
	CALL	INIT_DAM_FUNC
	LACK	41
	ORL	0XFF00
	CALL	STOR_VP

LOCAL_PRO0_PLAY_OGM1:

	RET
;---------------------------------------
LOCAL_PRO0_TMR:
	LAC	PRO_VAR1
	ADHK	1
	ANDK	0X3F
	SAH	PRO_VAR1
	CALL	LED_HLDISP

	;LAC	LED_H
	;XORL	0X0080
	;SAH	LED_H
	


	RET
LOCAL_PRO0_PLYVOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACK	0X0005
	CALL	STOR_VP
	
	LACK	0X0006
	SAH	PRO_VAR	
	
	LACK	0
	SAH	MSG_ID
	
	RET
LOCAL_PRO0_PLYMSG:
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACK	0X0005
	CALL	STOR_VP

	CALL	VPMSG_CHK		;进入播放子功能
	CALL	CLR_FLAG
	LACK	0X0001
	SAH	PRO_VAR	
;---
	LACL	0X8C	;"PL"
	CALL	SET_LED1
	LACL	0XC7
	CALL	SET_LED2

	LACK	0
	SAH	MSG_ID
;---
	CALL	LOAD_LOCF_VP
	
	BIT	ANN_FG,12
	BS	TB,LOCAL_PRO0_PLAY_NEW
	BIT	ANN_FG,14
	BS	TB,LOCAL_PRO0_PLAY_ALL
;LOCAL_PRO0_PLYMSG_NO:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	VP_No
	CALL	VP_Message

	LACK	0X0
	SAH	PRO_VAR	
	
	RET
;-------play new messages
LOCAL_PRO0_PLAY_NEW:
	LAC	MSG_N
	SBHK	1
	BZ	ACZ,LOCAL_PRO0_PLAY_NEWx
;LOCAL_PRO0_PLAY_NEW1:
	LAC	MSG_N
	CALL	ANNOUNCE_NUM
	CALL	VP_New
	CALL	VP_Message	;单数
	
	RET
LOCAL_PRO0_PLAY_NEWx:
	LAC	MSG_N
	CALL	ANNOUNCE_NUM
	CALL	VP_New
	CALL	VP_Messages	;复数
	
	RET
;-------play all messages
LOCAL_PRO0_PLAY_ALL:
	LAC	MSG_T
	SBHK	1
	BZ	ACZ,LOCAL_PRO0_PLAY_ALLx
;LOCAL_PRO0_PLAY_ALL1:
	LAC	MSG_T
	CALL	ANNOUNCE_NUM
	CALL	VP_Message	;单数
	
	RET
LOCAL_PRO0_PLAY_ALLx:
	LAC	MSG_T
	CALL	ANNOUNCE_NUM
	CALL	VP_Messages	;复数
	
	RET
LOCAL_PRO0_ONOFF:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	
	LAC	EVENT		;EVENT.9
	XORL	1<<9
	SAH	EVENT
	
	RET

;-------------------------------------------------------------------------------	
LOCAL_PROEND_BEFORINIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBEEP
	
	LACK	0X0
	SAH	PRO_VAR

	RET
;-------------------------------------------------------------------------------

LOCAL_PROOGM_PLYOGM:		;PLAY 
	LAC	MSG
	XORL	CMSG_KEYAS		;stop record(手动停止)
	BS	ACZ,LOCAL_PROOGM_PLYOGM_STOP
LOCAL_PROOGM_PLYOGM0_1:	
	LAC	MSG
	XORL	CVP_STOP		;stop record(播完停止)
	BS	ACZ,LOCAL_PROOGM_PLYOGM_STOP
LOCAL_PROOGM_PLYOGM0_2:	
	LAC	MSG
	XORL	CMSG_KEYES		;ERASE OGM(删除)
	BS	ACZ,LOCAL_PROOGM_PLYOGM_ERASE
LOCAL_PROOGM_PLYOGM0_3:
	;LAC	MSG
	;XORL	CMSG_KEYBS		;VOL-
	;BS	ACZ,LOCAL_PROX_VOLS
LOCAL_PROOGM_PLYOGM0_4:
	LAC	MSG
	XORL	CMSG_KEYFS		;VOL+
	BS	ACZ,LOCAL_PROX_VOLA
	
	RET
LOCAL_PROOGM_PLYOGM_STOP:
	BS	B1,LOCAL_PROEND_BEFORINIT
LOCAL_PROOGM_PLYOGM_ERASE:

	LAC	MSG_ID
	ANDL	0X0FF
	ORL	0X2080
	SAH	CONF
	CALL	DAM_BIOS
	
	LACL	0X6100
	SAH	CONF
	CALL	DAM_BIOS
	
	CALL	INIT_DAM_FUNC
	LACK	0X005
	CALL	STOR_VP
	
	RET

;-------------------------------------------------------------------------------
LOCAL_PROVOP:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,LOCAL_PROVOP_OVER
LOCAL_PROVOP_1:	
	LAC	MSG
	XORL	CMSG_KEY5S		;ON/OFF(stop)
	BS	ACZ,LOCAL_PROVOP_STOP
	
	RET
LOCAL_PROVOP_OVER:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	VOP_MAXID
	BZ	SGN,LOCAL_PROVOP_STOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID
	
	LAC	MSG_ID
	ORL	0XFF00
	CALL	STOR_VP
	
	RET
LOCAL_PROVOP_STOP:
	CALL	INIT_DAM_FUNC
	BS	B1,LOCAL_PROEND_BEFORINIT
;-------------------------------------------------------------------------------
LOCAL_PROEXT:

	RET
;-------------------------------------------------------------------------------
LOCAL_PROWORN:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
	
	LACK	0X0
	SAH	PRO_VAR
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_CASTONE:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	LACK	0
	SAH	PRO_VAR
	
	call	BBBEEP	;?????????????????????????????????????????????
	
	CALL	LINE_START
	
	RET
;-------------------------------------------------------------------------------
	
.END
