.LIST
LOCAL_PRO:
	LAC	PRO_VAR
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0		;(F0)0 = idle
	SBHK	1
	;BS	ACZ,LOCAL_PROPLY	;(F1) = play memo/icm
	SBHK	1
	BS	ACZ,LOCAL_PROREC	;(F2) = record memo
	SBHK	1
	BS	ACZ,LOCAL_PROOGM	;(F3) = record/play OGM
	SBHK	1
	BS	ACZ,LOCAL_PROMNU	;(F4) = for set time and remote code
	SBHK	1
	BS	ACZ,LOCAL_PROTWR	;(F5) = two way record
	SBHK	1
	BS	ACZ,LOCAL_PROVOP	;(F6) = for VOP test
	SBHK	1
	BS	ACZ,LOCAL_PROTXT	;(F7) = for test mode
	SBHK	1
	BS	ACZ,LOCAL_PROPNEW	;(F8) = for play new message
	SBHK	1
	BS	ACZ,LOCAL_PROPALL	;(F9) = for play all message
	
	RET
;---------------事件响应区
LOCAL_PRO0:
	LAC	PRO_VAR
	SFR	4
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0_0	;0 = idel
	SBHK	1
	BS	ACZ,LOCAL_PRO0_1	;1 = VP end(for key mode)
	SBHK	1
	BS	ACZ,LOCAL_PRO0_2	;2 = VP end(for vop only)
	SBHK	1
	BS	ACZ,LOCAL_PRO0_3	;3 = format
	
	RET
;-------------------------------------------------------------------------------	
LOCAL_PRO0_0:	
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,LOCAL_PRO0_INIT
LOCAL_PRO0_0_1:
	LAC	MSG
	XORL	CMSG_PLY		;Record message
	BS	ACZ,LOCAL_PRO0_PLYMSG
LOCAL_PRO0_0_2:
	LAC	MSG
	XORL	CMSG_PNEW		;Play new message
	BS	ACZ,LOCAL_PRO0_PLYNEW
	LAC	MSG
	XORL	CMSG_PALL		;Play all message
	BS	ACZ,LOCAL_PRO0_PLYALL
LOCAL_PRO0_0_3:
	LAC	MSG
	XORL	CMSG_KEY3L		;Erase all old messages
	BS	ACZ,LOCAL_PRO0_REAALL
LOCAL_PRO0_0_4:
	LAC	MSG
	XORL	CMSG_KEY4S		;Record two way
	BS	ACZ,LOCAL_PRO0_RECTWA
	LAC	MSG
	XORL	CMSG_KEY4L		;Record message
	BS	ACZ,LOCAL_PRO0_RECMSG
LOCAL_PRO0_0_5:
	LAC	MSG
	XORL	CPLY_OGM		;play OGM
	BS	ACZ,LOCAL_PRO0_PLYOGM
LOCAL_PRO0_0_5_1:	
	LAC	MSG
	XORL	CREC_OGM		;record OGM
	BS	ACZ,LOCAL_PRO0_RECOGM
;LOCAL_PRO0_0_6:
	LAC	MSG
	XORL	CMSG_KEY6S		;on/off
	BS	ACZ,LOCAL_PRO0_VPSTOP
;LOCAL_PRO0_0_7:
	LAC	MSG
	XORL	CMSG_KEY7D		;VOL+
	BS	ACZ,LOCAL_PRO0_VOLA
;LOCAL_PRO0_0_8:
	LAC	MSG
	XORL	CMSG_KEY8D		;VOL-
	BS	ACZ,LOCAL_PRO0_VOLS
LOCAL_PRO0_0_10:
	LAC	MSG
	XORL	CKEY_VOP
	BS	ACZ,LOCAL_PRO0_KEYVOP		;!!!要求语音播放完毕后,向mcu发退出指令0X2D
LOCAL_PRO0_0_11:
	LAC	MSG
	XORL	CVOP_VOP
	BS	ACZ,LOCAL_PRO0_VOPVOP		;!!!要求语音播放完毕后,不向mcu发退出指令0X2D
LOCAL_PRO0_0_16:
	LAC	MSG
	XORL	CMSG_TMR		;CMSG_TMR
	BS	ACZ,LOCAL_PRO0_TMR
LOCAL_PRO0_0_17:
	LAC	MSG
	XORL	CVP_STOP		;VP_STOP
	BS	ACZ,LOCAL_PRO0_VPSTOP
LOCAL_PRO0_0_19:
	LAC	MSG
	XORL	CTMODE_IN
	BS	ACZ,LOCAL_PRO0_TMODE
LOCAL_PRO0_0_20:	
	LAC	MSG
	XORL	CMSG_TVOP
	BS	ACZ,LOCAL_PRO0_TVOP

	RET
;---------------------------------------
LOCAL_PRO0_1:		;!!!要求语音播放完毕后,向mcu发退出指令0X2D
	LAC	MSG
	XORL	CMSG_KEY6S
	BS	ACZ,LOCAL_PRO0_VPSTOP
	LAC	MSG
	XORL	CKEY_VOP
	BS	ACZ,LOCAL_PRO0_KEYVOP
	LAC	MSG
	XORL	CVP_STOP		;VP_STOP
	BS	ACZ,LOCAL_PRO0_1_VPSTOP
	RET
LOCAL_PRO0_1_VPSTOP:
;!!!	
	CALL	EXIT_TOIDLE
;!!!
	LACK	0
	SAH	PRO_VAR
	BS	B1,LOCAL_PRO0_INIT
;-------------------------------------------------------------------------------
LOCAL_PRO0_2:
	LAC	MSG
	XORL	CMSG_KEY6S		;stop command
	BS	ACZ,LOCAL_PRO0_2_STOP
	LAC	MSG
	XORL	CVP_STOP		;erase key released
	BS	ACZ,LOCAL_PRO0_2_VPSTOP
	LAC	MSG
	XORL	CVOP_VOP
	BS	ACZ,LOCAL_PRO0_VOPVOP
	
	RET
LOCAL_PRO0_2_STOP:
	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR

	RET
LOCAL_PRO0_2_VPSTOP:
	BS	B1,LOCAL_PRO0_2_STOP
;-------------------------------------------------------------------------------	
LOCAL_PRO0_3:
	RET
;---------------

LOCAL_PRO0_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_OFF
	CALL	GC_CHK
	CALL	VPMSG_CHK
	LACL	500
	CALL	SET_TIMER
	LACK	0
	SAH	PRO_VAR1
;---
	RET
;---------------------------------------
LOCAL_PRO0_VPSTOP:
	LACK	0
	SAH	PRO_VAR
	BS	B1,LOCAL_PRO0_INIT
;---------------------------------------
LOCAL_PRO0_TMODE:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP

	LACK	0x0007
	SAH	PRO_VAR

	CALL	LOAD_TETF_VP
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_VOPVOP:		;!!!要求语音播放完毕后,不向mcu发退出指令0X2D
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACK	0X020
	SAH	PRO_VAR
;LOCAL_PRO0_VOPVOP_FIRSTCHK:		;第一次判断
	LAC	DTMF_VAL
	SBHK	0X75
	BS	ACZ,LOCAL_PRO0_VOPVOP_0X75	;0X75(Toll_Saver)
	SBHK	1
	BS	ACZ,LOCAL_PRO0_VOPVOP_0X76	;0x76
	SBHK	1
	BS	ACZ,LOCAL_PRO0_VOPVOP_0X77	;0X77
	SBHK	1
	BS	ACZ,LOCAL_PRO0_VOPVOP_0X78	;0X78
	
;LOCAL_PRO0_VOPVOP_SECONDCHK:		;第二次判断(12..19)
	LAC	DTMF_VAL
	SBHK	0X12
	BS	SGN,LOCAL_PRO0_VOPVOP_THIRDCHK	;<12
	SBHK	0X8
	BZ	SGN,LOCAL_PRO0_VOPVOP_THIRDCHK	;>20
	BS	B1,LOCAL_PRO0_VOPVOPXXRING
	
LOCAL_PRO0_VOPVOP_THIRDCHK:		;第三次判断(0..9)
	LAC	DTMF_VAL
	BS	SGN,LOCAL_PRO0_VOPVOP_FORTHCHK	;<0
	SBHK	0X10
	BZ	SGN,LOCAL_PRO0_VOPVOP_FORTHCHK	;>9
	BS	B1,LOCAL_PRO0_VOPVOPNUM
	
	
LOCAL_PRO0_VOPVOP_FORTHCHK:		;第四次判断
	CALL	BBBEEP
	
	RET
LOCAL_PRO0_VOPVOPNUM:
	LAC	DTMF_VAL
	CALL	ANNOUNCE_NUM

	RET
LOCAL_PRO0_VOPVOPXXRING:
	LAC	DTMF_VAL
	SBHK	0X010
	CALL	ANNOUNCE_NUM
	CALL	VP_Rings
	
	RET
LOCAL_PRO0_VOPVOP_0X75:
	CALL	VP_TollSaver
	
	RET
LOCAL_PRO0_VOPVOP_0X76:
	CALL	VP_PleaseSet
	CALL	VP_Rings
	
	LAC	VOI_ATT
	SFR	12
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0_VOPVOP_0X75
	
	CALL	ANNOUNCE_NUM
	CALL	VP_Rings
	
	RET
LOCAL_PRO0_VOPVOP_0X77:		;0X4F0X00
	CALL	VP_SecurityCode
;---PS1
	LAC	PASSWORD
	SFR	8
	ANDK	0X0F
	CALL	ANNOUNCE_NUM
;---PS2
	LAC	PASSWORD
	SFR	4
	ANDK	0X0F
	CALL	ANNOUNCE_NUM
;---PS3
	LAC	PASSWORD
	ANDK	0X0F
	CALL	ANNOUNCE_NUM
;---
	CALL	VP_PleaseSet
	CALL	VP_SecurityCode

	RET
LOCAL_PRO0_VOPVOP_0X78:		;0X4F0X01
	CALL	VP_SecurityCode
;---PS1
	LAC	PASSWORD
	SFR	8
	ANDK	0X0F
	CALL	ANNOUNCE_NUM
;---PS2
	LAC	PASSWORD
	SFR	4
	ANDK	0X0F
	CALL	ANNOUNCE_NUM
;---PS3
	LAC	PASSWORD
	ANDK	0X0F
	CALL	ANNOUNCE_NUM

	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_KEYVOP:		;!!!要求语音播放完毕后,向mcu发退出指令0X2D
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACK	0X010
	SAH	PRO_VAR
;LOCAL_PRO0_KEYVOP_LANGVOP:
	LAC	DTMF_VAL
	SBHK	0X71
	BS	ACZ,LOCAL_PRO0_KEYVOP_ANSWEROFF		;0X71
	SBHK	1
	BS	ACZ,LOCAL_PRO0_KEYVOP_ANSWERON		;0x72
	SBHK	1
	BS	ACZ,LOCAL_PRO0_KEYVOP_ANNOUNCEONE	;0X73
	SBHK	1
	BS	ACZ,LOCAL_PRO0_KEYVOP_ANSWERONTWO	;0X74
	SBHK	1
	BS	ACZ,LOCAL_PRO0_KEYVOP_PASSWORD		;0X75
	SBHK	1
	BS	ACZ,LOCAL_PRO0_KEYVOP_RINGDELAY		;0X76

	CALL	BBBEEP
	
	RET

LOCAL_PRO0_KEYVOP_ANSWERON:
	CALL	VP_CHKMemoryFull	;If Memful play "VP_MemoryFull"
	CALL	VP_AnswerOn

	RET
LOCAL_PRO0_KEYVOP_ANSWEROFF:
	CALL	VP_CHKMemoryFull	;If Memful play "VP_MemoryFull"
	CALL	VP_AnswerOff
	
	RET
LOCAL_PRO0_KEYVOP_ANNOUNCEONE:

	CALL	VP_Announcement
	LACK	1
	CALL	ANNOUNCE_NUM
	
	RET
LOCAL_PRO0_KEYVOP_ANSWERONTWO:

	CALL	VP_Announcement
	LACK	2
	CALL	ANNOUNCE_NUM
	
	RET
LOCAL_PRO0_KEYVOP_PASSWORD:
	LAC	PASSWORD
	SFR	8
	ANDK	0X0F
	CALL	ANNOUNCE_NUM	;PS1
	
	LAC	PASSWORD
	SFR	4
	ANDK	0X0F
	CALL	ANNOUNCE_NUM	;PS2
	
	LAC	PASSWORD
	ANDK	0X0F
	CALL	ANNOUNCE_NUM	;PS3

	RET
LOCAL_PRO0_KEYVOP_RINGDELAY:
	LAC	VOI_ATT
	SFR	12
	ANDK	0X0F
	BS	ACZ,LOCAL_PRO0_KEYVOP_RINGDELAYTS
	CALL	ANNOUNCE_NUM	;PS3
	CALL	VP_Rings	

	RET
LOCAL_PRO0_KEYVOP_RINGDELAYTS:
	CALL	VP_TollSaver

	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_ANNOTIME:

	RET
LOCAL_PRO0_PSWORD:
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_REAALL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	CALL	VP_All
	CALL	VP_Messages
	CALL	VP_Erased	

	CALL	VPMSG_CHK
	LACL	0X6080
	CALL	DAM_BIOSFUNC
	CALL	GC_CHK

	CALL	VPMSG_CHK
;!!!
	CALL	SEND_MSGNUM	;no necessary
;!!!
	LACK	0X010
	SAH	PRO_VAR
	
	RET
;---------------------------------------
LOCAL_PRO0_VOLS:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	CALL	CLR_TIMER
	
	LAC	VOI_ATT
        ANDL	0X07
        SBHK	1		;下限
        BZ	SGN,LOCAL_PROX_VOLS

	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP	
LOCAL_PROX_VOLS:
	LACL	CMSG_VOLS
	CALL	STOR_MSG
	
	RET
LOCAL_PRO0_VOLA:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BEEP
	CALL	CLR_TIMER
	
	LAC	VOI_ATT
        ANDL	0X07
        SBHK	7		;上限
        BS	SGN,LOCAL_PROX_VOLA

	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
LOCAL_PROX_VOLA:
	LACL	CMSG_VOLA
	CALL	STOR_MSG
	
	RET
LOCAL_PRO0_RECTWA:
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK
	BIT	ANN_FG,13	;check memoful?
	BS	TB,LOCAL_PRO0_RECMSG_MFUL
	
	CALL	INIT_DAM_FUNC
	CALL	SET_COMPS
	LACK	0X0005
	SAH	PRO_VAR

	CALL	LOAD_LOCF_VR
		
	LACL	CMSG_INIT
	CALL	STOR_MSG


	RET
;---------------------------------------
LOCAL_PRO0_RECMSG:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	SET_COMPS
	LACK	0X0002
	SAH	PRO_VAR

	CALL	BEEP

	CALL	LOAD_LOCF_VR

	BIT	ANN_FG,13	;check memoful?
	BS	TB,LOCAL_PRO0_RECMSG_MFUL

	RET
LOCAL_PRO0_RECMSG_MFUL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
	CALL	VP_MemoryFull
;---!!!	
	;CALL	SEND_MSGNUM
	;CALL	EXIT_TOIDLE
;---!!!	
	
	LACK	0X0022
	SAH	PRO_VAR
	
	RET
;---------------------------------------
LOCAL_PRO0_RECOGM:
	CALL	INIT_DAM_FUNC
;---delete the old OGM fisrt------------
	LAC	OGM_ID
	CALL	OGM_SELECT
	LAC	MSG_ID
	CALL	VPMSG_DEL
	CALL	GC_CHK
;-	
	CALL	VPMSG_CHK	;check mfull
	CALL	SET_COMPS
;---LED_L "rA"
	CALL	DAA_SPK
	CALL	BEEP
	;LACL	0XAF88
	;SAH	LED_L
	
	CALL	LOAD_LOCF_VR
	
	LACK	0X0003
	SAH	PRO_VAR

	BIT	ANN_FG,13	;check memoful?
	BS	TB,LOCAL_PRO0_RECOGM_MFUL
	

	RET
LOCAL_PRO0_RECOGM_MFUL:
	CALL	INIT_DAM_FUNC
	CALL	DAA_REC
	LACK	0X020
	CALL	STOR_VP
;---!!!	
	;CALL	SEND_MSGNUM
	;CALL	EXIT_TOIDLE
;---!!!	
	
	LACK	0X0013
	SAH	PRO_VAR
	
	RET
;---------------------------------------
LOCAL_PRO0_PLYOGM:
	CALL	INIT_DAM_FUNC	;准备播放OGM
;!!!
	CALL	LOAD_LOCF_VR
;!!!
	CALL	VPMSG_CHK
	
	CALL	DAA_SPK
	LACK	0X0023
	SAH	PRO_VAR		;进入播放OGM子功能
	CALL	CLR_TIMER

	LAC	OGM_ID
	CALL	OGM_SELECT

	CALL	VP_CHKMemoryFull
	LAC	MSG_ID
	ORL	0XFE00
	CALL	STOR_VP

	LAC	OGM_ID
	CALL	OGM_SELECT
	BZ	ACZ,MAIN_PRO0_PLAY_OGM1

	CALL	INIT_DAM_FUNC
	CALL	VP_CHKMemoryFull
	CALL	VP_DefOGM1
	
	LAC	OGM_ID
	SBHK	COGM1_ID
	BS	ACZ,MAIN_PRO0_PLAY_OGM1
	
	CALL	INIT_DAM_FUNC
	CALL	VP_CHKMemoryFull
	CALL	VP_DefOGM2

MAIN_PRO0_PLAY_OGM1:
	
	RET
;---------------------------------------
LOCAL_PRO0_TMR:
	CALL	DAA_OFF

	RET
;---------------------------------------
LOCAL_PRO0_TVOP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	LACK	0X0005
	CALL	STOR_VP
	
	CALL	CLR_TIMER
	LACK	0X0006
	SAH	PRO_VAR	
	
	LACK	0
	SAH	MSG_ID
	
	RET
;---------------------------------------
LOCAL_PRO0_PLYMSG:	
LOCAL_PRO0_PLYNEW:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	VPMSG_CHK
	CALL	VP_CHKMemoryFull
	CALL	VP_YouHave
	;LACK	0X0005
	;CALL	STOR_VP

	CALL	CLR_TIMER
	LACK	1
	SAH	PRO_VAR1
	LACK	0
	SAH	MSG_ID
;---	
	CALL	LOAD_LOCF_VPN
;---
	CALL	VPMSG_CHK		;进入播放子功能
	BIT	ANN_FG,12
	BS	TB,LOCAL_PRO0_PLYNEW_NEW
	BIT	ANN_FG,14
	BS	TB,LOCAL_PRO0_PLYALL
LOCAL_PRO0_PLYNEW_NOVP:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	VP_YouHaveNoMessages
	CALL	BBEEP
;---!!!	
	;CALL	SEND_MSGNUM	;no necessary
	CALL	EXIT_TOIDLE
;---!!!
	LACK	0X0
	SAH	PRO_VAR	

	RET
;-------message exist but not new
LOCAL_PRO0_PLYNEW_NONEW:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBBEEP
;---!!!	
	;CALL	SEND_MSGNUM	;no necessary
	CALL	EXIT_TOIDLE
;---!!!
	LACK	0X0
	SAH	PRO_VAR	

	RET
;-------play new messages
LOCAL_PRO0_PLYNEW_NEW:
	LACK	0X008
	SAH	PRO_VAR	
	
	LAC	MSG_N
	SBHK	1
	BS	ACZ,LOCAL_PRO0_PLAY_ONENEW
	
	LAC	MSG_N
	CALL	ANNOUNCE_NUM
	CALL	VP_NEW
	CALL	VP_Messages
	
	RET
LOCAL_PRO0_PLAY_ONENEW:
	LAC	MSG_N
	CALL	ANNOUNCE_NUM
	CALL	VP_NEW
	CALL	VP_Message
	
	RET
;-------------------------------------------------------------------------------
LOCAL_PRO0_PLYALL:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	VPMSG_CHK
	CALL	VP_CHKMemoryFull
	CALL	VP_YouHave
	;LACK	0X0005
	;CALL	STOR_VP

	CALL	CLR_TIMER
	LACK	1
	SAH	PRO_VAR1
	LACK	0X0009
	SAH	PRO_VAR	

	LACK	0
	SAH	MSG_ID
;---	
	CALL	LOAD_LOCF_VPA
;---
	CALL	VPMSG_CHK		;进入播放子功能
	BIT	ANN_FG,14
	BZ	TB,LOCAL_PRO0_PLYNEW_NOVP
;-------play all messages
LOCAL_PRO0_PLAY_OLD:
	LAC	MSG_T
	SBHK	1
	BS	ACZ,LOCAL_PRO0_PLAY_ONEALL

	LAC	MSG_T
	CALL	ANNOUNCE_NUM
	CALL	VP_Messages
	
	RET
LOCAL_PRO0_PLAY_ONEALL:
	LAC	MSG_T
	CALL	ANNOUNCE_NUM
	CALL	VP_Message
	
	RET
;---------------------------------------
LOCAL_PRO0_PDWN:
	
	RET
LOCAL_PRO0_PUP:
	
	RET

;-------------------------------------------------------------------------------	
LOCAL_PROEND_BEFORINIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_SPK
	CALL	BBEEP
	
	LACK	0X0
	SAH	PRO_VAR

	RET

;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
LOCAL_PROVOP:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,LOCAL_PROVOP_OVER
LOCAL_PROVOP_1:	
	LAC	MSG
	XORL	CMSG_KEY6S		;ON/OFF(stop)
	BS	ACZ,LOCAL_PROVOP_STOP
	
	RET
LOCAL_PROVOP_OVER:
	CALL	INIT_DAM_FUNC

	LAC	MSG_ID
	SBHK	56
	BZ	SGN,LOCAL_PROVOP_STOP

	LAC	MSG_ID			;next message
	ADHK	1
	SAH	MSG_ID
	
	LAC	MSG_ID
	ORL	0XFF00
	CALL	STOR_VP
	
	RET
LOCAL_PROVOP_STOP:
	CALL	INIT_DAM_FUNC
	BS	B1,LOCAL_PROEND_BEFORINIT

;-------------------------------------------------------------------------------

;-------------------------------------------------------------------------------
	
.END
