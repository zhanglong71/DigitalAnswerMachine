.LIST
ANS_STATE:
	LAC	MSG
	XORL	CMSG_CPC		;接线后摘机(相当于CPC)
	BS	ACZ,ANS_STATE_CPC
	
	LAC	MSG
	XORL	CMSG_KEYAS		;接线按ON/OFF(相当于CPC)
	BS	ACZ,ANS_STATE_CPC

	LAC	PRO_VAR
	ANDK	0XF
	BS	ACZ,ANS_STATE0		;for initial
	SBHK	1
	BS	ACZ,ANS_STATE_REC	;for record(ANSWER AND RECORD ICM)
	SBHK	1
	BS	ACZ,ANS_STATE_EXIT	;for end(TimeOut/BTONE/CTONE/VOX_ON)
	SBHK	1
	BS	ACZ,ANS_STATE_LINE	;for line(ANSWER ONLY)

	RET
ANS_STATE_CPC:
	BS	B1,ICM_STATE_EXIT_END
	
;=============================================================
ANS_STATE0:
	LAC	MSG
	XORL	CMSG_INIT		;INITIAL
	BS	ACZ,ANS_STATE_INIT
;ANS_STATE0_1:	
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_STATE0_VPSTOP		;CVP_STOP,OGM播放完毕
;ANS_STATE0_2:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ANS_STATE_DTMF

	RET
;---
ANS_STATE_INIT:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	LACL	0XFFFF
	SAH	PSWORD_TMP

	BS	B1,PROOGM_LOADOGM	;此地址在lacal.asm文件里

;-------
ICM_STATE0_VPSTOP:			;开始录音(ICM)/ON_LINE(only)
	LACL	0X86	;EE
	SAH	LED3
	LACL	0X86
	SAH	LED4
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC
	CALL	BCVOX_INIT

	LACK	0
	SAH	PRO_VAR1
	LACL	1000
	CALL	SET_TIMER

	LACK	0X0001
	SAH	PRO_VAR
	
	CALL	VPMSG_CHK
	CALL	REC_START

	RET
;---------------for DTMF
ANS_STATE_DTMF:		;比较密码
	CALL	BCVOX_INIT	;有键按下BCVOX要清零
	
.if	1
	LAC	DTMF_VAL
	ANDK	0X0F
	CALL	PSWORD_CHK
	BS	ACZ,ICM_REV_DTMF_PASS
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX1_YES
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX2_YES
	SBHK	1
	BS	ACZ,ICM_REV_DTMF_MBOX3_YES
.else
	LACL	0XA1
	SAH	LED3	;'d'
	
	LAC	DTMF_VAL
	ANDK	0X0F
	CALL	DGT_TAB
	SAH	LED4
.endif	
	RET

;-------
ICM_REV_DTMF_PASS:		;密码成功
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_REV_DTMF_YES1
	
	SRAM	CONF,11
	CALL	DAM_BIOS	;若是录音过程,则删除之
ICM_REV_DTMF_YES1:	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_REC

	CALL	CLR_FUNC	;先空
	LACL	REMOTE_PRO	;进入遥控
	CALL	PUSH_FUNC

	LACL	CMSG_INIT
	CALL	STOR_MSG
	
	LACK	0
	SAH	PRO_VAR		;程序步骤
	SAH	PRO_VAR1	;计时清零
	CALL	BCVOX_INIT
	
	RET
;-------
ICM_REV_DTMF_MBOX1_YES:		;更换MBOX_ID成功
	LACK	1
	SAH	MBOX_ID
	BS	B1,ICM_REV_DTMF_MBOX_YES
ICM_REV_DTMF_MBOX2_YES:
	LACK	2
	SAH	MBOX_ID
	BS	B1,ICM_REV_DTMF_MBOX_YES
ICM_REV_DTMF_MBOX3_YES:
	LACK	3
	SAH	MBOX_ID
ICM_REV_DTMF_MBOX_YES:
;---如果是OGM5(只答不录),就不改变MBOX_ID	
	LAC	MSG_N
	SBHK	5
	BS	ACZ,ICM_REV_MBOX_YES_END
	
	LAC	CONF
	SFR	12
	SBHK	1
	BZ	ACZ,ICM_REV_MBOX_YES1
	
	SRAM	CONF,11		;若是录音过程,则删除之
	CALL	DAM_BIOS
ICM_REV_MBOX_YES1:
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	LBEEP
	
	LACK	0
   	SAH	PRO_VAR
   	
	RET
ICM_REV_MBOX_YES_END:
	LACK	1
	SAH	MBOX_ID	
	RET

ICM_REV_DTMF_CHKASTER_YES:
	CALL	INIT_DAM_FUNC
	CALL	BBEEP
	
	RET
;=============================================================
ANS_STATE_REC:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ANS_STATE_DTMF
;ANS_STATE_REC_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,ICM_TMR_DET
;ANS_STATE_REC_2:
	LAC	MSG
	XORL	CMSG_VOX		;VOX_ON 8s
	BS	ACZ,ICM_VOX_DET
;ANS_STATE_REC_3:
	LAC	MSG
	XORL	CMSG_CTONE		;CTONE 8s
	BS	ACZ,ICM_CTONE_DET
;ANS_STATE_REC_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 8s
	BS	ACZ,ICM_BTONE_DET
;ANS_STATE_REC_5:
	LAC	MSG
	XORL	CREC_FULL		;REC_FULL
	BS	ACZ,ICM_REC_FUL
;ANS_STATE_REC_6:
	LAC	MSG
	XORL	CVP_STOP		;PLAY END
	BS	ACZ,ICM_FUL_EVP
	
	RET
;-------
ICM_VOX_DET:				;由于后BTONE,CTONE,VOX要持续一段时间,可不考虑小长度的录音删除问题
ICM_CTONE_DET:
ICM_BTONE_DET:
	CALL	INIT_DAM_FUNC
	CALL	VPMSG_CHK
;---
	LACK	0X0002
	SAH	PRO_VAR
	
;	CALL	DAA_OFF
	CALL	DAA_SPK		;???????????????????
	CALL	BBEEP		;替换语音BB

	RET

;---
ICM_TMR_DET:
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	120	
	BS	SGN,ICM_TMR_DET_END
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP
	
	LACK	0X0002
	SAH	PRO_VAR

ICM_TMR_DET_END:
	RET
;-------
ICM_REC_FUL:
	CALL	INIT_DAM_FUNC
	CALL	BBBEEP		;警告语音BBB
	
	RET
ICM_FUL_EVP:
	LACK	0
	SAH	PRO_VAR1
	CALL	INIT_DAM_FUNC
	CALL	LINE_START
	
	RET
;=================================================================
ANS_STATE_EXIT:
	LAC	MSG
	XORL	CVP_STOP		
	BS	ACZ,ICM_STATE_EXIT_END	;CVP_STOP,EXIT播放完毕
	
	RET
ICM_STATE_EXIT_END:		;退出答录状态
	CALL	INIT_DAM_FUNC
	CALL	HOOK_OFF
	CALL	CLR_FUNC	;先空
   	LACL	LOCAL_PRO	;进入本地操作
     	CALL	PUSH_FUNC
	
	LACK	0
	SAH	PRO_VAR
	
	LACL	CMSG_INIT
	CALL	STOR_MSG

	RET
;========================================================================
ANS_STATE_LINE:
	LAC	MSG
	XORL	CREV_DTMF		;CREV_DTMF
	BS	ACZ,ANS_STATE_DTMF
;ANS_STATE_LINE_1:
	LAC	MSG
	XORL	CMSG_TMR		;time 1s
	BS	ACZ,LINE_TMR_DET
;ANS_STATE_LINE_2:
	LAC	MSG
	XORL	CMSG_VOX		;VOX_ON 8s
	BS	ACZ,ICM_VOX_DET
;ANS_STATE_LINE_3:
	LAC	MSG
	XORL	CMSG_CTONE		;CTONE 8s
	BS	ACZ,ICM_CTONE_DET
;ANS_STATE_LINE_4:
	LAC	MSG
	XORL	CMSG_BTONE		;BTONE 8s
	BS	ACZ,ICM_BTONE_DET

	RET
;-------
LINE_TMR_DET:		;for memful/answer only/answer off
	LAC	PRO_VAR1
	ADHK	1
	SAH	PRO_VAR1
	SBHK	3
	BS	SGN,LINE_TMR_DET_END

	LACK	0X0002
	SAH	PRO_VAR
	
	CALL	INIT_DAM_FUNC
	CALL	DAA_LIN_SPK
	CALL	BBEEP	;替换语音BB

LINE_TMR_DET_END:
	RET
	

;-------------------------------------------------------------------------------

.END
